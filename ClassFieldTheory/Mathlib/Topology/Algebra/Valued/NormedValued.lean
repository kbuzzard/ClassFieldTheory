import ClassFieldTheory.Mathlib.RingTheory.Valuation.ValuativeRel
import Mathlib.Topology.Algebra.Valued.NormedValued

namespace NormedField

variable {K : Type*} [NormedField K] [IsUltrametricDist K]

open ValuativeRel

variable (K)

/-- Given an ultrametric normed field, make a canonical `ValuativeRel` instance. This instance is
scoped to avoid instance looping. -/
def toValuativeRel : ValuativeRel K :=
  .ofValuation valuation
scoped [NormedField] attribute [instance] toValuativeRel

theorem compatible : valuation.Compatible (R := K) where
  rel_iff_le _ _ := Iff.rfl
scoped [NormedField] attribute [instance] compatible

/-- The `ValuativeRel.valuation K` coming from the `ValuativeRel` that comes from
`NormedField.valuation`, is equivalent to `NormedField.valuation` itself. -/
theorem isEquiv : (ValuativeRel.valuation K).IsEquiv valuation :=
  ValuativeRel.isEquiv _ _

variable {K}

@[simp] theorem ball_norm_eq (x : K) :
    Metric.ball 0 â€–xâ€– = { y : K | valuation y < valuation x } := by
  ext y
  simp_rw [mem_ball_zero_iff, Set.mem_setOf_eq, valuation_apply, â† NNReal.coe_lt_coe, coe_nnnorm]

theorem valuation_ball_eq (x : K) :
    (valuation (K := K)).ball (valuation x) = Metric.ball 0 â€–xâ€– := by
  ext; simp_rw [Valuation.mem_ball_iff, valuation_apply, â† NNReal.coe_lt_coe,
    coe_nnnorm, mem_ball_zero_iff]

variable (K) in
omit [IsUltrametricDist K] in
lemma trivial_or_non_trivial : (âˆ€ x : K, x = 0 âˆ¨ â€–xâ€– = 1) âˆ¨ (âˆƒ x : K, 1 < â€–xâ€–) := by
  by_contra h
  push_neg at h
  obtain âŸ¨âŸ¨x, hx0, hx1âŸ©, hkâŸ© := h
  obtain hx1 | h1x := lt_or_gt_of_ne hx1
  Â· exact absurd (hk xâ»Â¹) (not_le_of_gt <| by rwa [norm_inv, one_lt_invâ‚€ (norm_pos_iff.2 hx0)])
  Â· exact not_lt_of_ge (hk x) h1x

theorem nhds_zero_basis_norm {K : Type*} [NormedField K] :
    (nhds 0).HasBasis (fun x : K â†¦ 0 < â€–xâ€–) fun x â†¦ Metric.ball (0 : K) â€–xâ€– where
  mem_iff' s := by
    refine âŸ¨fun hs0 â†¦ ?_, fun âŸ¨x, x_pos, hxsâŸ© â†¦ ?_âŸ©
    Â· obtain trivial | âŸ¨x, h1xâŸ© := trivial_or_non_trivial K
      Â· refine âŸ¨1, by simp, fun y hy1 â†¦ ?_âŸ©
        rw [(trivial y).resolve_right (ne_of_lt (by simpa using hy1))]
        exact mem_of_mem_nhds hs0
      Â· have hix1 : â€–xâ»Â¹â€– < 1 := norm_inv x â–¸ inv_lt_one_of_one_ltâ‚€ h1x
        have ix_pos : 0 < â€–xâ»Â¹â€– := norm_inv x â–¸ inv_pos_of_pos (one_pos.trans h1x)
        rw [(Metric.nhds_basis_ball_pow ix_pos hix1).mem_iff] at hs0
        obtain âŸ¨n, -, hnsâŸ© := hs0
        refine âŸ¨(xâ»Â¹) ^ n, norm_pow xâ»Â¹ n â–¸ pow_pos ix_pos n, by rwa [norm_pow]âŸ©
    Â· exact Metric.nhds_basis_ball.mem_iff.2 âŸ¨_, x_pos, fun y hy â†¦ hxs (by simpa using hy)âŸ©

theorem _root_.DiscreteTopology.of_trivial_norm (trivial : âˆ€ x : K, x = 0 âˆ¨ â€–xâ€– = 1) :
    DiscreteTopology K :=
  DiscreteTopology.of_forall_le_norm one_pos fun x hx â†¦ by rw [(trivial x).resolve_left hx]

section IsValuativeTopology

open NormedField Valued ValuativeRel

def valuativeRel (K : Type*) [NormedField K] [IsUltrametricDist K] : ValuativeRel K :=
  .ofValuation valuation
attribute [local instance] valuativeRel

@[simp] theorem _root_.NormedDivisionRing.norm_lt_one_iff_eq_zero_of_discrete
    {ð•œ : Type*} [NormedDivisionRing ð•œ] [DiscreteTopology ð•œ] (x : ð•œ) :
    â€–xâ€– < 1 â†” x = 0 := by
  simp [lt_iff_le_and_ne, NormedDivisionRing.norm_le_one_of_discrete x,
    NormedDivisionRing.norm_eq_one_iff_ne_zero_of_discrete]

theorem isValuativeTopology (K : Type*) [NormedField K] [IsUltrametricDist K] :
    IsValuativeTopology K where
  mem_nhds_iff {s x} := by
    have e := ValuativeRel.isEquiv (ValuativeRel.valuation K) valuation
    obtain _ | âŸ¨_, rflâŸ© := NormedField.discreteTopology_or_nontriviallyNormedField K
    Â· rw [mem_nhds_discrete]
      refine âŸ¨fun hxs â†¦ ?_, fun âŸ¨Î³, hÎ³âŸ© â†¦ ?_âŸ©
      Â· refine âŸ¨1, by simpa [e.lt_one_iff_lt_one, â† NNReal.coe_lt_coe]âŸ©
      exact hÎ³ âŸ¨0, by simpâŸ©
    Â· obtain âŸ¨z, h0z, hz1âŸ© := NormedField.exists_norm_lt_one K
      refine âŸ¨fun hsx â†¦ ?_, fun âŸ¨Î³, hÎ³âŸ© â†¦ ?_âŸ©
      Â· rw [(Metric.nhds_basis_ball_pow h0z hz1).mem_iff] at hsx
        obtain âŸ¨n, -, hnâŸ© := hsx
        refine âŸ¨Units.mk0 (ValuativeRel.valuation K z) (by simpa using h0z) ^ n, ?_âŸ©
        convert hn
        ext
        simp [â† map_pow, e.lt_iff_lt, â† NNReal.coe_lt_coe, dist_eq_norm, sub_eq_neg_add]
      Â· obtain âŸ¨y, rflâŸ© := unitsMap_valuation_surjective Î³
        refine Metric.mem_nhds_iff.mpr âŸ¨â€–y.valâ€–, by simp, ?_âŸ©
        convert hÎ³
        ext
        simp [e.lt_iff_lt, â† NNReal.coe_lt_coe, dist_eq_norm, sub_eq_neg_add]

end IsValuativeTopology

theorem isNontrivial (K : Type*) [NontriviallyNormedField K] [IsUltrametricDist K] :
    ValuativeRel.IsNontrivial K := by
  obtain âŸ¨x, hxâŸ© := NontriviallyNormedField.non_trivial (Î± := K)
  refine âŸ¨âŸ¨ValuativeRel.valuation K x, ?_, ?_âŸ©âŸ©
  Â· rw [Valuation.ne_zero_iff]; exact norm_pos_iff.1 (one_pos.trans hx)
  Â· exact ne_of_gt <| by rwa [(isEquiv K).one_lt_iff_one_lt, valuation_apply,
      â† NNReal.coe_lt_coe, NNReal.coe_one, coe_nnnorm]

scoped [NormedField] attribute [instance] isValuativeTopology isNontrivial
