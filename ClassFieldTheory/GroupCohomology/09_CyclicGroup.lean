import ClassFieldTheory.GroupCohomology.¬´06_LeftRegular¬ª
import ClassFieldTheory.GroupCohomology.¬´07_coind1_and_ind1¬ª
import ClassFieldTheory.GroupCohomology.¬´08_DimensionShift¬ª
import ClassFieldTheory.Mathlib.Algebra.Homology.ImageToKernel
import ClassFieldTheory.Mathlib.CategoryTheory.Abelian.Exact
import ClassFieldTheory.Mathlib.ModuleCatExact
import ClassFieldTheory.Mathlib.CategoryTheory.Abelian.Images

/-!
Let `M : Rep R G`, where `G` is a finite cyclic group.
We construct an exact sequence

  `0 ‚ü∂ M ‚ü∂ coind‚ÇÅ'.obj M ‚ü∂ ind‚ÇÅ'.obj M ‚ü∂ M ‚ü∂ 0`.

Using this sequence, we construct an isomorphism

  `dimensionShift.up.obj M ‚âÖ dimensionShift.down.obj M`.

Using this, construct isomorphisms

  `H‚Åø‚Å∫¬π(G,M) ‚âÖ H‚Åø‚Å∫¬≥(G,M)`.

-/

open
  Finsupp
  Rep
  leftRegular
  dimensionShift
  CategoryTheory
  Abelian
  ConcreteCategory
  Limits
  BigOperators
  groupCohomology

-- TODO: add universes
variable {R : Type} [CommRing R]
variable (G : Type) [Group G] [instCyclic : IsCyclic G]
variable (M : Rep R G)

noncomputable section

namespace IsCyclic
/--
`gen G` is a generator of the cyclic group `G`.
-/
def gen : G := IsCyclic.exists_generator.choose

variable {G} in
lemma gen_generate (x : G) : x ‚àà Subgroup.zpowers (gen G) :=
  IsCyclic.exists_generator.choose_spec x

theorem unique_gen_zpow_zmod [Fintype G] (x : G) :
    ‚àÉ! n : ZMod (Fintype.card G), x = gen G ^ n.val :=
  IsCyclic.unique_zpow_zmod gen_generate x

theorem unique_gen_pow [Fintype G] (x : G) :
    ‚àÉ! n < Fintype.card G, x = gen G ^ n := by
  obtain ‚ü®k, hk, hk_unique‚ü© := unique_gen_zpow_zmod G x
  refine ‚ü®k.val, ‚ü®‚ü®ZMod.val_lt _, hk‚ü©, ?_‚ü©‚ü©
  intro y ‚ü®hy_lt, hy‚ü©
  rw [‚Üê hk_unique y]
  ¬∑ rw [ZMod.val_natCast, Nat.mod_eq_of_lt hy_lt]
  ¬∑ simp [hy]

end IsCyclic

open IsCyclic

variable {G} [Fintype G] (M : Rep R G)

@[simp] lemma ofHom_sub (A B : ModuleCat R) (f‚ÇÅ f‚ÇÇ : A ‚Üí‚Çó[R] B) :
  (ofHom (f‚ÇÅ - f‚ÇÇ) : A ‚ü∂ B) = ofHom f‚ÇÅ - ofHom f‚ÇÇ := rfl

@[simp] lemma ofHom_add (A B : ModuleCat R) (f‚ÇÅ f‚ÇÇ : A ‚Üí‚Çó[R] B) :
  (ofHom (f‚ÇÅ + f‚ÇÇ) : A ‚ü∂ B) = ofHom f‚ÇÅ + ofHom f‚ÇÇ := rfl

@[simp] lemma ofHom_zero (A B : ModuleCat R) :
  (ofHom 0 : A ‚ü∂ B) = 0 := rfl

@[simp] lemma ofHom_one (A : ModuleCat R) :
  (ofHom 1 : A ‚ü∂ A) = ùüô A := rfl

omit [IsCyclic G] [Fintype G] in
@[simp] lemma Rep.œÅ_mul_eq_comp (M : Rep R G) (x y : G) :
    Action.œÅ M (x * y) = (Action.œÅ M y) ‚â´ (Action.œÅ M x) := map_mul (Action.œÅ M) x y

namespace Representation

variable {A : Type} [AddCommGroup A] [Module R A] (œÅ : Representation R G A)

omit [Fintype G] --[DecidableEq G]

@[simps] def map‚ÇÅ : (G ‚Üí A) ‚Üí‚Çó[R] (G ‚Üí A) where
  toFun f x := f x - f ((gen G)‚Åª¬π * x)
  map_add' _ _ := by
    ext; simp [add_sub_add_comm]
  map_smul' _ _ := by
    ext; simp [‚Üê smul_sub]

lemma map‚ÇÅ_comm (g : G) :
    map‚ÇÅ ‚àò‚Çó œÅ.coind‚ÇÅ' g = œÅ.coind‚ÇÅ' g ‚àò‚Çó map‚ÇÅ  := by
  apply LinearMap.ext
  intro
  apply funext
  intro
  simp [mul_assoc]

lemma map‚ÇÅ_comp_coind_Œπ :
    map‚ÇÅ (R := R) (G := G) (A := A) ‚àò‚Çó coind‚ÇÅ'_Œπ = 0 := by
  ext; simp

lemma map‚ÇÅ_ker :
    LinearMap.ker (map‚ÇÅ (R := R) (G := G) (A := A)) = LinearMap.range coind‚ÇÅ'_Œπ := by
  ext f
  constructor
  ¬∑ intro hf
    rw [LinearMap.mem_ker] at hf
    simp only [coind‚ÇÅ'_Œπ, LinearMap.mem_range, LinearMap.coe_mk, AddHom.coe_mk, ] at hf ‚ä¢
    use f (gen G)‚Åª¬π
    ext x
    obtain ‚ü®n, hx‚ü© : x ‚àà Subgroup.zpowers (gen G) := IsCyclic.exists_generator.choose_spec x
    dsimp at hx
    rw [Function.const_apply, ‚Üê hx]
    dsimp [map‚ÇÅ] at hf
    induction n generalizing x with
    | zero =>
        rw [zpow_zero]
        have := congr_fun hf 1
        simp only [mul_one, Pi.zero_apply] at this
        rw [sub_eq_zero] at this
        exact this.symm
    | succ n hn =>
      have := congr_fun hf ((gen G ^ ((n : ‚Ñ§) + 1)))
      simp only [Pi.zero_apply, sub_eq_zero] at this
      rw [this, hn _ rfl]
      group
    | pred n hn =>
      have := congr_fun hf ((gen G ^ (- (n : ‚Ñ§))))
      simp only [Pi.zero_apply, sub_eq_zero] at this
      rw [zpow_sub_one (gen G) _, hn _ rfl, this]
      group
  ¬∑ rintro ‚ü®_, rfl‚ü©
    exact LinearMap.congr_fun map‚ÇÅ_comp_coind_Œπ _

@[simps!] def map‚ÇÇ : (G ‚Üí‚ÇÄ A) ‚Üí‚Çó[R] (G ‚Üí‚ÇÄ A) :=
  LinearMap.id - lmapDomain _ _ (fun x ‚Ü¶ gen G * x)

omit [Fintype G] in
lemma map‚ÇÇ_apply (f : G ‚Üí‚ÇÄ A) (x : G) :
    Representation.map‚ÇÇ (R := R) f x = f x - f ((gen G)‚Åª¬π * x) := by
  simp [Representation.map‚ÇÇ]
  convert Finsupp.mapDomain_apply ?_ _ ((gen G)‚Åª¬π * x)
  ¬∑ simp
  ¬∑ intro x y h
    simpa using h

omit [Fintype G] in
@[simp] lemma map‚ÇÇ_comp_lsingle (x : G) :
    map‚ÇÇ (R := R) (G := G) (A := A) ‚àò‚Çó lsingle x = lsingle x - lsingle (gen G * x) := by
  ext
  simp [map‚ÇÇ, LinearMap.sub_comp]

lemma map‚ÇÇ_comm (g : G) :
    map‚ÇÇ ‚àò‚Çó œÅ.ind‚ÇÅ' g = œÅ.ind‚ÇÅ' g ‚àò‚Çó map‚ÇÇ := by
  ext x : 1
  rw [LinearMap.comp_assoc, ind‚ÇÅ'_comp_lsingle, LinearMap.comp_assoc, map‚ÇÇ_comp_lsingle,
    LinearMap.comp_sub, ind‚ÇÅ'_comp_lsingle, ‚ÜêLinearMap.comp_assoc, map‚ÇÇ_comp_lsingle,
    LinearMap.sub_comp, ind‚ÇÅ'_comp_lsingle, mul_assoc]

lemma ind‚ÇÅ'_œÄ_comp_map‚ÇÇ :
    ind‚ÇÅ'_œÄ ‚àò‚Çó map‚ÇÇ (R := R) (G := G) (A := A) = 0 := by
  ext : 1
  rw [LinearMap.comp_assoc, map‚ÇÇ_comp_lsingle, LinearMap.comp_sub,
    LinearMap.zero_comp, sub_eq_zero, ind‚ÇÅ'_œÄ_comp_lsingle, ind‚ÇÅ'_œÄ_comp_lsingle]

lemma map‚ÇÇ_range [Fintype G] [DecidableEq G] :
    LinearMap.range (map‚ÇÇ (R := R) (G := G) (A := A)) = LinearMap.ker ind‚ÇÅ'_œÄ := by
  ext w
  constructor
  ¬∑ rintro ‚ü®y, rfl‚ü©
    have := ind‚ÇÅ'_œÄ_comp_map‚ÇÇ (R := R) (G := G) (A := A)
    apply_fun (¬∑ y) at this
    exact this
  ¬∑ intro hw_ker
    let f : G ‚Üí A := fun g ‚Ü¶ ‚àë i ‚àà Finset.Icc 0 (unique_gen_pow G g).choose, w (gen G ^ i)
    have hf_apply (k : ‚Ñ§) : f (gen G ^ k) = ‚àë i ‚àà Finset.Icc 0 (k.natMod (Fintype.card G)),
        w (gen G ^ i) := by
      simp only [f]
      congr
      rw [((unique_gen_pow G (gen G ^ k)).choose_spec.right (k.natMod (Fintype.card G))
        ‚ü®?_, ?_‚ü©).symm]
      ¬∑ exact  Int.natMod_lt Fintype.card_ne_zero
      ¬∑ simp [‚Üê zpow_natCast, Int.natMod, Int.ofNat_toNat, Int.emod_nonneg]
    have hf_apply_of_lt (k : ‚Ñï) (hk : k < Fintype.card G) :
        f (gen G ^ k) = ‚àë i ‚àà Finset.Icc 0 k, w (gen G ^ i) := by
      convert hf_apply k
      ¬∑ simp
      ¬∑ zify
        rw [Int.natMod, Int.toNat_of_nonneg, Int.emod_eq_of_lt]
        ¬∑ positivity
        ¬∑ exact_mod_cast hk
        ¬∑ apply Int.emod_nonneg
          simp
    use equivFunOnFinite.symm f
    ext g
    rw [map‚ÇÇ_apply]
    change f g - f ((gen G)‚Åª¬π * g) = w g
    obtain ‚ü®k, ‚ü®hk_lt, rfl‚ü©, hk_unique‚ü© := unique_gen_pow G g
    by_cases hk : k = 0
    ¬∑ rw [hk, hf_apply_of_lt, pow_zero, mul_one]
      ¬∑ have : (gen G)‚Åª¬π = gen G ^ (Fintype.card G - 1 : ‚Ñï) := by
          rw [inv_eq_iff_mul_eq_one, ‚Üê pow_succ', Nat.sub_add_cancel Fintype.card_pos,
            pow_card_eq_one]
        rw [this, hf_apply_of_lt]
        ¬∑ simp only [Finset.Icc_self, Finset.sum_singleton, pow_zero, sub_eq_self]
          calc
          _ = ‚àë i ‚àà Finset.Ico 0 (Fintype.card G), w (gen G ^ i) := by
            congr
            apply Finset.Icc_sub_one_right_eq_Ico_of_not_isMin
            rw [isMin_iff_forall_not_lt]
            push_neg
            use 0, Fintype.card_pos
          _ = ‚àë x ‚àà (Finset.Ico 0 (Fintype.card G)).image (gen G ^ ¬∑), w x := by
            rw [Finset.sum_image]
            intro x hx y hy h
            simp only [Nat.Ico_zero_eq_range, Finset.coe_range, Set.mem_Iio] at hx hy h
            simp only at hk_unique
            have := (unique_gen_pow G (gen G ^ x)).choose_spec.right
            rw [this x, this y]
            ¬∑ simp only [hy, h, and_self]
            ¬∑ simp only [hx, and_self]
          _ = ‚àë x ‚àà (Finset.univ : Finset G), w x := by
            congr
            rw [Finset.eq_univ_iff_forall]
            intro x
            simp only [Nat.Ico_zero_eq_range, Finset.mem_image, Finset.mem_range]
            obtain ‚ü®a, ha, ha'‚ü© := unique_gen_pow G x
            use a, ha.left, ha.right.symm
          _ = 0 := by
            simpa [Finsupp.sum_fintype] using hw_ker
        ¬∑ simpa using Fintype.card_pos
      ¬∑ exact Fintype.card_pos
    ¬∑ rw [‚Üê zpow_neg_one, hf_apply_of_lt, ‚Üê zpow_natCast, ‚Üê zpow_add, neg_add_eq_sub,
        show (k : ‚Ñ§) - 1 = (k - 1 : ‚Ñï) by omega, zpow_natCast, hf_apply_of_lt]
      ¬∑ nth_rw 1 [show k = k - 1 + 1 by omega]
        rw [Finset.sum_Icc_succ_top, add_sub_cancel_left, zpow_natCast,
          Nat.sub_add_cancel (by omega)]
        omega
      all_goals omega

end Representation

namespace Rep

/--
The map `coind‚ÇÅ'.obj M ‚ü∂ coind‚ÇÅ' M` which takes a function `f : G ‚Üí M.V` to
`x ‚Ü¶ f x - f ((gen G)‚Åª¬π * x)`.
-/
def map‚ÇÅ : coind‚ÇÅ' (R := R) (G := G) ‚ü∂ coind‚ÇÅ' where
  app M := {
    hom := ofHom Representation.map‚ÇÅ
    comm _ := by
      ext : 1
      exact Representation.map‚ÇÅ_comm _ _
  }
  naturality _ _ _ := by
    ext v
    dsimp only [Representation.map‚ÇÅ, coind‚ÇÅ']
    ext x
    simp

omit [Fintype G] in
lemma coind_Œπ_gg_map‚ÇÅ_app : coind‚ÇÅ'_Œπ.app M ‚â´ map‚ÇÅ.app M = 0 := by
  ext : 2
  exact Representation.map‚ÇÅ_comp_coind_Œπ

omit [Fintype G] in
lemma coind_Œπ_gg_map‚ÇÅ : coind‚ÇÅ'_Œπ ‚â´ map‚ÇÅ (R := R) (G := G) = 0 := by
  ext : 2
  exact coind_Œπ_gg_map‚ÇÅ_app _

def map‚ÇÇ : ind‚ÇÅ' (R := R) (G := G) ‚ü∂ ind‚ÇÅ' where
  app M := {
    hom := ofHom Representation.map‚ÇÇ
    comm g := by
      ext : 1
      exact Representation.map‚ÇÇ_comm _ _
  }
  naturality X Y f:= by
    ext (w : G ‚Üí‚ÇÄ X.V)
    simp only [Action.comp_hom, ModuleCat.hom_comp, ModuleCat.hom_ofHom, LinearMap.coe_comp,
      Function.comp_apply]
    change (_ : G ‚Üí‚ÇÄ _) = _
    ext g
    simp [ind‚ÇÅ', Representation.map‚ÇÇ_apply, -Representation.map‚ÇÇ_apply_toFun]

omit [Fintype G] in
lemma map‚ÇÇ_app_gg_ind‚ÇÅ'_œÄ_app :  map‚ÇÇ.app M ‚â´ ind‚ÇÅ'_œÄ.app M = 0 := by
  ext : 2
  exact Representation.ind‚ÇÅ'_œÄ_comp_map‚ÇÇ

omit [Fintype G] in
lemma map‚ÇÇ_gg_ind‚ÇÅ'_œÄ : map‚ÇÇ (R := R) (G := G) ‚â´ ind‚ÇÅ'_œÄ = 0 := by
  ext : 2
  exact map‚ÇÇ_app_gg_ind‚ÇÅ'_œÄ_app _

/--
Let `M` be a representation of a finite cyclic group `G`.
Then the following square commutes

  ` coind‚ÇÅ'.obj M -------> coind‚ÇÅ'.obj M `
  `      |                      |        `
  `      |                      |        `
  `      ‚Üì                      ‚Üì        `
  `   ind‚ÇÅ'.obj M ------->   ind‚ÇÅ'.obj M `

The vertical maps are the canonical isomorphism `ind‚ÇÅ'_iso_coind‚ÇÅ`
and the horizontal maps are `map‚ÇÅ` and `map‚ÇÇ`.
-/
lemma map‚ÇÅ_comp_ind‚ÇÅ'_iso_coind‚ÇÅ' :
    map‚ÇÅ.app M ‚â´ (ind‚ÇÅ'_iso_coind‚ÇÅ'.app M).inv = (ind‚ÇÅ'_iso_coind‚ÇÅ'.app M).inv ‚â´ map‚ÇÇ.app M := by
  ext x
  simp [coind‚ÇÅ', ind‚ÇÅ'] at x ‚ä¢
  ext d
  simp only [ind‚ÇÅ'_iso_coind‚ÇÅ', Representation.ind‚ÇÅ'_lequiv_coind‚ÇÅ', linearEquivFunOnFinite,
    Equiv.invFun_as_coe, ModuleCat.hom_ofHom, map‚ÇÅ, Representation.map‚ÇÅ, LinearMap.coe_mk,
    AddHom.coe_mk, LinearEquiv.coe_coe, LinearEquiv.coe_symm_mk, equivFunOnFinite_symm_apply_toFun,
    map‚ÇÇ, Representation.map‚ÇÇ_apply]

/-- The first short complex in the periodicity sequence. -/
@[simps] def periodSeq‚ÇÅ : ShortComplex (Rep R G) where
  X‚ÇÅ := M
  X‚ÇÇ := coind‚ÇÅ'.obj M
  X‚ÇÉ := ind‚ÇÅ'.obj M
  f := coind‚ÇÅ'_Œπ.app M
  g := map‚ÇÅ.app M ‚â´ (ind‚ÇÅ'_iso_coind‚ÇÅ'.app M).inv
  zero := by simp [reassoc_of% coind_Œπ_gg_map‚ÇÅ_app]

/-- The second short complex in the periodicity sequence. -/
@[simps] def periodSeq‚ÇÇ : ShortComplex (Rep R G) where
  X‚ÇÅ := coind‚ÇÅ'.obj M
  X‚ÇÇ := ind‚ÇÅ'.obj M
  X‚ÇÉ := M
  f := map‚ÇÅ.app M ‚â´ (ind‚ÇÅ'_iso_coind‚ÇÅ'.app M).inv
  g := ind‚ÇÅ'_œÄ.app M
  zero := by
    rw [ Category.assoc, reassoc_of% map‚ÇÅ_comp_ind‚ÇÅ'_iso_coind‚ÇÅ']; simp [map‚ÇÇ_app_gg_ind‚ÇÅ'_œÄ_app]

/-- The first short complex in the periodicity sequence as a functor. -/
@[simps] def periodSeq‚ÇÅFunctor : Rep R G ‚•§ ShortComplex (Rep R G) where
  obj := periodSeq‚ÇÅ
  map {M N} f := ShortComplex.homMk f (coind‚ÇÅ'.map f) (ind‚ÇÅ'.map f) (by cat_disch) (by cat_disch)

/-- The second short complex in the periodicity sequence as a functor. -/
@[simps] def periodSeq‚ÇÇFunctor : Rep R G ‚•§ ShortComplex (Rep R G) where
  obj := periodSeq‚ÇÇ
  map {M N} f := ShortComplex.homMk (coind‚ÇÅ'.map f) (ind‚ÇÅ'.map f) f (by cat_disch) (by cat_disch)

lemma exact_periodSeq‚ÇÅ : (periodSeq‚ÇÅ M).Exact := by
  -- `S` is `ShortComplex (Rep R G)` here, but `Rep R G` is equivalent to `ModuleCat R[G]`.
  -- This step transfers our task to exactness in `ModuleCat R[G]`.
  apply Functor.reflects_exact_of_faithful equivalenceModuleMonoidAlgebra.functor
  -- A sequence of `R`-modules is exact if `LinearMap.range _ = LinearMap.ker _`
  -- In fact, `range ‚â§ ker` in complexes, so we just need `ker ‚â§ range`.
  apply ShortComplex.Exact.moduleCat_of_ker_le_range
  simp [equivalenceModuleMonoidAlgebra, toModuleMonoidAlgebra,
    toModuleMonoidAlgebraMap, ModuleCat.hom_ofHom]
  -- Now, we get `w` with `w ‚àà ker`.
  intro (w : G ‚Üí M.V) hw
  simp only [LinearMap.mem_range, LinearMap.coe_mk]
  change w ‚àà LinearMap.range Representation.coind‚ÇÅ'_Œπ
  simpa [‚Üê Representation.map‚ÇÅ_ker] using (LinearEquiv.symm_apply_eq _).mp hw

lemma exact_periodSeq‚ÇÇ : (periodSeq‚ÇÇ M).Exact := by
  classical
  apply Functor.reflects_exact_of_faithful equivalenceModuleMonoidAlgebra.functor
  apply ShortComplex.Exact.moduleCat_of_ker_le_range
  simp [equivalenceModuleMonoidAlgebra, toModuleMonoidAlgebra, toModuleMonoidAlgebraMap,
    ModuleCat.hom_ofHom]
  intro w hw_ker
  change w ‚àà LinearMap.ker (Representation.ind‚ÇÅ'_œÄ (R := R)) at hw_ker
  rw [‚Üê Representation.map‚ÇÇ_range] at hw_ker
  obtain ‚ü®y, rfl‚ü© := hw_ker
  use (y : G ‚Üí M.V)
  change (linearEquivFunOnFinite ..).symm (Representation.map‚ÇÅ y) = Representation.map‚ÇÇ (R := R) y
  ext w
  rw [Representation.map‚ÇÇ_apply]
  simp [linearEquivFunOnFinite]

/-- `up M` is the cokernel of the first map in the periodicity sequence,
so is isomorphic to the coimage of the second map. -/
@[simps!]
def upIsoCoimagePeriodSeq‚ÇÅ : up.obj M ‚âÖ Abelian.coimage (periodSeq‚ÇÅ M).g :=
  have := (exact_periodSeq‚ÇÅ M).epi_kernelLift
  cokernel.congr _ _ (by simp) ‚â™‚â´ cokernelEpiComp (kernel.lift _ _ (periodSeq‚ÇÅ M).zero) _

/-- `down M` is the kernel of the third map in the periodicity sequence,
so is isomorphic to the image of the second map. -/
def downIsoImagePeriodSeq‚ÇÇ : down.obj M ‚âÖ Abelian.image (periodSeq‚ÇÇ M).f :=
  have := (exact_periodSeq‚ÇÇ M).mono_cokernelDesc
  kernel.congr _ _ (by simp) ‚â™‚â´ kernelCompMono _ (cokernel.desc _ _ (periodSeq‚ÇÇ M).zero)

/-- The up functor is isomorphic to the first periodicity sequence functor composed with the coimage
functor. -/
def upIsoCoimagePeriodSeq‚ÇÅFunctor :
    up (R := R) (G := G) ‚âÖ periodSeq‚ÇÅFunctor ‚ãô ShortComplex.gFunctor ‚ãô coimageFunctor :=
  NatIso.ofComponents upIsoCoimagePeriodSeq‚ÇÅ <| fun f => by
    simp [upIsoCoimagePeriodSeq‚ÇÅ]
    sorry

/-- The down functor is isomorphic to the second periodicity sequence functor composed with the
image functor. -/
def downIsoImagePeriodSeq‚ÇÇFunctor :
    down (R := R) (G := G) ‚âÖ periodSeq‚ÇÇFunctor ‚ãô ShortComplex.fFunctor ‚ãô imageFunctor :=
  NatIso.ofComponents downIsoImagePeriodSeq‚ÇÇ sorry

/-- The up and down functors for a finite cyclic group are pointwise isomorphic. -/
@[simps! hom inv]
def upIsoDownObj : up.obj M ‚âÖ down.obj M := calc
    up.obj M
      ‚âÖ Abelian.coimage (periodSeq‚ÇÅ M).g := upIsoCoimagePeriodSeq‚ÇÅ _
    _ ‚âÖ Abelian.image (periodSeq‚ÇÇ M).f := Abelian.coimageIsoImage _
    _ ‚âÖ down.obj M := (downIsoImagePeriodSeq‚ÇÇ _).symm

/-- The up and down functors for a finite cyclic group are naturally isomorphic. -/
def upIsoDown : up (R := R) (G := G) ‚âÖ down :=
  upIsoCoimagePeriodSeq‚ÇÅFunctor ‚â™‚â´
    NatIso.ofComponents (fun M ‚Ü¶ Abelian.coimageIsoImage _) sorry
    ‚â™‚â´ downIsoImagePeriodSeq‚ÇÇFunctor.symm

def periodicCohomology (n : ‚Ñï) :
    functor R G (n + 1) ‚âÖ functor R G (n + 3) := by
  apply Iso.trans (down_Œ¥iso_natTrans n)
  apply Iso.trans (Functor.isoWhiskerRight upIsoDown.symm _)
  exact up_Œ¥iso_natTrans _

def periodicCohomology' (n m : ‚Ñï) :
    functor R G (n + 1) ‚âÖ functor R G (n + 1 + 2 * m) := by
  induction m with
  | zero =>
    apply Iso.refl
  | succ m ih =>
    apply Iso.trans ih
    rw [mul_add, mul_one, ‚Üêadd_assoc, add_assoc, add_comm 1, ‚Üêadd_assoc]
    exact periodicCohomology _

def periodicCohomology_mod2 (m n : ‚Ñï) (h : m ‚â° n [MOD 2]) :
    functor R G (m + 1) ‚âÖ functor R G (n + 1) :=
  if hLe : m ‚â§ n then
    (periodicCohomology' m ((n - m) /2)).trans <| eqToIso (by grind [Nat.modEq_iff_dvd])
  else
   (eqToIso (by grind [Nat.modEq_iff_dvd])).trans (periodicCohomology' n ((m - n) /2)).symm

/--
Let `M` be a representation of a finite cyclic group `G`. Suppose there are even
and positive integers `e` and `o` with `e` even and `o` odd, such that
`H·µâ(G,M)` and `H·µí(G,M)` are both zero.
Then `H‚Åø(G,M)` is zero for all `n > 0`.
-/
lemma isZero_ofEven_Odd {M : Rep R G} {a b : ‚Ñï}
    (h‚Çë : IsZero (groupCohomology M (2 * a + 2)))
    (h‚Çí : IsZero (groupCohomology M (2 * b + 1))) (n : ‚Ñï) :
    IsZero (groupCohomology M (n + 1)) := by
  obtain hn | hn := n.even_or_odd
  ¬∑ refine .of_iso h‚Çí <| (periodicCohomology_mod2 n (2 * b) ?_).app M
    grind [Nat.modEq_iff_dvd]
  ¬∑ refine .of_iso h‚Çë <| (periodicCohomology_mod2 n (2 * a + 1) ?_).app M
    grind [Nat.modEq_iff_dvd]

end Rep

include instCyclic in
def periodicTateCohomology (n : ‚Ñ§) :
    tateCohomology (R := R) (G := G) n ‚âÖ tateCohomology (n + 2) :=
  sorry

variable {n : ‚Ñ§} {N : ‚Ñï} {G : Type} [Group G] [IsCyclic G] [Fintype G] {M : Rep ‚Ñ§ G} [M.IsTrivial]

namespace TateCohomology

/-- The even Tate cohomology of a trivial representation of a finite cyclic group of order `N` is
`‚Ñ§/N‚Ñ§`. -/
def evenTrivialInt [IsCyclic G] (hG : Nat.card G = N) (hn : Even n) :
    (tateCohomology n).obj (trivial ‚Ñ§ G ‚Ñ§) ‚âÖ .of ‚Ñ§ (ZMod N) := sorry

/-- A trivial torsion-free representation of a finite cyclic group has trivial odd Tate cohomology.
-/
lemma isZero_odd_trivial_of_isAddTorsionFree {M : Type} [AddCommGroup M] [IsAddTorsionFree M]
    (hn : Odd n) : IsZero ((tateCohomology n).obj <| trivial ‚Ñ§ G M) := sorry

end TateCohomology

namespace groupCohomology

/-- The nonzero even group cohomology of a trivial representation of a finite cyclic group of order
`N` is `‚Ñ§/N‚Ñ§`. -/
def evenTrivialInt {n : ‚Ñï} (hG : Nat.card G = N) (hn : Even n) (hn‚ÇÄ : n ‚â† 0) :
    groupCohomology (trivial ‚Ñ§ G ‚Ñ§) n ‚âÖ .of ‚Ñ§ (ZMod N) := by
  obtain _ | n := n
  ¬∑ simp at hn‚ÇÄ
  exact .trans ((TateCohomology.isoGroupCohomology n).app _).symm <|
    TateCohomology.evenTrivialInt hG (mod_cast hn)

/-- A trivial torsion-free representation of a finite cyclic group has trivial odd group
cohomology. -/
lemma isZero_odd_trivial_of_isAddTorsionFree {n : ‚Ñï} {M : Type} [AddCommGroup M]
    [IsAddTorsionFree M] (hn : Odd n) : IsZero (groupCohomology (trivial ‚Ñ§ G M) n) := by
  obtain _ | n := n
  ¬∑ simp at hn
  exact .of_iso (TateCohomology.isZero_odd_trivial_of_isAddTorsionFree <| mod_cast hn) <|
    (TateCohomology.isoGroupCohomology n).symm.app _

end groupCohomology

namespace groupHomology

/-- The odd group homology of a trivial representation of a finite cyclic group of order `N` is
`‚Ñ§/N‚Ñ§`. -/
def oddTrivialInt {n : ‚Ñï} (hG : Nat.card G = N) (hn : Odd n) :
    groupHomology (trivial ‚Ñ§ G ‚Ñ§) n ‚âÖ .of ‚Ñ§ (ZMod N) := by
  obtain _ | n := n
  ¬∑ simp at hn
  exact .trans ((TateCohomology.isoGroupHomology n).app _).symm <|
    TateCohomology.evenTrivialInt hG <| by rw [‚Üê neg_add', even_neg]; exact mod_cast hn.add_one

/-- A trivial torsion-free representation of a finite cyclic group has trivial nonzero even group
homology. -/
lemma isZero_even_trivial_of_isAddTorsionFree {n : ‚Ñï} {M : Type} [AddCommGroup M]
    [IsAddTorsionFree M] (hn : Even n) (hn‚ÇÄ : n ‚â† 0) :
    IsZero (groupHomology (trivial ‚Ñ§ G M) n) := by
  obtain _ | n := n
  ¬∑ simp at hn‚ÇÄ
  exact .of_iso (TateCohomology.isZero_odd_trivial_of_isAddTorsionFree <| by
    rw [‚Üê neg_add', odd_neg]; exact mod_cast hn.add_one) <|
    (TateCohomology.isoGroupHomology n).symm.app _

end groupHomology
