import ClassFieldTheory.Cohomology.Functors.Restriction

/-!
In this file we have a group homomorphism `Ï† : G â†’* Q` satisfying the condition

  `surj : Function.Surjective Ï†`.

From this, we define a functor

  `Rep.quotientToInvariantsFunctor' surj : Rep R G â¥¤ Rep R Q`,

which takes a representation `M` of `G` to the subspace of vectors which are fixed by `Ï†.ker`,
with the obvious action of `Q`.

We use the abbreviation `M â†‘ surj` for `(Rep.quotientToInvariantsFunctor' surj).obj M`.

We define the natural map of `G`-representations

  `((M â†‘ surj) â†“ Ï†) âŸ¶ M`.

Using this map, we define the inflation map as a morphism of functors

  `groupCohomology.cochain_infl : quotientToInvariantsFunctor' surj â‹™ cochainsFunctor R Q âŸ¶ cochainsFunctor R G`.

Using this we define the inflation map on group cohomology:

  `infl (n : â„•) : Rep.quotientToInvariantsFunctor' surj â‹™ functor R Q n âŸ¶ functor R G n`

Since this is defined on cochains first, we are able to deduce `Î´`-naturality of the inflation map
on cohomology.
-/

open CategoryTheory
  ConcreteCategory
  Limits
  Rep
  groupCohomology
  HomologicalComplex

variable {R G : Type} [CommRing R] [Group G]

variable {Q : Type} [Group Q] {Ï† : G â†’* Q} (surj : Function.Surjective Ï†)

namespace Rep

@[simps! (isSimp := false) obj_V] noncomputable def quotientToInvariantsFunctor' :
    Rep R G â¥¤ Rep R Q :=
  quotientToInvariantsFunctor R Ï†.ker â‹™
    (Rep.resEquiv R (QuotientGroup.quotientKerEquivOfSurjective _ surj)).inverse

/--
`M â†‘ H` means the `H` invariants of `M`, as a representation of `G â§¸ H`.
-/
notation3 M " â†‘ " surj => (Rep.quotientToInvariantsFunctor' surj).obj M

lemma quotientToInvariantsFunctor'_obj_Ï (M : Rep R G) :
    (M â†‘ surj).Ï =
    (M.quotientToInvariants Ï†.ker).Ï.comp
      ((QuotientGroup.quotientKerEquivOfSurjective Ï† surj).symm) := rfl

@[simp] lemma QuotientGroup.quotientKerEquivOfSurjective_symm_apply
    {G H : Type*} [Group G] [Group H] (Ï† : G â†’* H) (hÏ† : (â‡‘Ï†).Surjective) (x : G) :
    (QuotientGroup.quotientKerEquivOfSurjective Ï† hÏ†).symm (Ï† x) = x :=
  MulEquiv.symm_apply_eq _ |>.mpr rfl

lemma quotientToInvariantsFunctor'_obj_Ï_apply (M : Rep R G) (g : G) :
    (M â†‘ surj).Ï (Ï† g) = (M.quotientToInvariants Ï†.ker).Ï g := by
  simp [quotientToInvariantsFunctor'_obj_V, quotientToInvariantsFunctor'_obj_Ï]

@[simp] lemma quotientToInvariantsFunctor'_obj_Ï_applyâ‚‚ (M : Rep R G) (g : G)
    (v : (quotientToInvariantsFunctor' surj).obj M) :
    ((M â†‘ surj).Ï (Ï† g) v).val = M.Ï g v.val := by
  rw [quotientToInvariantsFunctor'_obj_Ï_apply]
  rfl

instance : (quotientToInvariantsFunctor' (R := R) surj).PreservesZeroMorphisms where
  map_zero _ _ := rfl

@[simps!] noncomputable def res_quotientToInvariantsFunctor'_Î¹ (M : Rep R G) :
    ((M â†‘ surj) â†“ Ï†) âŸ¶ M where
  hom := ModuleCat.ofHom (Submodule.subtype _)
  comm g := by ext; exact quotientToInvariantsFunctor'_obj_Ï_applyâ‚‚ surj ..

end Rep
namespace groupCohomology

noncomputable def cochain_infl :
    quotientToInvariantsFunctor' surj â‹™ cochainsFunctor R Q âŸ¶ cochainsFunctor R G where
  app M := cochainsMap Ï† <| res_quotientToInvariantsFunctor'_Î¹ surj M
  naturality _ _ _ := rfl

/--
The inflation map `Hâ¿(Gâ§¸H, M â†‘ H) âŸ¶ Hâ¿(G,M)` as a natural transformation.
This is defined using the inflation map on cocycles.
-/
noncomputable def infl (n : â„•) :
    Rep.quotientToInvariantsFunctor' surj â‹™ functor R Q n âŸ¶ functor R G n :=
  (groupCohomology.cochain_infl surj) â—« ğŸ™ (homologyFunctor _ _ n)



/--
Assume that we have a short exact sequence `0 â†’ A â†’ B â†’ C â†’ 0` in `Rep R G`
and that the sequence of `H`- invariants is also a short exact in `Rep R (G â§¸ H)` :

  `0 â†’ Aá´´ â†’ Bá´´ â†’ Cá´´ â†’ 0`.

Then we have a commuting square

`   Hâ¿(G â§¸ H, Cá´´)  âŸ¶   H^{n+1}(G â§¸ H, Aá´´) `
`         |                 |             `
`         â†“                 â†“             `
`     Hâ¿(G , C)    âŸ¶   H^{n+1}(G,A)       `

where the horizontal maps are connecting homomorphisms
and the vertical maps are inflation.
-/
lemma infl_Î´_naturality {S : ShortComplex (Rep R G)} (hS : S.ShortExact)
    (hS' : (S.map (quotientToInvariantsFunctor' surj)).ShortExact)  (i j : â„•) (hij : i + 1 = j) :
    Î´ hS' i j hij â‰« (infl surj j).app _ = (infl surj i).app _ â‰« Î´ hS i j hij
    := by
  let C := S.map (cochainsFunctor R G)
  let S' := S.map (quotientToInvariantsFunctor' surj)
  let C' := S'.map (cochainsFunctor R Q)
  let Ï† : C' âŸ¶ C := {
    Ï„â‚ := (cochain_infl surj).app S.Xâ‚
    Ï„â‚‚ := (cochain_infl surj).app S.Xâ‚‚
    Ï„â‚ƒ := (cochain_infl surj).app S.Xâ‚ƒ
    commâ‚â‚‚ := ((cochain_infl surj).naturality S.f).symm
    commâ‚‚â‚ƒ := ((cochain_infl surj).naturality S.g).symm
  }
  have sesâ‚ : C.ShortExact := map_cochainsFunctor_shortExact hS
  have sesâ‚‚ : C'.ShortExact := map_cochainsFunctor_shortExact hS'
  exact HomologySequence.Î´_naturality Ï† sesâ‚‚ sesâ‚ i j hij
