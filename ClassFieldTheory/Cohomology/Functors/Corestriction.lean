/-
Copyright (c) 2025 Kevin Buzzard. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kevin Buzzard, Aaron Liu, Yunzhou Xie
-/
import ClassFieldTheory.Cohomology.Functors.UpDown
import ClassFieldTheory.Mathlib.Algebra.Module.Torsion.Basic
import ClassFieldTheory.Mathlib.CategoryTheory.Category.Basic
import ClassFieldTheory.Mathlib.CategoryTheory.Category.Cat
import ClassFieldTheory.Mathlib.GroupTheory.GroupAction.Quotient
import ClassFieldTheory.Mathlib.RepresentationTheory.Homological.GroupCohomology.LongExactSequence

/-!
# Corestriction

If `S` is a finite index subgroup of `G` and `M` is a `G`-module
then there's a corestriction map `H^n(S,M) â†’ H^n(G,M)`, defined
by averaging on `H^0` and then by dimension shifting for
general `H^n`.

## Remarks

Cassels-Froehlich define cores on *homology* for an arbitrary
morphism `S â†’ G` and then if `G` is finite they
extend it to Tate cohomology by dimension shifting.
It agrees with our definition on H^0-hat so presumably
agrees with our definition in general for G finite.

Arguably this filename has too large a number.

## TODO

cores o res = multiplication by index
-/

noncomputable section

open
  Rep
  dimensionShift
  groupCohomology
  CategoryTheory
  Limits

variable {R : Type} [CommRing R] -- R a comm ring
variable {G : Type} [Group G] {S : Subgroup G} -- G a group, S a subgroup

attribute [local instance] Subgroup.fintypeQuotientOfFiniteIndex

namespace groupCohomology

-- let V be an R[G]-module
lemma cores_auxâ‚ {V : Type} [AddCommMonoid V] [Module R V] (Ï : Representation R G V)
    -- if v âˆˆ V is S-invariant
    (v : V) (hv : âˆ€ s âˆˆ S, (Ï s) v = v) (gâ‚ gâ‚‚ : G)
    -- then for gâ‚ and gâ‚‚ in G such that gâ‚S=gâ‚‚S, then gâ‚â€¢v=gâ‚‚â€¢v
    (h : (QuotientGroup.mk gâ‚ : G â§¸ S) = QuotientGroup.mk gâ‚‚) : Ï gâ‚ v = Ï gâ‚‚ v := by
  rw [show gâ‚‚ = gâ‚ * (gâ‚â»Â¹ * gâ‚‚) by simp, map_mul, Module.End.mul_apply,
  hv _ (QuotientGroup.eq.1 h)]

-- Cor: if X is any finite set and sâ‚, sâ‚‚ : X â†’ G are such that X -> G -> G/S is a bijection
-- for both of them, then âˆ‘_g sâ‚(g)v = âˆ‘_g sâ‚‚(g)v for v in M^S
lemma cores_auxâ‚‚ {X : Type} {V : Type} [Fintype X] [AddCommGroup V] [Module R V] {sâ‚ : X â†’ G}
    {sâ‚‚ : X â†’ G} (Ï : Representation R G V) (v : V) (hv : âˆ€ s âˆˆ S, (Ï s) v = v)
    (hsâ‚ : Function.Bijective (fun x â†¦ QuotientGroup.mk (sâ‚ x) : X â†’ G â§¸ S))
    (hsâ‚‚ : Function.Bijective (fun x â†¦ QuotientGroup.mk (sâ‚‚ x) : X â†’ G â§¸ S)) :
    âˆ‘ x : X, Ï (sâ‚ x) v = âˆ‘ x : X, Ï (sâ‚‚ x) v := by
  let e1 : X â‰ƒ G â§¸ S := Equiv.ofBijective (QuotientGroup.mk âˆ˜ sâ‚) hsâ‚
  let e2 : X â‰ƒ G â§¸ S := Equiv.ofBijective (QuotientGroup.mk âˆ˜ sâ‚‚) hsâ‚‚
  exact Finset.sum_equiv (e1.trans e2.symm) (by simp) fun i _ â†¦ cores_auxâ‚ Ï v hv _ _ <| by
    rw [Equiv.trans_apply]
    exact (e2.apply_symm_apply _).symm

variable [S.FiniteIndex]

/-- The H^0 corestriction map for S âŠ† G a finite index subgroup, as an `R`-linear
map on invariants. -/
@[simps]
def _root_.Rep.coresâ‚€_obj (V : Rep R G) :
    -- Defining an R-linear map from V^S to V^G
    (V â†“ S.subtype).Ï.invariants â†’â‚—[R] V.Ï.invariants where
  toFun x := âŸ¨âˆ‘ i : G â§¸ S, V.Ï i.out x.1, fun g â†¦ by
    simp only [map_sum, â† LinearMap.comp_apply, â† Module.End.mul_eq_comp, â† map_mul]
    letI : Fintype (G â§¸ S) := Subgroup.fintypeQuotientOfFiniteIndex
    refine (cores_auxâ‚‚ V.Ï x.1 (by simpa [-SetLike.coe_mem] using x.2) (by simp) ?_).symm
    simp_rw [QuotientGroup.mk_mul', QuotientGroup.out_eq', MulAction.bijective]âŸ©
  map_add' := by simp [Finset.sum_add_distrib]
  map_smul' := by simp [Finset.smul_sum]

/-- The corestriction functor on H^0 for S âŠ† G a finite index subgroup, as a
functor `H^0(S,-) â†’ H^0(G,-)`. -/
@[simps]
def coresâ‚€ : Rep.res S.subtype â‹™ functor R S 0 âŸ¶ functor R G 0 where
  app M :=
    (H0Iso (M â†“ S.subtype)).hom â‰« (ModuleCat.ofHom (Rep.coresâ‚€_obj M)) â‰« (H0Iso M).inv
  naturality := by
    intro X Y f
    simp_rw [â† Category.assoc]
    rw [(H0Iso Y).comp_inv_eq]
    simp_rw [Category.assoc]
    rw [functor_map, map_id_comp_H0Iso_hom, (H0Iso X).inv_hom_id_assoc, Functor.comp_map,
      functor_map, map_id_comp_H0Iso_hom_assoc, (H0Iso (X â†“ S.subtype)).cancel_iso_hom_left]
    ext x
    have comm := congr(âˆ‘ i : G â§¸ S, ModuleCat.Hom.hom $(f.comm i.out) x.val)
    simpa [Rep.coresâ‚€_obj] using comm.symm

/-- The morphism `HÂ¹(S, Mâ†“S) âŸ¶ HÂ¹(G, M)`. -/
def coresâ‚_obj (M : Rep R G) :
    -- defining HÂ¹(S, Mâ†“S) âŸ¶ HÂ¹(G, M) by a diagram chase
    (functor R S 1).obj (M â†“ S.subtype) âŸ¶ (functor R G 1).obj M := by
  -- Recall we have 0 âŸ¶ M âŸ¶ coindâ‚'^G M âŸ¶ up_G M âŸ¶ 0 a short exact sequence
  -- of `G`-modules which restricts to a short exact sequence of `S`-modules.
  -- First I claim Î´ : Hâ°(S,(up_G M)â†“S) âŸ¶ HÂ¹(S,Mâ†“S) is surjective
  have : Epi (mapShortComplexâ‚ƒ (shortExact_upSES_res M S.subtype) (rfl : 0 + 1 = 1)).g :=
    -- because `coindâ‚'^G M` has trivial cohomology
    epi_Î´_up_zero_res (R := R) (Ï† := S.subtype) M S.subtype_injective
  -- so it suffices to give a map Hâ°(S,(up_G M)â†“S) âŸ¶ HÂ¹(G,M) such that the
  -- image of Hâ°(S,(coindâ‚'^G M)â†“S) is in the kernel of that map
  refine (mapShortComplexâ‚ƒ_exact (shortExact_upSES_res M S.subtype) (rfl : 0 + 1 = 1)).desc ?_ ?_
  Â· -- The map Hâ°(S,up_G M)â†“S) âŸ¶ HÂ¹(G,M) is just the composite of
    -- coresâ‚€ : Hâ°(S,up_G Mâ†“S) âŸ¶ Hâ°(G,up_G M) and Î´ : Hâ°(G,up_G M) âŸ¶ HÂ¹(G,M)
    exact (coresâ‚€.app _) â‰« (Î´ (shortExact_upSES M) 0 1 rfl)
  Â· -- We now need to prove that the induced map
    -- Hâ°(S,(coindâ‚'^G M)â†“S) âŸ¶ HÂ¹(G,M) is 0
    -- This is a diagram chase. The map is Hâ°(S,(coindâ‚'^G M)â†“S) âŸ¶ Hâ°(S,(up_G M)â†“S)
    -- (functoriality of Hâ°) followed by coresâ‚€ : Hâ°(S,(up_G M)â†“S) âŸ¶ Hâ°(G, up_G M)
    -- followed by Î´ : Hâ°(G, up_G M) âŸ¶ HÂ¹(G, M). First put the brackets around
    -- the first two terms.
    rw [â† Category.assoc]
    -- now apply naturality of coresâ‚€, because I want to change
    -- Hâ°(S,(coindâ‚'^G M)â†“S) âŸ¶ Hâ°(S,(up_G M)â†“S) âŸ¶ Hâ°(G, up_G M) to
    -- Hâ°(S,(coindâ‚'^G M)â†“S) âŸ¶ Hâ°(G,(coindâ‚'^G M)) âŸ¶ Hâ°(G, up_G M)
    let bar := cokernel.Ï€ (coindâ‚'_Î¹.app M)
    -- coresâ‚€ : res S.subtype â‹™ functor R (â†¥S) 0 âŸ¶ functor R G 0
    have baz := coresâ‚€.naturality (F := (res S.subtype â‹™ functor R (â†¥S) 0)) bar
    change ((res S.subtype â‹™ functor R (â†¥S) 0).map bar â‰« (coresâ‚€.app (up.obj M))) â‰« _ = 0
    change _ â‰« (coresâ‚€.app (up.obj M)) = _ â‰« _ at baz
    rw [baz, Category.assoc]
    convert comp_zero -- cancel first functor
    exact (mapShortComplexâ‚ƒ (shortExact_upSES M) (rfl : 0 + 1 = 1)).zero

@[reassoc]
lemma commSq_coresâ‚ (M : Rep R G) :
  Î´ (shortExact_upSES_res M S.subtype) 0 1 rfl â‰« coresâ‚_obj (S := S) M =
    (coresâ‚€ (S := S)).app _ â‰« Î´ (shortExact_upSES M) 0 1 rfl :=
  have : Epi (mapShortComplexâ‚ƒ (shortExact_upSES_res M S.subtype) (rfl : 0 + 1 = 1)).g :=
    epi_Î´_up_zero_res (R := R) (Ï† := S.subtype) M S.subtype_injective
  (mapShortComplexâ‚ƒ_exact (shortExact_upSES_res M S.subtype) (rfl : 0 + 1 = 1)).g_desc _ _

theorem coresâ‚_naturality  (X Y : Rep R G) (f : X âŸ¶ Y) :
    (res S.subtype â‹™ functor R (â†¥S) 1).map f â‰« coresâ‚_obj Y =
    coresâ‚_obj X â‰« (functor R G 1).map f := by
  haveI : Epi (Î´ (shortExact_upSES_res X S.subtype) 0 1 rfl) :=
    epi_Î´_up_zero_res (R := R) (Ï† := S.subtype) X S.subtype_injective
  symm
  refine CategoryTheory.cubeLemma
    (H0 (up.obj X â†“ S.subtype)) (H1 (X â†“ S.subtype)) (H0 (up.obj X)) (H1 X)
    (H0 (up.obj Y â†“ S.subtype)) (H1 (Y â†“ S.subtype)) (H0 (up.obj Y)) (H1 Y)
    -- four ?_ are the maps in the conclusion of the lemma
    (Î´ (shortExact_upSES_res X S.subtype) 0 1 rfl) (Î´ (shortExact_upSES X) 0 1 rfl)
    (Î´ (shortExact_upSES_res Y S.subtype) 0 1 rfl) (Î´ (shortExact_upSES Y) 0 1 rfl)
    (coresâ‚€.app (up.obj X)) _ (coresâ‚€.app (up.obj Y)) _
    (map (.id S) ((res S.subtype).map (up.map f)) 0) _
    (map (.id G) (up.map f) 0) _
    ?_ ?_ ?_ ?_ (by exact (coresâ‚€ (S := S)|>.naturality (X := up.obj X) (up.map f)).symm) this
  all_goals symm
  Â· exact commSq_coresâ‚ X
  Â· exact commSq_coresâ‚ Y
  Â· exact Î´_naturality (shortExact_upSES_res X S.subtype) (shortExact_upSES_res Y S.subtype)
      ((upShortComplex â‹™ (res (R := R) S.subtype).mapShortComplex).map f) 0 1 rfl
  Â· exact Î´_naturality (shortExact_upSES X) (shortExact_upSES Y)
      âŸ¨f, coindâ‚'.map f, up.map f, rfl, by aesop_catâŸ© 0 1 rfl

/-- Corestriction on objects in group cohomology. -/
def cores_obj : (M : Rep R G) â†’ (n : â„•) â†’
    (functor R S n).obj (M â†“ S.subtype) âŸ¶ (functor R G n).obj M
| M, 0 => coresâ‚€.app M
| M, 1 => coresâ‚_obj M
| M, (d + 2) =>
  -- Î´ : H^{d+1}(G,up -) â‰… H^{d+2}(G,-)
  let up_Î´_bottom_Iso := Rep.dimensionShift.Î´UpNatIso (R := R) (G := G) d
  -- `M âŸ¶ coindâ‚'^G M âŸ¶ up_G M` as a complex of S-modules
  let upsc_top := (upShortComplex.obj M).map (res S.subtype)
  -- the above complex of S-modules is exact
  have htopexact : upsc_top.ShortExact := shortExact_upSES_res M S.subtype
  -- so Î´ : H^{d+1}(S,up_G M) â‰… H^{d+2}(S,M)...
  let up_Î´_top_isIso : IsIso (Î´ (htopexact) (d + 1) (d + 2) rfl) := by
    -- ...because `coindâ‚'^G M` has trivial cohomology as S-module
    -- have := M.coindâ‚'_trivialCohomology
    have : upsc_top.Xâ‚‚.TrivialCohomology := Rep.TrivialCohomology.res_subtype (coindâ‚'.obj M)
    refine isIso_Î´_of_isZero (htopexact) (d + 1) ?_ ?_
    all_goals simpa only [upSES_Xâ‚‚] using isZero_of_trivialCohomology
  let ih := cores_obj (up.obj M) (d + 1)
  (asIso (Î´ (htopexact) (d + 1) (d + 2) rfl)).inv â‰« ih â‰« (up_Î´_bottom_Iso).hom.app M

theorem cores_succ_naturality (n : â„•) (X Y : Rep R G) (f : X âŸ¶ Y) :
    (res S.subtype â‹™ functor R (â†¥S) (n + 1)).map f â‰« cores_obj Y (n + 1) =
    cores_obj X (n + 1) â‰« (functor R G (n + 1)).map f := by
  revert X Y f
  induction n with
  | zero => exact fun _ _ _ â†¦ coresâ‚_naturality ..
  | succ n ih =>
    intro X Y f
    have := Î´_naturality (shortExact_upSES_res X S.subtype) (shortExact_upSES_res Y S.subtype)
      ((upShortComplex â‹™ (res (R := R) S.subtype).mapShortComplex).map f) (n + 1) (n + 2) rfl
    simp only [Functor.comp_obj, functor_obj, Functor.comp_map, functor_map, cores_obj,
      ShortComplex.map_Xâ‚ƒ, ShortComplex.map_Xâ‚, asIso_inv, up_obj, Functor.id_obj, coindâ‚'_obj,
      Î´UpNatIso, Î´UpIso, NatIso.ofComponents_hom_app, asIso_hom, Category.assoc, IsIso.eq_inv_comp]
    rw [â† Category.assoc]
    simp only [ShortComplex.map_Xâ‚ƒ, upSES_Xâ‚ƒ, up_obj, Functor.id_obj, coindâ‚'_obj,
      ShortComplex.map_Xâ‚, upSES_Xâ‚, Functor.comp_map, upShortComplex_obj,
      Functor.mapShortComplex_map_Ï„â‚, upShortComplex_map_Ï„â‚, Functor.mapShortComplex_map_Ï„â‚ƒ,
      upShortComplex_map_Ï„â‚ƒ, up_map] at this
    rw [this, Category.assoc, â† Category.assoc (Î´ _ _ _ _), IsIso.hom_inv_id, Category.id_comp,
      Î´_naturality (shortExact_upSES X) (shortExact_upSES Y) âŸ¨f, coindâ‚'.map f, up.map f, rfl,
      by aesop_catâŸ© (n + 1) (n + 2) rfl, â† Category.assoc, â† Category.assoc]
    exact congr((Â· â‰« Î´ (shortExact_upSES _) _ _ _) $(ih (up.obj X) (up.obj Y) (up.map f)))

variable (R) (S) in
/-- Corestriction as a natural transformation. -/
def coresNatTrans (n : â„•) : Rep.res S.subtype â‹™ functor R S n âŸ¶ functor R G n where
  app M := (groupCohomology.cores_obj M n)
  naturality X Y f := match n with
    | 0 => coresâ‚€.naturality f
    | n + 1 => cores_succ_naturality n X Y f

lemma map_H0Iso_hom_f_apply'.{u} {k G H : Type u} [CommRing k] [Group G] [Group H] {A : Rep k H} {B : Rep k G}
    (f : G â†’* H) (Ï† : A â†“ f âŸ¶ B) (x : groupCohomology A 0) :
    (H0Iso B).hom.hom ((map f Ï† 0).hom x) =
    Ï†.hom.hom ((H0Iso A).hom.hom x : A) :=
  map_H0Iso_hom_f_apply ..

-- `simp` does a lot of work here, and it was quite some effort getting
-- it to do so, so I hope this proof never breaks...
lemma cores_resâ‚€ : rest (R := R) (S.subtype) 0 â‰« coresâ‚€ = S.index â€¢ (.id _) := by
  ext M v
  apply (ConcreteCategory.injective_of_mono_of_preservesPullback (H0Iso M).hom)
  ext
  simp [rest, Subgroup.index, groupCohomology.map_H0Iso_hom_f_apply' S.subtype,
    (M.Ï.mem_invariants ((H0Iso M).hom.hom v)).1 (Subtype.prop _)]

/-!
            rest                       cores
Hâ¿(G, up M) ---> Hâ¿(S, upM â†“ S.subtype) ---> Hâ¿(G, up M)
    |                                         |
    | Î´                                       | Î´
    v       rest                       cores  v
Hâ¿âºÂ¹(G, M)  ---> Hâ¿âºÂ¹(S, M â†“ S.subtype) ---> Hâ¿âºÂ¹(G, M)

-/
lemma commSqâ‚™ (n : â„•) (M : Rep R G) :
    (rest S.subtype n â‰« coresNatTrans R S n).app (up.obj M) â‰« Î´ (shortExact_upSES M) n (n + 1) rfl =
    Î´ (shortExact_upSES M) n (n + 1) rfl â‰« (rest S.subtype (n + 1) â‰« coresNatTrans R S (n + 1)).app M := by
  rw [NatTrans.comp_app, NatTrans.comp_app]
  match n with
  | 0 =>
    exact comp_commSq _ _ _ _ _ _ _ (Î´ (shortExact_upSES_res M S.subtype) 0 1 rfl)
      (rest_Î´_naturality (shortExact_upSES M) S.subtype 0 1 rfl |>.symm) (commSq_coresâ‚ ..|>.symm)
  | n + 1 =>
    refine comp_commSq _ _ _ _ _ _ _ (Î´ (shortExact_upSES_res M S.subtype) (n + 1) (n + 2) rfl)
      (rest_Î´_naturality (shortExact_upSES M) S.subtype (n + 1) (n + 2) rfl).symm ?_
    simp [-up_obj, coresNatTrans, cores_obj, Î´UpNatIso, Î´UpIso]

lemma cores_res (n : â„•) :
    (rest (R := R) (S.subtype) n â‰« coresNatTrans R S n : functor R G n âŸ¶ functor R G n) =
      S.index â€¢ (.id _) := by
  induction n with
  | zero => exact cores_resâ‚€
  | succ n ih =>
    ext M : 2
    haveI : Epi (Î´ (shortExact_upSES M) n (n + 1) rfl) :=
    match n with
    | 0 => Î´_up_zero_epi ..
    | m + 1 => Î´_up_isIso M m|>.epi_of_iso _
    rw [â† cancel_epi (Î´ (shortExact_upSES M) n (n + 1) rfl),  â† commSqâ‚™ n M, ih]
    simp

/-- Any element of H^n-hat (n âˆˆ â„¤) is `|G|`-torsion. -/
lemma torsion_of_finite_of_neZero {n : â„•} [NeZero n] (M : Rep R G)
    (x : groupCohomology M n) : Nat.card G â€¢ x = 0 := by
  if hG : Infinite G then simp else
  simp only [not_infinite_iff_finite] at hG
  have := by simpa using (LinearMap.ext_iff.1 <| ModuleCat.hom_ext_iff.1
    congr(NatTrans.app $(cores_res (R := R) n (G := G) (S := âŠ¥)) M)) x
  simp [â† this, rest, IsZero.eq_zero_of_tgt isZero_of_trivialCohomology <|
    map _ (ğŸ™ (M â†“ (âŠ¥ : Subgroup G).subtype)) n]

-- /-- Any element of H^n-hat (n âˆˆ â„¤) is `|G|`-torsion. -/
-- lemma tateCohomology_torsion {n : â„¤} [Fintype G] (M : Rep R G) (x : (tateCohomology n).obj M) :
--     Nat.card G â€¢ x = 0 := sorry

-- Should the above really be a statement about a functor?
-- Something like this?

-- instance (n : â„¤) [Finite G] : Functor.Additive (tateCohomology (R := R) (G := G) n) := sorry

-- this doesn't work
-- lemma tateCohomology_torsion' {n : â„¤} [Finite G] :
--     (Nat.card G) â€¢ (CategoryTheory.NatTrans.id (tateCohomology (R := R) (G := G) n)) = 0 := sorry

-- p^infty-torsion injects into H^(Sylow) (for group cohomology)

lemma pTorsion_eq_sylowTorsion {n : â„•} [NeZero n] [Finite G] (M : Rep R G)
    (p : â„•) [Fact p.Prime] (P : Sylow p G) (x : groupCohomology M n) :
    (âˆƒ d, (p ^ d) â€¢ x = 0) â†” x âˆˆ Submodule.torsionBy R _ (Nat.card P) where
  mp := by
    rintro âŸ¨d, hdâŸ©
    obtain âŸ¨k, hk1, hk2âŸ© := Nat.dvd_prime_pow Fact.out|>.1 <| Nat.gcd_dvd_right (Nat.card G) (p ^ d)
    obtain âŸ¨m, hmâŸ© := P.pow_dvd_card_of_pow_dvd_card (hk2 â–¸ Nat.gcd_dvd_left (Nat.card G) (p ^ d))
    simp [hm, mul_comm _ m, mul_smul, - Nat.cast_pow, Nat.cast_smul_eq_nsmul, â† hk2, smul_comm m]
    simp [smul_comm _ m, hd, torsion_of_finite_of_neZero]
  mpr h := âŸ¨(Nat.card G).factorization p, P.card_eq_multiplicity â–¸ by
    simpa [Nat.cast_smul_eq_nsmul] using hâŸ©

lemma injects_to_sylowCoh {n : â„•} [NeZero n] [Finite G] (M : Rep R G)
    (p : â„•) [Fact p.Prime] (P : Sylow p G) : Function.Injective
    ((map P.toSubgroup.subtype (ğŸ™ (_ â†“ _)) n).hom âˆ˜â‚— (Module.IsTorsionBy.coprime_decompose
    (M := groupCohomology M n) (Subgroup.card_mul_index P.toSubgroup).symm
    (Sylow.card_coprime_index P) (fun x â†¦ Nat.cast_smul_eq_nsmul R (Nat.card G) x â–¸
    torsion_of_finite_of_neZero M x)).symm.toLinearMap âˆ˜â‚— LinearMap.inl _ _ _) :=
  Function.Injective.of_comp (f := (cores_obj M n).hom) <| by
  have eq := by simpa [rest_app, coresNatTrans] using
    ModuleCat.hom_ext_iff.1 congr(NatTrans.app $(cores_res (R := R) (G := G) (S := P) n) M)
  simp only [functor_obj, LinearMap.coe_comp, LinearMap.coe_inl, â† Function.comp_assoc]
  simp only [â† LinearMap.coe_comp, eq, Module.End.mul_eq_comp, LinearMap.comp_id,
    LinearEquiv.coe_coe]
  intro âŸ¨x1, hx1âŸ© âŸ¨x2, hx2âŸ©
  simp only [Function.comp_apply, Module.IsTorsionBy.coprime_decompose_symm_apply,
    ZeroMemClass.coe_zero, smul_zero, add_zero, map_smul, Module.End.natCast_apply,
    Subtype.mk.injEq]
  intro h
  replace h := by simpa using congr((Â· + ((Nat.card P).gcdA P.toSubgroup.index : R) â€¢ 0) $h)
  nth_rw 1 [â† Submodule.mem_torsionBy_iff _ _|>.1 hx1,
    â† Submodule.mem_torsionBy_iff _ _|>.1 hx2] at h
  rw [â† Nat.cast_smul_eq_nsmul R P.toSubgroup.index, â† Nat.cast_smul_eq_nsmul R P.toSubgroup.index,
    â† smul_assoc, â† smul_assoc, â† smul_assoc, â† smul_assoc] at h
  simp only [â† add_smul, smul_eq_mul] at h
  rw [â† Ring.intCast_ofNat, â† Int.cast_mul, â† Ring.intCast_ofNat (Nat.card P), â† Int.cast_mul,
    â† Int.cast_add, add_comm, mul_comm, mul_comm _ (P.toSubgroup.index : â„¤),
    â† Nat.gcd_eq_gcd_ab, Nat.coprime_iff_gcd_eq_one.1 (Sylow.card_coprime_index P)] at h
  simpa using h

lemma groupCohomology_Sylow {n : â„•} (hn : 0 < n) [Finite G] (M : Rep R G)
    (x : groupCohomology M n) (p : â„•) [Fact p.Prime] (P : Sylow p G) (hx : âˆƒ d, (p ^ d) â€¢ x = 0)
    (hx' : x â‰  0) : ((rest (P.toSubgroup.subtype) n).app M).hom x â‰  0 := by
  classical
  haveI : NeZero n := âŸ¨ne_of_gt hnâŸ©
  simpa [Functor.comp_obj, functor_obj, rest_app, ne_eq] using by_contra fun hx2 â†¦ hx' <|
    @Subtype.ext_iff _ (p := fun x â†¦ x âˆˆ Submodule.torsionBy R (groupCohomology M n) (Nat.card P))
    âŸ¨x, pTorsion_eq_sylowTorsion M p P x|>.1 hxâŸ© 0|>.1 <| groupCohomology.injects_to_sylowCoh M p P
    (by simp [not_not.1 hx2])
