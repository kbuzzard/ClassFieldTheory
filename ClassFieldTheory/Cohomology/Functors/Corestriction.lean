/-
Copyright (c) 2025 Kevin Buzzard. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kevin Buzzard, Aaron Liu
-/
import ClassFieldTheory.Cohomology.Functors.UpDown
import ClassFieldTheory.Mathlib.GroupTheory.GroupAction.Quotient
import ClassFieldTheory.Mathlib.CategoryTheory.Category.Basic
import ClassFieldTheory.Mathlib.CategoryTheory.Category.Cat
import ClassFieldTheory.Mathlib.RepresentationTheory.Homological.GroupCohomology.LongExactSequence

/-!
# Corestriction

If `S` is a finite index subgroup of `G` and `M` is a `G`-module
then there's a corestriction map `H^n(S,M) ‚Üí H^n(G,M)`, defined
by averaging on `H^0` and then by dimension shifting for
general `H^n`.

## Remarks

Cassels-Froehlich define cores on *homology* for an arbitrary
morphism `S ‚Üí G` and then if `G` is finite they
extend it to Tate cohomology by dimension shifting.
It agrees with our definition on H^0-hat so presumably
agrees with our definition in general for G finite.

Arguably this filename has too large a number.

## TODO

cores o res = multiplication by index
-/

noncomputable section

open
  Rep
  dimensionShift
  groupCohomology
  CategoryTheory
  Limits

variable {R : Type} [CommRing R] -- R a comm ring
variable {G : Type} [Group G] {S : Subgroup G} -- G a group, S a subgroup

attribute [local instance] Subgroup.fintypeQuotientOfFiniteIndex

namespace groupCohomology

-- let V be an R[G]-module
lemma cores_aux‚ÇÅ {V : Type} [AddCommMonoid V] [Module R V] (œÅ : Representation R G V)
    -- if v ‚àà V is S-invariant
    (v : V) (hv : ‚àÄ s ‚àà S, (œÅ s) v = v) (g‚ÇÅ g‚ÇÇ : G)
    -- then for g‚ÇÅ and g‚ÇÇ in G such that g‚ÇÅS=g‚ÇÇS, then g‚ÇÅ‚Ä¢v=g‚ÇÇ‚Ä¢v
    (h : (QuotientGroup.mk g‚ÇÅ : G ‚ß∏ S) = QuotientGroup.mk g‚ÇÇ) : œÅ g‚ÇÅ v = œÅ g‚ÇÇ v := by
  rw [show g‚ÇÇ = g‚ÇÅ * (g‚ÇÅ‚Åª¬π * g‚ÇÇ) by simp, map_mul, Module.End.mul_apply,
  hv _ (QuotientGroup.eq.1 h)]

-- Cor: if X is any finite set and s‚ÇÅ, s‚ÇÇ : X ‚Üí G are such that X -> G -> G/S is a bijection
-- for both of them, then ‚àë_g s‚ÇÅ(g)v = ‚àë_g s‚ÇÇ(g)v for v in M^S
lemma cores_aux‚ÇÇ {X : Type} {V : Type} [Fintype X] [AddCommGroup V] [Module R V] {s‚ÇÅ : X ‚Üí G}
    {s‚ÇÇ : X ‚Üí G} (œÅ : Representation R G V) (v : V) (hv : ‚àÄ s ‚àà S, (œÅ s) v = v)
    (hs‚ÇÅ : Function.Bijective (fun x ‚Ü¶ QuotientGroup.mk (s‚ÇÅ x) : X ‚Üí G ‚ß∏ S))
    (hs‚ÇÇ : Function.Bijective (fun x ‚Ü¶ QuotientGroup.mk (s‚ÇÇ x) : X ‚Üí G ‚ß∏ S)) :
    ‚àë x : X, œÅ (s‚ÇÅ x) v = ‚àë x : X, œÅ (s‚ÇÇ x) v := by
  let e1 : X ‚âÉ G ‚ß∏ S := Equiv.ofBijective (QuotientGroup.mk ‚àò s‚ÇÅ) hs‚ÇÅ
  let e2 : X ‚âÉ G ‚ß∏ S := Equiv.ofBijective (QuotientGroup.mk ‚àò s‚ÇÇ) hs‚ÇÇ
  exact Finset.sum_equiv (e1.trans e2.symm) (by simp) fun i _ ‚Ü¶ cores_aux‚ÇÅ œÅ v hv _ _ <| by
    rw [Equiv.trans_apply]
    exact (e2.apply_symm_apply _).symm

variable [S.FiniteIndex]

/-- The H^0 corestriction map for S ‚äÜ G a finite index subgroup, as an `R`-linear
map on invariants. -/
@[simps]
def _root_.Rep.cores‚ÇÄ_obj (V : Rep R G) :
    -- Defining an R-linear map from V^S to V^G
    (V ‚Üì S.subtype).œÅ.invariants ‚Üí‚Çó[R] V.œÅ.invariants where
  toFun x := ‚ü®‚àë i : G ‚ß∏ S, V.œÅ i.out x.1, fun g ‚Ü¶ by
    simp only [map_sum, ‚Üê LinearMap.comp_apply, ‚Üê Module.End.mul_eq_comp, ‚Üê map_mul]
    letI : Fintype (G ‚ß∏ S) := Subgroup.fintypeQuotientOfFiniteIndex
    refine (cores_aux‚ÇÇ V.œÅ x.1 (by simpa [-SetLike.coe_mem] using x.2) (by simp) ?_).symm
    simp_rw [QuotientGroup.mk_mul', QuotientGroup.out_eq', MulAction.bijective]‚ü©
  map_add' := by simp [Finset.sum_add_distrib]
  map_smul' := by simp [Finset.smul_sum]

/-- The corestriction functor on H^0 for S ‚äÜ G a finite index subgroup, as a
functor `H^0(S,-) ‚Üí H^0(G,-)`. -/
@[simps]
def cores‚ÇÄ : Rep.res S.subtype ‚ãô functor R S 0 ‚ü∂ functor R G 0 where
  app M :=
    (H0Iso (M ‚Üì S.subtype)).hom ‚â´ (ModuleCat.ofHom (Rep.cores‚ÇÄ_obj M)) ‚â´ (H0Iso M).inv
  naturality := by
    intro X Y f
    simp_rw [‚Üê Category.assoc]
    rw [(H0Iso Y).comp_inv_eq]
    simp_rw [Category.assoc]
    rw [functor_map, map_id_comp_H0Iso_hom, (H0Iso X).inv_hom_id_assoc, Functor.comp_map,
      functor_map, map_id_comp_H0Iso_hom_assoc, (H0Iso (X ‚Üì S.subtype)).cancel_iso_hom_left]
    ext x
    have comm := congr(‚àë i : G ‚ß∏ S, ModuleCat.Hom.hom $(f.comm i.out) x.val)
    simpa [Rep.cores‚ÇÄ_obj] using comm.symm

/-- The morphism `H¬π(S, M‚ÜìS) ‚ü∂ H¬π(G, M)`. -/
def cores‚ÇÅ_obj [DecidableEq G] (M : Rep R G) :
    -- defining H¬π(S, M‚ÜìS) ‚ü∂ H¬π(G, M) by a diagram chase
    (functor R S 1).obj (M ‚Üì S.subtype) ‚ü∂ (functor R G 1).obj M := by
  -- Recall we have 0 ‚ü∂ M ‚ü∂ coind‚ÇÅ'^G M ‚ü∂ up_G M ‚ü∂ 0 a short exact sequence
  -- of `G`-modules which restricts to a short exact sequence of `S`-modules.
  -- First I claim Œ¥ : H‚Å∞(S,(up_G M)‚ÜìS) ‚ü∂ H¬π(S,M‚ÜìS) is surjective
  have : Epi (mapShortComplex‚ÇÉ (shortExact_upSES_res M S.subtype) (rfl : 0 + 1 = 1)).g :=
    -- because `coind‚ÇÅ'^G M` has trivial cohomology
    epi_Œ¥_up_zero_res (R := R) (œÜ := S.subtype) M S.subtype_injective
  -- so it suffices to give a map H‚Å∞(S,(up_G M)‚ÜìS) ‚ü∂ H¬π(G,M) such that the
  -- image of H‚Å∞(S,(coind‚ÇÅ'^G M)‚ÜìS) is in the kernel of that map
  refine (mapShortComplex‚ÇÉ_exact (shortExact_upSES_res M S.subtype) (rfl : 0 + 1 = 1)).desc ?_ ?_
  ¬∑ -- The map H‚Å∞(S,up_G M)‚ÜìS) ‚ü∂ H¬π(G,M) is just the composite of
    -- cores‚ÇÄ : H‚Å∞(S,up_G M‚ÜìS) ‚ü∂ H‚Å∞(G,up_G M) and Œ¥ : H‚Å∞(G,up_G M) ‚ü∂ H¬π(G,M)
    exact (cores‚ÇÄ.app _) ‚â´ (Œ¥ (shortExact_upSES M) 0 1 rfl)
  ¬∑ -- We now need to prove that the induced map
    -- H‚Å∞(S,(coind‚ÇÅ'^G M)‚ÜìS) ‚ü∂ H¬π(G,M) is 0
    -- This is a diagram chase. The map is H‚Å∞(S,(coind‚ÇÅ'^G M)‚ÜìS) ‚ü∂ H‚Å∞(S,(up_G M)‚ÜìS)
    -- (functoriality of H‚Å∞) followed by cores‚ÇÄ : H‚Å∞(S,(up_G M)‚ÜìS) ‚ü∂ H‚Å∞(G, up_G M)
    -- followed by Œ¥ : H‚Å∞(G, up_G M) ‚ü∂ H¬π(G, M). First put the brackets around
    -- the first two terms.
    rw [‚Üê Category.assoc]
    -- now apply naturality of cores‚ÇÄ, because I want to change
    -- H‚Å∞(S,(coind‚ÇÅ'^G M)‚ÜìS) ‚ü∂ H‚Å∞(S,(up_G M)‚ÜìS) ‚ü∂ H‚Å∞(G, up_G M) to
    -- H‚Å∞(S,(coind‚ÇÅ'^G M)‚ÜìS) ‚ü∂ H‚Å∞(G,(coind‚ÇÅ'^G M)) ‚ü∂ H‚Å∞(G, up_G M)
    let bar := cokernel.œÄ (coind‚ÇÅ'_Œπ.app M)
    -- cores‚ÇÄ : res S.subtype ‚ãô functor R (‚Ü•S) 0 ‚ü∂ functor R G 0
    have baz := cores‚ÇÄ.naturality (F := (res S.subtype ‚ãô functor R (‚Ü•S) 0)) bar
    change ((res S.subtype ‚ãô functor R (‚Ü•S) 0).map bar ‚â´ (cores‚ÇÄ.app (up.obj M))) ‚â´ _ = 0
    change _ ‚â´ (cores‚ÇÄ.app (up.obj M)) = _ ‚â´ _ at baz
    rw [baz, Category.assoc]
    convert comp_zero -- cancel first functor
    exact (mapShortComplex‚ÇÉ (shortExact_upSES M) (rfl : 0 + 1 = 1)).zero

@[reassoc]
lemma commSq_cores‚ÇÅ [DecidableEq G] (M : Rep R G) :
  Œ¥ (shortExact_upSES_res M S.subtype) 0 1 rfl ‚â´ cores‚ÇÅ_obj (S := S) M =
    (cores‚ÇÄ (S := S)).app _ ‚â´ Œ¥ (shortExact_upSES M) 0 1 rfl :=
  have : Epi (mapShortComplex‚ÇÉ (shortExact_upSES_res M S.subtype) (rfl : 0 + 1 = 1)).g :=
    epi_Œ¥_up_zero_res (R := R) (œÜ := S.subtype) M S.subtype_injective
  (mapShortComplex‚ÇÉ_exact (shortExact_upSES_res M S.subtype) (rfl : 0 + 1 = 1)).g_desc _ _

theorem cores‚ÇÅ_naturality  (X Y : Rep R G) (f : X ‚ü∂ Y) [DecidableEq G] :
    (res S.subtype ‚ãô functor R (‚Ü•S) 1).map f ‚â´ cores‚ÇÅ_obj Y =
    cores‚ÇÅ_obj X ‚â´ (functor R G 1).map f := by
  haveI : Epi (Œ¥ (shortExact_upSES_res X S.subtype) 0 1 rfl) :=
    epi_Œ¥_up_zero_res (R := R) (œÜ := S.subtype) X S.subtype_injective
  symm
  refine CategoryTheory.cubeLemma
    (H0 (up.obj X ‚Üì S.subtype)) (H1 (X ‚Üì S.subtype)) (H0 (up.obj X)) (H1 X)
    (H0 (up.obj Y ‚Üì S.subtype)) (H1 (Y ‚Üì S.subtype)) (H0 (up.obj Y)) (H1 Y)
    -- four ?_ are the maps in the conclusion of the lemma
    (Œ¥ (shortExact_upSES_res X S.subtype) 0 1 rfl) (Œ¥ (shortExact_upSES X) 0 1 rfl)
    (Œ¥ (shortExact_upSES_res Y S.subtype) 0 1 rfl) (Œ¥ (shortExact_upSES Y) 0 1 rfl)
    (cores‚ÇÄ.app (up.obj X)) _ (cores‚ÇÄ.app (up.obj Y)) _
    (map (.id S) ((res S.subtype).map (up.map f)) 0) _
    (map (.id G) (up.map f) 0) _
    ?_ ?_ ?_ ?_ (by exact (cores‚ÇÄ (S := S)|>.naturality (X := up.obj X) (up.map f)).symm) this
  all_goals symm
  ¬∑ exact commSq_cores‚ÇÅ X
  ¬∑ exact commSq_cores‚ÇÅ Y
  ¬∑ exact Œ¥_naturality (shortExact_upSES_res X S.subtype) (shortExact_upSES_res Y S.subtype)
      ((upShortComplex ‚ãô (res (R := R) S.subtype).mapShortComplex).map f) 0 1 rfl
  ¬∑ exact Œ¥_naturality (shortExact_upSES X) (shortExact_upSES Y)
      ‚ü®f, coind‚ÇÅ'.map f, up.map f, rfl, by aesop_cat‚ü© 0 1 rfl

/-- Corestriction on objects in group cohomology. -/
def cores_obj [DecidableEq G] : (M : Rep R G) ‚Üí (n : ‚Ñï) ‚Üí
    (functor R S n).obj (M ‚Üì S.subtype) ‚ü∂ (functor R G n).obj M
| M, 0 => cores‚ÇÄ.app M
| M, 1 => cores‚ÇÅ_obj M
| M, (d + 2) =>
  -- Œ¥ : H^{d+1}(G,up -) ‚âÖ H^{d+2}(G,-)
  let up_Œ¥_bottom_Iso := Rep.dimensionShift.Œ¥UpNatIso (R := R) (G := G) d
  -- `M ‚ü∂ coind‚ÇÅ'^G M ‚ü∂ up_G M` as a complex of S-modules
  let upsc_top := (upShortComplex.obj M).map (res S.subtype)
  -- the above complex of S-modules is exact
  have htopexact : upsc_top.ShortExact := shortExact_upSES_res M S.subtype
  -- so Œ¥ : H^{d+1}(S,up_G M) ‚âÖ H^{d+2}(S,M)...
  let up_Œ¥_top_isIso : IsIso (Œ¥ (htopexact) (d + 1) (d + 2) rfl) := by
    -- ...because `coind‚ÇÅ'^G M` has trivial cohomology as S-module
    -- have := M.coind‚ÇÅ'_trivialCohomology
    have : upsc_top.X‚ÇÇ.TrivialCohomology := Rep.TrivialCohomology.res_subtype (coind‚ÇÅ'.obj M)
    refine isIso_Œ¥_of_isZero (htopexact) (d + 1) ?_ ?_
    all_goals simpa only [upSES_X‚ÇÇ] using isZero_of_trivialCohomology
  let ih := cores_obj (up.obj M) (d + 1)
  (asIso (Œ¥ (htopexact) (d + 1) (d + 2) rfl)).inv ‚â´ ih ‚â´ (up_Œ¥_bottom_Iso).hom.app M

theorem cores_succ_naturality (n : ‚Ñï) (X Y : Rep R G) (f : X ‚ü∂ Y) [DecidableEq G] :
    (res S.subtype ‚ãô functor R (‚Ü•S) (n + 1)).map f ‚â´ cores_obj Y (n + 1) =
    cores_obj X (n + 1) ‚â´ (functor R G (n + 1)).map f := by
  revert X Y f
  induction n with
  | zero => exact fun _ _ _ ‚Ü¶ cores‚ÇÅ_naturality ..
  | succ n ih =>
    intro X Y f
    have := Œ¥_naturality (shortExact_upSES_res X S.subtype) (shortExact_upSES_res Y S.subtype)
      ((upShortComplex ‚ãô (res (R := R) S.subtype).mapShortComplex).map f) (n + 1) (n + 2) rfl
    simp only [Functor.comp_obj, functor_obj, Functor.comp_map, functor_map, cores_obj,
      ShortComplex.map_X‚ÇÉ, ShortComplex.map_X‚ÇÅ, asIso_inv, up_obj, Functor.id_obj, coind‚ÇÅ'_obj,
      Œ¥UpNatIso, Œ¥UpIso, NatIso.ofComponents_hom_app, asIso_hom, Category.assoc, IsIso.eq_inv_comp]
    rw [‚Üê Category.assoc]
    simp only [ShortComplex.map_X‚ÇÉ, upSES_X‚ÇÉ, up_obj, Functor.id_obj, coind‚ÇÅ'_obj,
      ShortComplex.map_X‚ÇÅ, upSES_X‚ÇÅ, Functor.comp_map, upShortComplex_obj,
      Functor.mapShortComplex_map_œÑ‚ÇÅ, upShortComplex_map_œÑ‚ÇÅ, Functor.mapShortComplex_map_œÑ‚ÇÉ,
      upShortComplex_map_œÑ‚ÇÉ, up_map] at this
    rw [this, Category.assoc, ‚Üê Category.assoc (Œ¥ _ _ _ _), IsIso.hom_inv_id, Category.id_comp,
      Œ¥_naturality (shortExact_upSES X) (shortExact_upSES Y) ‚ü®f, coind‚ÇÅ'.map f, up.map f, rfl,
      by aesop_cat‚ü© (n + 1) (n + 2) rfl, ‚Üê Category.assoc, ‚Üê Category.assoc]
    exact congr((¬∑ ‚â´ Œ¥ (shortExact_upSES _) _ _ _) $(ih (up.obj X) (up.obj Y) (up.map f)))

variable (R) (S) in
/-- Corestriction as a natural transformation. -/
def coresNatTrans (n : ‚Ñï) [DecidableEq G] : Rep.res S.subtype ‚ãô functor R S n ‚ü∂ functor R G n where
  app M := (groupCohomology.cores_obj M n)
  naturality X Y f := match n with
    | 0 => cores‚ÇÄ.naturality f
    | n + 1 => cores_succ_naturality n X Y f

lemma map_H0Iso_hom_f_apply'.{u} {k G H : Type u} [CommRing k] [Group G] [Group H] {A : Rep k H} {B : Rep k G}
    (f : G ‚Üí* H) (œÜ : A ‚Üì f ‚ü∂ B) (x : groupCohomology A 0) :
    (H0Iso B).hom.hom ((map f œÜ 0).hom x) =
    œÜ.hom.hom ((H0Iso A).hom.hom x : A) :=
  map_H0Iso_hom_f_apply ..

-- `simp` does a lot of work here, and it was quite some effort getting
-- it to do so, so I hope this proof never breaks...
lemma cores_res‚ÇÄ : rest (R := R) (S.subtype) 0 ‚â´ cores‚ÇÄ = S.index ‚Ä¢ (.id _) := by
  ext M v
  apply (ConcreteCategory.injective_of_mono_of_preservesPullback (H0Iso M).hom)
  ext
  simp [rest, Subgroup.index, groupCohomology.map_H0Iso_hom_f_apply' S.subtype,
    (M.œÅ.mem_invariants ((H0Iso M).hom.hom v)).1 (Subtype.prop _)]

/-!
            rest                       cores
H‚Åø(G, up M) ---> H‚Åø(S, upM ‚Üì S.subtype) ---> H‚Åø(G, up M)
    |                                         |
    | Œ¥                                       | Œ¥
    v       rest                       cores  v
H‚Åø‚Å∫¬π(G, M)  ---> H‚Åø‚Å∫¬π(S, M ‚Üì S.subtype) ---> H‚Åø‚Å∫¬π(G, M)

-/
lemma commSq‚Çô (n : ‚Ñï) [DecidableEq G] (M : Rep R G) :
    (rest S.subtype n ‚â´ coresNatTrans R S n).app (up.obj M) ‚â´ Œ¥ (shortExact_upSES M) n (n + 1) rfl =
    Œ¥ (shortExact_upSES M) n (n + 1) rfl ‚â´ (rest S.subtype (n + 1) ‚â´ coresNatTrans R S (n + 1)).app M := by
  rw [NatTrans.comp_app, NatTrans.comp_app]
  match n with
  | 0 =>
    exact comp_commSq _ _ _ _ _ _ _ (Œ¥ (shortExact_upSES_res M S.subtype) 0 1 rfl)
      (rest_Œ¥_naturality (shortExact_upSES M) S.subtype 0 1 rfl |>.symm) (commSq_cores‚ÇÅ ..|>.symm)
  | n + 1 =>
    refine comp_commSq _ _ _ _ _ _ _ (Œ¥ (shortExact_upSES_res M S.subtype) (n + 1) (n + 2) rfl)
      (rest_Œ¥_naturality (shortExact_upSES M) S.subtype (n + 1) (n + 2) rfl).symm ?_
    simp [-up_obj, coresNatTrans, cores_obj, Œ¥UpNatIso, Œ¥UpIso]

lemma cores_res (n : ‚Ñï) [DecidableEq G] :
    (rest (R := R) (S.subtype) n ‚â´ coresNatTrans R S n : functor R G n ‚ü∂ functor R G n) =
      S.index ‚Ä¢ (.id _) := by
  induction n with
  | zero => exact cores_res‚ÇÄ
  | succ n ih =>
    ext M : 2
    haveI : Epi (Œ¥ (shortExact_upSES M) n (n + 1) rfl) :=
    match n with
    | 0 => Œ¥_up_zero_epi ..
    | m + 1 => Œ¥_up_isIso M m|>.epi_of_iso _
    rw [‚Üê cancel_epi (Œ¥ (shortExact_upSES M) n (n + 1) rfl),  ‚Üê commSq‚Çô n M, ih]
    simp

/-- Any element of H^n-hat (n ‚àà ‚Ñ§) is `|G|`-torsion. -/
lemma torsion_of_finite_of_neZero {n : ‚Ñï} [NeZero n] [DecidableEq G] (M : Rep R G)
    (x : groupCohomology M n) : Nat.card G ‚Ä¢ x = 0 := by
  if hG : Infinite G then simp else
  simp only [not_infinite_iff_finite] at hG
  have := by simpa using (LinearMap.ext_iff.1 <| ModuleCat.hom_ext_iff.1
    congr(NatTrans.app $(cores_res (R := R) n (G := G) (S := ‚ä•)) M)) x
  simp [‚Üê this, rest, IsZero.eq_zero_of_tgt isZero_of_trivialCohomology <|
    map _ (ùüô (M ‚Üì (‚ä• : Subgroup G).subtype)) n]

-- /-- Any element of H^n-hat (n ‚àà ‚Ñ§) is `|G|`-torsion. -/
-- lemma tateCohomology_torsion {n : ‚Ñ§} [Fintype G] (M : Rep R G) (x : (tateCohomology n).obj M) :
--     Nat.card G ‚Ä¢ x = 0 := sorry

-- Should the above really be a statement about a functor?
-- Something like this?

-- instance (n : ‚Ñ§) [Finite G] : Functor.Additive (tateCohomology (R := R) (G := G) n) := sorry

-- this doesn't work
-- lemma tateCohomology_torsion' {n : ‚Ñ§} [Finite G] :
--     (Nat.card G) ‚Ä¢ (CategoryTheory.NatTrans.id (tateCohomology (R := R) (G := G) n)) = 0 := sorry

-- p^infty-torsion injects into H^(Sylow) (for group cohomology)
lemma groupCohomology_Sylow {n : ‚Ñï} (hn : 0 < n) [Finite G] (M : Rep R G)
    (x : groupCohomology M n) (p : ‚Ñï) (P : Sylow p G) (hx : ‚àÉ d, (p ^ d) ‚Ä¢ x = 0)
    (hx' : x ‚â† 0) : ((rest (P.toSubgroup.subtype) n).app M).hom x ‚â† 0 := by
  simp only [Functor.comp_obj, functor_obj, ne_eq]

  sorry

-- Want an analogous statement for Tate cohomology but I can't find restriction
-- in Tate cohomology
