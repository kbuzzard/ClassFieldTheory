import ClassFieldTheory.Cohomology.Functors.Inflation
import ClassFieldTheory.Mathlib.Algebra.Algebra.Equiv
import ClassFieldTheory.Mathlib.LinearAlgebra.Finsupp.Defs
import ClassFieldTheory.Mathlib.RepresentationTheory.Rep
import Mathlib.Data.Finsupp.Notation
import Mathlib.FieldTheory.Galois.NormalBasis
import Mathlib.RepresentationTheory.FiniteIndex

/-!
Let `G` be a group. We define two functors:

  `Rep.coind‚ÇÅ G : ModuleCat R ‚•§ Rep R G` and
  `Rep.ind‚ÇÅ G   : ModuleCat R ‚•§ Rep R G`.

For an `R`-module `A`, the representation `(coind‚ÇÅ G).obj A` is the space of functions `f : G ‚Üí A`,
with the action of `G` by right-translation. In other words `(g f) x = f (x g)` for `g : G`.

The space `(ind‚ÇÅ G).obj A` is `G ‚Üí‚ÇÄ A` with the action of `G` by right-translation, i.e.
`g (single x v) = single (x * g‚Åª¬π) v`.

Both `ind‚ÇÅ` and `coind‚ÇÅ` are defined as special cases of the functors `ind` and `coind` in Mathlib.

We also define two functors

  `coind‚ÇÅ' : Rep R G ‚•§ Rep R G` and
  `ind‚ÇÅ' : Rep R G ‚•§ Rep R G`.

For a representation `M` of `G`, the representation `coind‚ÇÅ'.obj M` is the representation of `G`
on `G ‚Üí M.V`, where the action of `G` is by `M.œÅ` on `M.V` and by right-translation on `G`.

`ind‚ÇÅ'.obj M` is the representation of `G` on `G ‚Üí‚ÇÄ M.V`, where the action of `G` is by `M.œÅ` on
`M.V` and by right-translation on `G`.

We define the canonical monomorphism `coind‚ÇÅ'_Œπ : M ‚ü∂ coind‚ÇÅ'.obj M` which takes a vector `v` to
the constant function on `G` with value `v`.

We define the canonical epimorphism `ind‚ÇÅ'_œÄ : ind‚ÇÅ'.obj M ‚ü∂ M` which takes a finitely supported
function to the sum of its values.

We prove that `ind‚ÇÅ'.obj M` is isomorphic to `(ind‚ÇÅ G).obj M.V`.
Similarly, we show that `coind‚ÇÅ'.obj M` is isomorphic to `(coind‚ÇÅ G).obj M.V`.

## Implementation notes

`ind‚ÇÅ`/`coind‚ÇÅ` are defined as the base change of finsupp/pi quotiented out by the trivial
relation.
This is because they are abbrevs of the general construction from mathlib.

Instead of redefining them as `G ‚Üí‚ÇÄ A`/`G ‚Üí A` with the `G`-action on the domain, which would break
the defeq with the general construction, we provide `ind‚ÇÅAsFinsupp`/`coind‚ÇÅAsPi`, a version of
`ind‚ÇÅ`/`coind‚ÇÅ` that's actually defined as `G ‚Üí‚ÇÄ A`/`G ‚Üí A`.

`ind‚ÇÅAsFinsupp`/`coind‚ÇÅAsPi` are not bundled as functors because they should only be used for
pointwise computations.
-/

open
  Finsupp
  Representation
  Rep
  CategoryTheory
  NatTrans
  ConcreteCategory
  Limits
  groupCohomology

noncomputable section

variable (R G : Type) [CommRing R] [Group G]

namespace Representation

variable (V W : Type) [AddCommGroup V] [Module R V] [AddCommGroup W] [Module R W]

abbrev coind‚ÇÅV := coindV (‚ä• : Subgroup G).subtype (trivial R _ V)

instance : FunLike (coind‚ÇÅV R G V) G V where
  coe f := f.val
  coe_injective' := Subtype.val_injective

instance : Coe (G ‚Üí V) (coind‚ÇÅV R G V) where
  coe f := ‚ü®f,by
    intro ‚ü®g, hg‚ü© x
    rw [Subgroup.mem_bot] at hg
    simp [hg]
  ‚ü©

/--
The representation of `G` on the space `G ‚Üí V` by right-translation on `G`.
(`V` is an `R`-module with no action of `G`).
-/
abbrev coind‚ÇÅ := coind (‚ä• : Subgroup G).subtype (trivial R _ V)

@[simp] lemma coind‚ÇÅ_apply‚ÇÉ (f : coind‚ÇÅV R G V) (g x : G) : coind‚ÇÅ R G V g f x = f (x * g) := rfl

abbrev Ind‚ÇÅV := IndV (‚ä• : Subgroup G).subtype (trivial R _ V)
abbrev Ind‚ÇÅV.mk := IndV.mk (‚ä• : Subgroup G).subtype (trivial R _ V)
/--
The induced representation of a group `G` on `G ‚Üí‚ÇÄ V`, where the action of `G` is by
left-translation on `G`; no action of `G` on `V` is assumed.
-/
abbrev ind‚ÇÅ := ind (‚ä• : Subgroup G).subtype (trivial R _ V)

lemma ind‚ÇÅ_apply (g x : G) : (ind‚ÇÅ R G V) g ‚àò‚Çó Ind‚ÇÅV.mk R G V x = Ind‚ÇÅV.mk R G V (x * g‚Åª¬π) := by
  ext; simp

/-- A version of `ind‚ÇÅ` that's actually defined as an action on `G ‚Üí‚ÇÄ A`. -/
def ind‚ÇÅAsFinsupp : Representation R G (G ‚Üí‚ÇÄ V) where
  toFun g := (mapDomain.linearEquiv _ _ <| .symm <| .mulRight g).toLinearMap
  map_one' := by simp [Module.End.one_eq_id]
  map_mul' := by simp [Module.End.mul_eq_comp, -Equiv.mulRight_mul, ‚Üê Finsupp.lmapDomain_comp]

/-- A version of `coind‚ÇÅ` that's actually defined as `G ‚Üí A` with some action. -/
def coind‚ÇÅAsPi : Representation R G (G ‚Üí V) where
  toFun g := (LinearEquiv.funCongrLeft _ _ <| .mulRight g).toLinearMap
  map_one' := by simp [Module.End.one_eq_id, Equiv.Perm.one_def]
  map_mul' := by simp [Module.End.mul_eq_comp, Equiv.Perm.mul_def]

variable {R G V} (œÅ : Representation R G V)

@[simp] lemma ind‚ÇÅAsFinsupp_single (g x : G) (v : V) :
    ind‚ÇÅAsFinsupp R G V g (.single x v) = .single (x * g‚Åª¬π) v := by simp [ind‚ÇÅAsFinsupp]

@[simp] lemma coind‚ÇÅAsPi_single [DecidableEq G] (g x : G) (v : V) :
    coind‚ÇÅAsPi R G V g (Pi.single x v) = Pi.single (x * g‚Åª¬π) v := by
  ext
  simp [coind‚ÇÅAsPi, LinearMap.funLeft, Function.comp_def, Pi.single, Function.update,
    eq_mul_inv_iff_mul_eq]

@[simp]
lemma ind‚ÇÅAsFinsupp_apply (g : G) (f : G ‚Üí‚ÇÄ V) (x : G) :
    ind‚ÇÅAsFinsupp R G V g f x = f (x * g) := by
  simp [ind‚ÇÅAsFinsupp, -mapDomain.toLinearMap_linearEquiv, -mapDomain.coe_linearEquiv,
    -toLinearMap_mapDomainLinearEquiv]

@[simp]
lemma coind‚ÇÅAsPi_apply (g : G) (f : G ‚Üí V) (x : G) : coind‚ÇÅAsPi R G V g f x = f (x * g) := rfl

/--
Given a representation `œÅ` of `G` on `V`, `coind‚ÇÅ' œÅ` is the representation of `G`
on `G ‚Üí V`, where the action of `G` is `(g f) x = œÅ g (f (x * g))`.
-/
@[simps] def coind‚ÇÅ' : Representation R G (G ‚Üí V) where
  toFun g := {
    toFun f x := œÅ g (f (x * g))
    map_add' _ _ := by ext; simp
    map_smul' _ _ := by ext; simp
  }
  map_one' := by ext; simp
  map_mul' g‚ÇÅ g‚ÇÇ := by ext; simp [mul_assoc]

@[simp] lemma coind‚ÇÅ'_apply‚ÇÉ (g x : G) (f : G ‚Üí V) : coind‚ÇÅ' œÅ g f x = œÅ g (f (x * g)) := rfl

/--
The linear bijection from `G ‚Üí V` to `G ‚Üí V`, which gives intertwines the
representations `coind‚ÇÅ' œÅ` and `coind‚ÇÅ R G V`.
-/
@[simps] def coind‚ÇÅ'_lequiv_coind‚ÇÅ : (G ‚Üí V) ‚âÉ‚Çó[R] coind‚ÇÅV R G V where
  toFun f       := fun x ‚Ü¶ œÅ x (f x)
  map_add' _ _  := by ext; simp
  map_smul' _ _ := by ext; simp
  invFun f x    := œÅ x‚Åª¬π (f x)
  left_inv f    := by ext; apply inv_self_apply
  right_inv _   := by ext; simp; rfl

lemma coind‚ÇÅ'_lequiv_coind‚ÇÅ_comm (g : G) :
    coind‚ÇÅ'_lequiv_coind‚ÇÅ œÅ ‚àò‚Çó coind‚ÇÅ' œÅ g = coind‚ÇÅ R G V g ‚àò‚Çó coind‚ÇÅ'_lequiv_coind‚ÇÅ œÅ := by
  ext; simp

/--
The linear map from `V` to `G ‚Üí V` taking a vector `v : V` to the comstant function
with value `V`. If `œÅ` is a representation of `G` on `V`, then this map intertwines
`œÅ` and `œÅ.coind‚ÇÅ'`.
-/
@[simps] def coind‚ÇÅ'_Œπ : V ‚Üí‚Çó[R] (G ‚Üí V) where
  toFun     := Function.const G
  map_add'  := by simp
  map_smul' := by simp

/--
The map `coind‚ÇÅ'_Œπ` intertwines a representation `œÅ` of `G` on `V` with the
representation `œÅ.coind‚ÇÅ'` of `G` on `G ‚Üí V`.
-/
lemma coind‚ÇÅ'_Œπ_comm (g : G) : coind‚ÇÅ' œÅ g ‚àò‚Çó coind‚ÇÅ'_Œπ = coind‚ÇÅ'_Œπ ‚àò‚Çó œÅ g := by ext; simp

variable {W X : Type} [AddCommGroup W] [Module R W] [AddCommGroup X] [Module R X]

/--
`ind‚ÇÅ' œÅ` is the representation of `G` on `G ‚Üí‚ÇÄ V`, where the action is defined by
`œÅ.ind‚ÇÅ' g f x = œÅ g (f (x * g))`.
-/
@[simps] def ind‚ÇÅ' : Representation R G (G ‚Üí‚ÇÄ V) where
  toFun g := lmapDomain _ _ (fun x ‚Ü¶ x * g‚Åª¬π) ‚àò‚Çó mapRange.linearMap (œÅ g)
  map_one' := by ext; simp
  map_mul' _ _ := by ext; simp [mul_assoc]

lemma ind‚ÇÅ'_apply‚ÇÇ (f : G ‚Üí‚ÇÄ V) (g x : G) : œÅ.ind‚ÇÅ' g f x = œÅ g (f (x * g)) := by
  dsimp only [ind‚ÇÅ'_apply, LinearMap.coe_comp, Function.comp_apply, mapRange.linearMap_apply,
    lmapDomain_apply]
  have : x = x * g * g‚Åª¬π := eq_mul_inv_of_mul_eq rfl
  rw [this, mapDomain_apply (mul_left_injective g‚Åª¬π)]
  simp

private abbrev ind‚ÇÅ'_map (f : V ‚Üí‚Çó[R] W) : (G ‚Üí‚ÇÄ V) ‚Üí‚Çó[R] (G ‚Üí‚ÇÄ W) := mapRange.linearMap f

omit [Group G] in
private lemma ind‚ÇÅ'_map_comp_lsingle (f : V ‚Üí‚Çó[R] W) (x : G) :
    (ind‚ÇÅ'_map f) ‚àò‚Çó (lsingle x) = (lsingle x) ‚àò‚Çó f  := by
  ext; simp

@[simp] lemma ind‚ÇÅ'_comp_lsingle (g x : G) : œÅ.ind‚ÇÅ' g ‚àò‚Çó lsingle x = lsingle (x * g‚Åª¬π) ‚àò‚Çó œÅ g := by
  ext; simp

lemma ind‚ÇÅ'_map_comm {œÅ' : Representation R G W} {f : V ‚Üí‚Çó[R] W}
    (hf : ‚àÄ g : G, f ‚àò‚Çó œÅ g = œÅ' g ‚àò‚Çó f) (g : G) :
    ind‚ÇÅ'_map f ‚àò‚Çó œÅ.ind‚ÇÅ' g = œÅ'.ind‚ÇÅ' g ‚àò‚Çó ind‚ÇÅ'_map f := by
  ext : 1
  rw [LinearMap.comp_assoc, ind‚ÇÅ'_comp_lsingle, ‚Üê LinearMap.comp_assoc, ind‚ÇÅ'_map_comp_lsingle,
    LinearMap.comp_assoc, hf, LinearMap.comp_assoc, ind‚ÇÅ'_map_comp_lsingle,
    ‚Üê LinearMap.comp_assoc, ‚Üê LinearMap.comp_assoc, ind‚ÇÅ'_comp_lsingle]

@[simps] def ind‚ÇÅ'_œÄ : (G ‚Üí‚ÇÄ V) ‚Üí‚Çó[R] V where
  toFun f := f.sum (fun _ ‚Ü¶ (1 : V ‚Üí‚Çó[R] V))
  map_add' _ _ :=  sum_add_index' (congrFun rfl) fun _ _ ‚Ü¶ congrFun rfl
  map_smul' _ _ := by simp

omit [Group G] in
@[simp] lemma ind‚ÇÅ'_œÄ_comp_lsingle (x : G) :
    ind‚ÇÅ'_œÄ ‚àò‚Çó lsingle x = LinearMap.id (R := R) (M := V) := by
  ext
  simp

lemma ind‚ÇÅ'_œÄ_comm (g : G) : ind‚ÇÅ'_œÄ ‚àò‚Çó ind‚ÇÅ' œÅ g = œÅ g ‚àò‚Çó ind‚ÇÅ'_œÄ := by
  ext; simp

@[simps] def ind‚ÇÅ'_lmap : (G ‚Üí‚ÇÄ V) ‚Üí‚Çó[R] Ind‚ÇÅV R G V where
  toFun f:= f.sum (fun g v ‚Ü¶ Ind‚ÇÅV.mk R G V g (œÅ g v))
  map_add' _ _ := by
    rw [sum_add_index']
    ¬∑ simp
    ¬∑ intros
      simp only [map_add]
  map_smul' _ _ := by
    rw [sum_smul_index']
    ¬∑ simp only [map_smul, RingHom.id_apply, smul_sum]
    ¬∑ intro
      simp only [map_zero]

def ind‚ÇÅ'_invlmap_aux : (G ‚Üí‚ÇÄ R) ‚Üí‚Çó[R] V ‚Üí‚Çó[R] G ‚Üí‚ÇÄ V where
  toFun f := {
    toFun v := f.sum (fun g r ‚Ü¶ Finsupp.single g (r ‚Ä¢ (œÅ g‚Åª¬π v)))
    map_add' v1 v2 := by simp
    map_smul' r v := by simp [Finsupp.smul_sum, smul_smul, mul_comm]}
  map_add' f1 f2 := by
    ext v g
    simp only [LinearMap.coe_mk, AddHom.coe_mk, sum_apply, LinearMap.add_apply, coe_add, coe_sum,
      Pi.add_apply]
    rw [Finsupp.sum_add_index' (f := f1) (g := f2) (by simp) (by simp [add_smul]),
      Finsupp.sum_apply', Finsupp.sum_apply']
  map_smul' r f := by
    ext : 1
    simp only [LinearMap.coe_mk, AddHom.coe_mk, RingHom.id_apply, LinearMap.smul_apply]
    rw [Finsupp.sum_smul_index' (by simp), Finsupp.smul_sum]
    simp [smul_smul]

def ind‚ÇÅ'_invlmap : Ind‚ÇÅV R G V ‚Üí‚Çó[R] (G ‚Üí‚ÇÄ V) :=
  (TensorProduct.lift (ind‚ÇÅ'_invlmap_aux œÅ)).comp ((Coinvariants.ker _).quotEquivOfEqBot (by
    simpa [Coinvariants.ker, sub_eq_zero]
     using fun a ‚Ü¶ by exact LinearMap.congr_fun TensorProduct.map_id a)).toLinearMap

/--
The linear automorphism of `G ‚Üí‚ÇÄ V`, which gives an isomorphism
between `ind‚ÇÅ' œÅ` and `ind‚ÇÅ R G V`.
-/
@[simps!] def ind‚ÇÅ'_lequiv : (G ‚Üí‚ÇÄ V) ‚âÉ‚Çó[R] Ind‚ÇÅV R G V where
  toLinearMap := ind‚ÇÅ'_lmap œÅ
  invFun := ind‚ÇÅ'_invlmap œÅ
  left_inv f := by
    rw [LinearMap.toFun_eq_coe]
    induction f using Finsupp.induction_linear
    ¬∑ simp
    ¬∑ rename_i f g h1 h2
      rw [map_add, map_add, h1, h2]
    ¬∑ rename_i g v
      simp [ind‚ÇÅ'_invlmap, Submodule.quotEquivOfEqBot, Coinvariants.mk]
      change (TensorProduct.lift œÅ.ind‚ÇÅ'_invlmap_aux)
        (LinearMap.id (R := R) ((fun‚ÇÄ | g => (1 : R)) ‚äó‚Çú[R] (œÅ g) v)) = fun‚ÇÄ | g => v
      simp [ind‚ÇÅ'_invlmap_aux]
  right_inv f := by
    rw [LinearMap.toFun_eq_coe]
    induction f using Submodule.Quotient.induction_on with | H f
    induction f using TensorProduct.induction_on with
    | zero => simp
    | add f g h1 h2 =>
      rw [‚Üê Submodule.mkQ_apply, map_add, map_add, map_add,
        Submodule.mkQ_apply, Submodule.mkQ_apply, h1, h2]
    | tmul x y =>
    simp only [ind‚ÇÅ'_invlmap, Submodule.quotEquivOfEqBot,
      LinearEquiv.ofLinear_toLinearMap, LinearMap.coe_comp, Function.comp_apply]
    change œÅ.ind‚ÇÅ'_lmap (TensorProduct.lift œÅ.ind‚ÇÅ'_invlmap_aux (LinearMap.id (R := R) (x ‚äó‚Çú[R] y)))
      = Submodule.Quotient.mk (x ‚äó‚Çú[R] y)
    simp only [LinearMap.id_coe, id_eq, TensorProduct.lift.tmul]
    induction x using Finsupp.induction_linear with
    | zero => simp
    | add f g h1 h2 =>
      rw [map_add, LinearMap.add_apply, map_add, h1, h2, TensorProduct.add_tmul]
      rfl
    | single g r =>
      simp [ind‚ÇÅ'_invlmap_aux, Coinvariants.mk]
      rw [‚Üê map_smul, ‚Üê Submodule.mkQ_apply]
      congr
      rw [TensorProduct.smul_tmul', Finsupp.smul_single, smul_eq_mul, mul_one]

@[simp] lemma ind‚ÇÅ'_lequiv_comp_lsingle (x : G) :
    œÅ.ind‚ÇÅ'_lequiv ‚àò‚Çó lsingle x = Ind‚ÇÅV.mk R G V x ‚àò‚Çó œÅ x := by ext; simp

lemma ind‚ÇÅ'_lequiv_comm (g : G) :
    ind‚ÇÅ'_lequiv œÅ ‚àò‚Çó ind‚ÇÅ' œÅ g = ind‚ÇÅ R G V g ‚àò‚Çó ind‚ÇÅ'_lequiv œÅ := by
  ext x : 1
  rw [LinearMap.comp_assoc, ind‚ÇÅ'_comp_lsingle,
    ‚Üê LinearMap.comp_assoc, ind‚ÇÅ'_lequiv_comp_lsingle, LinearMap.comp_assoc, map_mul]
  change _ ‚àò‚Çó (_ * œÅ g) = _
  rw [mul_assoc, ‚Üê map_mul, inv_mul_cancel, map_one, mul_one]
  nth_rw 2 [LinearMap.comp_assoc]
  rw [ind‚ÇÅ'_lequiv_comp_lsingle, ‚Üê LinearMap.comp_assoc, ind‚ÇÅ_apply]

def ind‚ÇÅ'_lequiv_coind‚ÇÅ' [Finite G] : (G ‚Üí‚ÇÄ V) ‚âÉ‚Çó[R] (G ‚Üí V) :=
  linearEquivFunOnFinite R V G

omit [Group G] in
lemma ind‚ÇÅ'_lequiv_coind‚ÇÅ'_apply [Finite G] (x y : G) (v : V) :
  ind‚ÇÅ'_lequiv_coind‚ÇÅ' (R := R) (single x v) y = (single x v y) := rfl

lemma ind‚ÇÅ'_lequiv_coind‚ÇÅ'_comm [Finite G] (g : G) :
    ind‚ÇÅ'_lequiv_coind‚ÇÅ'.toLinearMap ‚àò‚Çó œÅ.ind‚ÇÅ' g = œÅ.coind‚ÇÅ' g ‚àò‚Çó ind‚ÇÅ'_lequiv_coind‚ÇÅ'.toLinearMap
    := by
  ext x : 1
  rw [LinearMap.comp_assoc, LinearMap.comp_assoc, ind‚ÇÅ'_comp_lsingle]
  ext v y : 2
  simp [ind‚ÇÅ'_lequiv_coind‚ÇÅ'_apply]
  by_cases h : x * g‚Åª¬π = y
  ¬∑ rw [h, single_eq_same, ‚Üê h, mul_assoc, inv_mul_cancel, mul_one, single_eq_same]
  ¬∑ rw [single_eq_of_ne, single_eq_of_ne, map_zero]
    ¬∑ contrapose! h
      rw [‚Üê h, mul_inv_cancel_right]
    ¬∑ exact Ne.symm h

lemma ind‚ÇÅ'_lequiv_coind‚ÇÅ'_comm' [Finite G] (g : G) :
    ind‚ÇÅ'_lequiv_coind‚ÇÅ'.symm.toLinearMap ‚àò‚Çó œÅ.coind‚ÇÅ' g
    = œÅ.ind‚ÇÅ' g ‚àò‚Çó ind‚ÇÅ'_lequiv_coind‚ÇÅ'.symm.toLinearMap := by
  ext f : 1
  simp only [LinearMap.coe_comp, LinearEquiv.coe_coe, Function.comp_apply]
  rw [LinearEquiv.symm_apply_eq]
  symm
  change (ind‚ÇÅ'_lequiv_coind‚ÇÅ'.toLinearMap ‚àò‚Çó (œÅ.ind‚ÇÅ' g)) _ = (œÅ.coind‚ÇÅ' g) f
  rw [ind‚ÇÅ'_lequiv_coind‚ÇÅ'_comm, LinearMap.comp_apply, LinearEquiv.coe_coe,
    LinearEquiv.apply_symm_apply]

lemma coind‚ÇÅ'_Œπ_app_injective : Function.Injective (@coind‚ÇÅ'_Œπ R G _ V _ _) := by
  intro _ _ h
  change Function.const _ _ = Function.const _ _ at h
  simpa using h

end Representation

namespace Rep

variable {R} (M : Rep R G) (A : ModuleCat R)

abbrev coind‚ÇÅ : ModuleCat R ‚•§ Rep R G :=
  trivialFunctor R (‚ä• : Subgroup G) ‚ãô coindFunctor R (‚ä• : Subgroup G).subtype

variable {G}

def coind‚ÇÅ_quotientToInvariants_iso_aux1 {Q : Type} [Group Q] (œÜ : G ‚Üí* Q) :
    invariants (((coind‚ÇÅ G).obj A).œÅ.comp œÜ.ker.subtype) ‚âÉ‚Çó[R]
      coindV (‚ä• : Subgroup (G ‚ß∏ œÜ.ker)).subtype (trivial R (‚ä• : Subgroup (G ‚ß∏ œÜ.ker)) A).œÅ where
  toFun x := ‚ü®Quotient.lift x.1.1 (fun a b hab ‚Ü¶ by
    nth_rw 1 [‚Üê x.2 ‚ü®a‚Åª¬π * b, QuotientGroup.leftRel_apply.mp hab‚ü©]
    simp), by simp [coindV]‚ü©
  map_add' x y := by
    ext x
    induction x using QuotientGroup.induction_on
    simp
  map_smul' r x := by
    ext x
    induction x using QuotientGroup.induction_on
    simp
  invFun x := ‚ü®‚ü®x.1.comp QuotientGroup.mk, by simp [coindV, trivialFunctor]‚ü©, fun a ‚Ü¶ by
    simpa [‚Üê Subtype.val_inj] using funext (by simp)‚ü©
  left_inv := by simpa [Function.LeftInverse] using fun _ _ _ ‚Ü¶ funext (by simp)
  right_inv := by
    simp only [Function.RightInverse, Function.LeftInverse, trivialFunctor_obj_V,
      Functor.comp_obj, coindFunctor_obj, of_œÅ, Subtype.forall, Subtype.mk.injEq]
    intro _ _
    ext x
    induction x using QuotientGroup.induction_on
    simp

def coind‚ÇÅ_quotientToInvariants_iso_aux2 {H : Type} [Group H] (œÜ : G ‚âÉ* H) :
    (coindV (‚ä• : Subgroup G).subtype
    ((trivialFunctor R (‚ä• : Subgroup G)).obj A).œÅ) ‚âÉ‚Çó[R]
    ‚Ü•(coindV (‚ä• : Subgroup H).subtype ((trivialFunctor R (‚ä• : Subgroup H)).obj A).œÅ) where
  toFun x := ‚ü®x.1.comp œÜ.symm, by
    simp [coindV, trivialFunctor]‚ü©
  map_add' _ _ := rfl
  map_smul' _ _ := rfl
  invFun y := ‚ü®y.1.comp œÜ, by simp [coindV, trivialFunctor]‚ü©
  left_inv := by simpa [Function.LeftInverse] using fun a ha ‚Ü¶ by simp [Function.comp_assoc]
  right_inv := by
    simpa [Function.RightInverse, Function.LeftInverse] using
      fun a ha ‚Ü¶ by simp [Function.comp_assoc]

def coind‚ÇÅ_quotientToInvariants_iso {Q : Type} [Group Q] {œÜ : G ‚Üí* Q}
    (surj : Function.Surjective œÜ) :
    (((coind‚ÇÅ G).obj A) ‚Üë surj) ‚âÖ (coind‚ÇÅ Q).obj A := by
  refine mkIso _ _ (LinearEquiv.toModuleIso ((coind‚ÇÅ_quotientToInvariants_iso_aux1 A œÜ).trans
    (coind‚ÇÅ_quotientToInvariants_iso_aux2 A (QuotientGroup.quotientKerEquivOfSurjective œÜ surj))))
    (fun q x ‚Ü¶ ?_)
  simp only [Functor.comp_obj, coindFunctor_obj, trivialFunctor_obj_V, of_œÅ, ModuleCat.of_coe,
    LinearEquiv.toModuleIso_hom, ModuleCat.hom_ofHom, LinearEquiv.coe_coe, LinearEquiv.trans_apply,
    coind_apply]
  ext q'
  obtain ‚ü®g, rfl‚ü© := surj q
  obtain ‚ü®g', rfl‚ü© := surj q'
  simp [coind‚ÇÅ_quotientToInvariants_iso_aux2, coind‚ÇÅ_quotientToInvariants_iso_aux1,
    ‚Üê QuotientGroup.mk_mul]

/--
The functor which takes a representation `œÅ` of `G` on `V` to the
coinduced representation on `G ‚Üí V`, where the action of `G` is by `œÅ` in `V` and by
right translation on `G`.
-/
@[simps obj]
def coind‚ÇÅ' : Rep R G ‚•§ Rep R G where
  obj M := of M.œÅ.coind‚ÇÅ'
  map œÜ := {
    hom := ofHom ((ModuleCat.Hom.hom œÜ.hom).compLeft G)
    comm g := by
      ext
      change (Action.œÅ _ g ‚â´ œÜ.hom) _ = _
      rw [œÜ.comm]
      rfl
  }
  map_id _ := rfl
  map_comp _ _ := rfl

/--
The inclusion of a representation `M` of `G` in the coinduced representation `coind‚ÇÅ'.obj M`.
This map takes an element `m : M` to the constant function with value `M`.
-/
@[simps] def coind‚ÇÅ'_Œπ : ùü≠ (Rep R G) ‚ü∂ coind‚ÇÅ' where
  app M := {
    hom    := ofHom Representation.coind‚ÇÅ'_Œπ
    comm _ := by ext : 1; exact M.œÅ.coind‚ÇÅ'_Œπ_comm _
  }
  naturality _ _ _ := by simpa using by rfl

instance : Mono (coind‚ÇÅ'_Œπ.app M) := by
  refine (mono_iff_injective (coind‚ÇÅ'_Œπ.app M)).mpr ?_
  intro x y eq
  change Function.const G x 1 = Function.const G y 1
  exact congrFun eq 1

@[simps] def coind‚ÇÅ'_obj_iso_coind‚ÇÅ : coind‚ÇÅ'.obj M ‚âÖ (coind‚ÇÅ G).obj M.V where
  hom.hom := ofHom M.œÅ.coind‚ÇÅ'_lequiv_coind‚ÇÅ.toLinearMap
  inv.hom := ofHom M.œÅ.coind‚ÇÅ'_lequiv_coind‚ÇÅ.symm.toLinearMap
  hom.comm g := by ext : 1; exact M.œÅ.coind‚ÇÅ'_lequiv_coind‚ÇÅ_comm g
  inv.comm g := by
    ext f
    simp only [Functor.comp_obj, coindFunctor_obj, trivialFunctor_obj_V,
      RingHom.toMonoidHom_eq_coe, RingEquiv.toRingHom_eq_coe, MonoidHom.coe_comp,
      MonoidHom.coe_coe, RingHom.coe_coe, Function.comp_apply, coind_apply, ModuleCat.hom_comp,
      ModuleCat.hom_ofHom, LinearMap.coe_comp, œÅ_hom]
    simp [coind‚ÇÅ', Representation.coind‚ÇÅ', coind‚ÇÅ'_lequiv_coind‚ÇÅ]
    congr

variable (G)

/--
The functor taking an `R`-module `A` to the induced representation of `G` on `G ‚Üí‚ÇÄ A`,
where the action of `G` is by left-translation.
-/
def ind‚ÇÅ : ModuleCat R ‚•§ Rep R G :=
  trivialFunctor R (‚ä• : Subgroup G) ‚ãô indFunctor R (‚ä• : Subgroup G).subtype

@[simp] lemma ind‚ÇÅ_obj_œÅ (A : ModuleCat R) : ((ind‚ÇÅ G).obj A).œÅ = Representation.ind‚ÇÅ R G A := rfl

variable {G}

/--
The functor taking a representation `M` of `G` to the induced representation on
the space `G ‚Üí‚ÇÄ M`. The action of `G` on `G ‚Üí‚ÇÄ M.V` is by left-translation on `G` and
by `M.œÅ` on `M.V`.
-/
@[simps! obj, simps! obj_œÅ, simps! obj_œÅ_apply]
def ind‚ÇÅ' : Rep R G ‚•§ Rep R G where
  obj M := of M.œÅ.ind‚ÇÅ'
  map f := {
    hom := ofHom (Representation.ind‚ÇÅ'_map f.hom.hom)
    comm g := by
      ext : 1
      apply ind‚ÇÅ'_map_comm
      intro g
      simpa [ConcreteCategory.ext_iff] using f.comm g
  }
  map_id _ := by
    ext : 2
    exact mapRange.linearMap_id
  map_comp _ _ := by
    ext : 2
    exact mapRange.linearMap_comp _ _

/--
The natural projection `ind‚ÇÅ'.obj M ‚ü∂ M`, which takes `f : G ‚Üí‚ÇÄ M.V` to the sum of the
values of `f`.
-/
@[simps! app_hom]
def ind‚ÇÅ'_œÄ : ind‚ÇÅ' ‚ü∂ ùü≠ (Rep R G) where
  app M := ofHom (C := Rep R G) {
    val := Representation.ind‚ÇÅ'_œÄ
    property g := by
      rw [‚Üê LinearMap.coe_comp, ‚Üê LinearMap.coe_comp, ‚Üê DFunLike.ext'_iff]
      apply ind‚ÇÅ'_œÄ_comm
  }
  naturality _ _ x := by
    ext z
    change Representation.ind‚ÇÅ'_œÄ ((ind‚ÇÅ'.map x).hom.hom z) =
      x.hom.hom (Representation.ind‚ÇÅ'_œÄ z)
    simp [ind‚ÇÅ', sum_mapRange_index]
    exact (map_finsuppSum x.hom.hom z _).symm

instance instEpiAppInd‚ÇÅ'_œÄ : Epi (ind‚ÇÅ'_œÄ.app M) := by
  refine (epi_iff_surjective (ind‚ÇÅ'_œÄ.app M)).2 fun m ‚Ü¶ ‚ü®single 1 m, ?_‚ü©
  change Representation.ind‚ÇÅ'_œÄ _ = _
  simp only [Functor.id_obj, ind‚ÇÅ'_œÄ_apply, Module.End.one_apply, sum_single_index]

def ind‚ÇÅ'_obj_iso_ind‚ÇÅ : ind‚ÇÅ'.obj M ‚âÖ (ind‚ÇÅ G).obj M.V :=
  mkIso _ _ (LinearEquiv.toModuleIso M.œÅ.ind‚ÇÅ'_lequiv) fun g x ‚Ü¶
    LinearMap.congr_fun (ind‚ÇÅ'_lequiv_comm M.œÅ g) x

variable (G) in
/-- A version of `ind‚ÇÅ` that's actually defined as `G ‚Üí‚ÇÄ A` with some action. -/
abbrev ind‚ÇÅAsFinsupp : Rep R G := .of <| Representation.ind‚ÇÅAsFinsupp R G A

variable (G) in
/-- A version of `coind‚ÇÅ` that's actually defined as `G ‚Üí A` with some action. -/
abbrev coind‚ÇÅAsPi : Rep R G := .of <| Representation.coind‚ÇÅAsPi R G A

/-- `ind‚ÇÅAsFinsupp` is isomorphic to `ind‚ÇÅ` pointwise. -/
def ind‚ÇÅAsFinsuppIso : ind‚ÇÅAsFinsupp G A ‚âÖ (ind‚ÇÅ G).obj A :=
  mkIso _ _ (Iso.refl (ModuleCat.of R (G ‚Üí‚ÇÄ A))) ‚â™‚â´ ind‚ÇÅ'_obj_iso_ind‚ÇÅ (.trivial _ _ _)

/-- `coind‚ÇÅAsPi` is isomorphic to `coind‚ÇÅ` pointwise. -/
def coind‚ÇÅAsPiIso : coind‚ÇÅAsPi G A ‚âÖ (coind‚ÇÅ G).obj (.of R A) :=
  mkIso _ _ (Iso.refl (ModuleCat.of R (G ‚Üí A))) ‚â™‚â´ coind‚ÇÅ'_obj_iso_coind‚ÇÅ (.trivial _ _ _)

section FiniteGroup

variable (A : ModuleCat R)
set_option linter.unusedSectionVars false

-- Hack:
instance : DecidableRel ‚áë(QuotientGroup.rightRel (‚ä• : Subgroup G)) :=
  Classical.decRel _

abbrev ind‚ÇÅ_obj_iso_coind‚ÇÅ_obj [Finite G] : (ind‚ÇÅ G).obj A ‚âÖ (coind‚ÇÅ G).obj A :=
  indCoindIso _

def ind‚ÇÅ'_iso_coind‚ÇÅ' [Finite G] : ind‚ÇÅ' (R := R) (G := G) ‚âÖ coind‚ÇÅ' where
  hom := {
    app M := {
      hom := ofHom ind‚ÇÅ'_lequiv_coind‚ÇÅ'.toLinearMap
      comm g := by
        ext : 1
        apply ind‚ÇÅ'_lequiv_coind‚ÇÅ'_comm
    }
  }
  inv := {
    app M := {
      hom := ofHom ind‚ÇÅ'_lequiv_coind‚ÇÅ'.symm.toLinearMap
      comm g := by
        ext : 1
        apply ind‚ÇÅ'_lequiv_coind‚ÇÅ'_comm'
    }
    naturality _ _ _ := by
      ext : 3
      change ind‚ÇÅ'_lequiv_coind‚ÇÅ'.symm _ = _
      rw [LinearEquiv.symm_apply_eq]
      rfl
  }

lemma ind‚ÇÅ'_iso_coind‚ÇÅ'_app_apply [Finite G] (f : G ‚Üí‚ÇÄ M.V) (x : G) :
    (ind‚ÇÅ'_iso_coind‚ÇÅ'.app M).hom f x = f x := by
  rfl

end FiniteGroup

variable (K L : Type) [Field K] [Field L] [Algebra K L] [IsGalois K L] [FiniteDimensional K L]

/-- For a finite Galois extension `L/K`, the isomorphism between `ind‚ÇÅ` of `K`
and `L` in the category of `(L ‚âÉ‚Çê[K] L)`-representations. -/
noncomputable def iso_ind‚ÇÅ :
    (Rep.ind‚ÇÅ (L ‚âÉ‚Çê[K] L)).obj (.of K K) ‚âÖ .of (AlgEquiv.toLinearMapHom K L) := by
  classical
  refine (Rep.ind‚ÇÅAsFinsuppIso (G := (L ‚âÉ‚Çê[K] L)) (.of K K)).symm ‚â™‚â´
    mkIso _ _ (LinearEquiv.toModuleIso
      ((IsGalois.normalBasis K L).reindex (Equiv.inv (L ‚âÉ‚Çê[K] L))).repr.symm) ?_
  intro x f
  simp only [LinearEquiv.toModuleIso_hom, Module.Basis.coe_repr_symm, Module.Basis.coe_reindex,
    Equiv.inv_symm, Equiv.inv_apply, ModuleCat.hom_ofHom, AlgEquiv.toLinearMapHom]
  rw [Finsupp.linearCombination_apply, Finsupp.linearCombination_apply,
    Finsupp.sum_fintype _ _ fun i ‚Ü¶ by exact zero_smul K _,
    Finsupp.sum_fintype _ _ fun i ‚Ü¶ by exact zero_smul K _]
  simp only [Function.comp_apply, map_sum, map_smul]
  apply Fintype.sum_equiv (.mulRight x)
  simp [IsGalois.normalBasis_apply _‚Åª¬π, IsGalois.normalBasis_apply (_ * _)]
