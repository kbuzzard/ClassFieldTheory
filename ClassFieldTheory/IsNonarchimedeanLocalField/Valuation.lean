/-
Copyright (c) 2025 Kevin Buzzard. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kevin Buzzard
-/
import ClassFieldTheory.Cohomology.Functors.Restriction
import ClassFieldTheory.IsNonarchimedeanLocalField.Basic
import Mathlib.FieldTheory.Galois.IsGaloisGroup
/-

# 1 â†’ ğ’ª[K]Ë£ â†’ KË£ â†’ â„¤ â†’ 0

We construct the short exact sequence `0 â†’ Additive (ğ’ª[K]Ë£) â†’ Additive (KË£) â†’ â„¤ â†’ 0` in
the following sense: we define the maps `ker_v K` and `v K`, prove the first is
injective, the second is surjective, and the pair is `Function.Exact`.

-/

namespace ValuativeRel

section proofWanted

-- I only need it for IsNonarchimedeanLocalField but it should be true in this generality
lemma valuation_eq_one_iff (K : Type*) [Field K] [ValuativeRel K] (x : K) :
  (ValuativeRel.valuation K) x = 1 â†” âˆƒ r : ğ’ª[K]Ë£, x = r := sorry

end proofWanted

def ker_v (K : Type*) [CommRing K] [ValuativeRel K] : Additive (ğ’ª[K]Ë£) â†’+ Additive KË£ :=
  (Units.map (ğ’ª[K].subtype).toMonoidHom).toAdditive

variable {K : Type*} [CommRing K] [ValuativeRel K]

lemma ker_v_def (r : ğ’ª[K]Ë£) :
  ker_v K (.ofMul r) = .ofMul (Units.map (ğ’ª[K].subtype).toMonoidHom r) := rfl

lemma ker_v_injective : (ker_v K : Additive (ğ’ª[K]Ë£) â†’ Additive KË£).Injective := by
  intro a b h
  obtain âŸ¨a, rflâŸ© := Additive.ofMul.surjective a
  obtain âŸ¨b, rflâŸ© := Additive.ofMul.surjective b
  exact Units.map_injective ((Set.injective_codRestrict Subtype.property).mp fun â¦ƒaâ‚ aâ‚‚â¦„ a â†¦ a) h

end ValuativeRel

namespace IsNonarchimedeanLocalField

section valuation

open ValuativeRel

open scoped WithZero

/--
The â„¤áµâ°-valued valuation on a nonarchimedean local field. Note that it sends
a uniformiser to `.exp (-1)`.
-/
noncomputable def vâ‚€ {K : Type*} [Field K] [ValuativeRel K] [TopologicalSpace K]
    [IsNonarchimedeanLocalField K] : K â†’*â‚€ â„¤áµâ° :=
  (valueGroupWithZeroIsoInt K : ValueGroupWithZero K â†’*â‚€ â„¤áµâ°).comp
  (ValuativeRel.valuation K : K â†’*â‚€ ValueGroupWithZero K)

variable {K : Type*} [Field K] [ValuativeRel K] [TopologicalSpace K]
    [IsNonarchimedeanLocalField K]

lemma vâ‚€_uniformiser {Ï– : ğ’ª[K]} (hÏ– : Irreducible Ï–) :
    vâ‚€ (Ï– : K) = .exp (-1) :=
  valueGroupWithZeroIsoInt_irreducible hÏ–

/--
The valuation on the units of a nonarch local field. Domain is actually
`Additive KË£` so that it can be an additive group homomorphism to â„¤.
Normalised so that it sends a uniformiser to +1.
-/
noncomputable def v (K : Type*) [Field K] [ValuativeRel K] [TopologicalSpace K]
    [IsNonarchimedeanLocalField K] : Additive KË£ â†’+ â„¤ :=
  let fâ‚ƒ : KË£ â†’* (â„¤áµâ°)Ë£ := Units.map (vâ‚€.toMonoidHom : K â†’* â„¤áµâ°)
  -- here we introduce the sign
  let fâ‚„ : (â„¤áµâ°)Ë£ â†’* Multiplicative â„¤ := (WithZero.unitsWithZeroEquiv.toMonoidHom)â»Â¹
  (fâ‚„.comp fâ‚ƒ).toAdditiveLeft

lemma v_def (x : KË£) : Multiplicative.ofAdd (v K (.ofMul x)) = vâ‚€ (xâ»Â¹ : K) := by
  simp [v, vâ‚€]

lemma v_uniformiser {Ï– : ğ’ª[K]} (hÏ– : Irreducible Ï–) :
    v K (.ofMul <| Units.mk0 (Ï– : K) hÏ–.ne_zero') = 1 := by
  apply Multiplicative.ofAdd.injective
  apply WithZero.coe_injective
  simp [v_def, vâ‚€_uniformiser hÏ–, WithZero.exp]

lemma v_surjective : (v K : Additive KË£ â†’ â„¤).Surjective := by
  intro n
  obtain âŸ¨Ï–â‚€, hÏ–â‚€âŸ© := IsDiscreteValuationRing.exists_irreducible (ğ’ª[K])
  let Ï– : KË£ := Units.mk0 (Ï–â‚€ : K) hÏ–â‚€.ne_zero'
  use n â€¢ (.ofMul Ï–)
  simp [v_uniformiser hÏ–â‚€, Ï–]

lemma v_eq_zero_iff (x : KË£) : v K (.ofMul x) = 0 â†” valuation K x = 1 := by
  rw [â† Multiplicative.ofAdd.apply_eq_iff_eq, â† WithZero.coe_inj]
  simp [v_def, vâ‚€]

lemma ker_v_ker : Function.Exact (ker_v K) (v K) := by
  intro x
  obtain âŸ¨k, rflâŸ© := Additive.ofMul.surjective x
  rw [v_eq_zero_iff, valuation_eq_one_iff]
  simp only [Set.mem_range, Additive.exists, ker_v_def, Additive.ofMul.apply_eq_iff_eq]
  apply exists_congr (fun r â†¦ ?_)
  rw [â† Units.val_inj]
  simp [Eq.comm]

end valuation

end IsNonarchimedeanLocalField
