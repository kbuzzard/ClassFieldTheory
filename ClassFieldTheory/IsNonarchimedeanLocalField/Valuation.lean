/-
Copyright (c) 2025 Kevin Buzzard. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kevin Buzzard
-/
import ClassFieldTheory.Cohomology.Functors.Restriction
import ClassFieldTheory.IsNonarchimedeanLocalField.Basic
import ClassFieldTheory.Mathlib.Topology.Algebra.Valued.ValuativeRel
import Mathlib.FieldTheory.Galois.IsGaloisGroup
/-

# 1 â†’ ğ’ª[K]Ë£ â†’ KË£ â†’ â„¤ â†’ 0

We construct the short exact sequence `0 â†’ Additive ğ’ª[K]Ë£ â†’ Additive (KË£) â†’ â„¤ â†’ 0` in
the following sense: we define the maps `kerV K` and `v K`, prove the first is
injective, the second is surjective, and the pair is `Function.Exact`.

-/

namespace ValuativeRel

def kerV (K : Type*) [CommRing K] [ValuativeRel K] : Additive ğ’ª[K]Ë£ â†’+ Additive KË£ :=
  (Units.map ğ’ª[K].subtype).toAdditive

variable {K : Type*} [CommRing K] [ValuativeRel K]

@[simp] lemma kerV_apply (r : ğ’ª[K]Ë£) :
    kerV K (.ofMul r) = .ofMul (Units.map (ğ’ª[K].subtype).toMonoidHom r) := rfl

lemma kerV_injective : (â‡‘(kerV K)).Injective :=
  Units.map_injective Subtype.val_injective

end ValuativeRel

namespace IsNonarchimedeanLocalField

section valuation

open ValuativeRel

open scoped WithZero

/--
The `â„¤áµâ°`-valued valuation on a nonarchimedean local field. Note that it sends
a uniformiser to `.exp (-1)`.
-/
noncomputable def valuationInt (K : Type*) [Field K] [ValuativeRel K] [TopologicalSpace K]
    [IsNonarchimedeanLocalField K] : Valuation K â„¤áµâ° :=
  (valuation K).map (valueGroupWithZeroIsoInt K) <| OrderHomClass.mono <| valueGroupWithZeroIsoInt K

variable {K : Type*} [Field K] [ValuativeRel K] [TopologicalSpace K]
  [IsNonarchimedeanLocalField K]

-- move
theorem _root_.OrderIsoClass.strict_mono {Î± Î² F : Type*} [Preorder Î±] [Preorder Î²]
    [EquivLike F Î± Î²] [OrderIsoClass F Î± Î²] (f : F) : StrictMono f := fun x y â†¦
  by simp [lt_iff_le_not_ge]

instance : (valuationInt K).Compatible :=
  Valuation.compatible_map _ <| OrderIsoClass.strict_mono _

lemma valuationInt_uniformiser {Ï– : ğ’ª[K]} (hÏ– : Irreducible Ï–) :
    valuationInt K Ï– = .exp (-1) :=
  valueGroupWithZeroIsoInt_irreducible hÏ–

/--
The valuation on the units of a nonarch local field. Domain is actually
`Additive KË£` so that it can be an additive group homomorphism to â„¤.
Normalised so that it sends a uniformiser to +1.
-/
noncomputable def v (K : Type*) [Field K] [ValuativeRel K] [TopologicalSpace K]
    [IsNonarchimedeanLocalField K] : Additive KË£ â†’+ â„¤ :=
  let fâ‚ƒ : KË£ â†’* (â„¤áµâ°)Ë£ := Units.map (valuationInt K)
  -- here we introduce the sign
  let fâ‚„ : (â„¤áµâ°)Ë£ â†’* Multiplicative â„¤ := (WithZero.unitsWithZeroEquiv.toMonoidHom)â»Â¹
  (fâ‚„.comp fâ‚ƒ).toAdditiveLeft

@[simp] lemma v_apply (x : Additive KË£) : v K x = -(valuationInt K x.toMul).log := by
  obtain âŸ¨x, rflâŸ© := Additive.ofMul.surjective x
  rw [toMul_ofMul]
  generalize hy : valuationInt K x = y
  cases y using WithZero.expRecOn with
  | zero => simp at hy
  | exp y => simp [v, hy]

lemma v_uniformiser {Ï– : ğ’ª[K]} (hÏ– : Irreducible Ï–) :
    v K (.ofMul <| Units.mk0 (Ï– : K) hÏ–.ne_zero') = 1 := by
  simp [valuationInt_uniformiser hÏ–]

lemma v_surjective : (v K : Additive KË£ â†’ â„¤).Surjective := by
  intro n
  obtain âŸ¨Ï–â‚€, hÏ–â‚€âŸ© := IsDiscreteValuationRing.exists_irreducible ğ’ª[K]
  use n â€¢ .ofMul (.mk0 (Ï–â‚€ : K) hÏ–â‚€.ne_zero')
  simp [valuationInt_uniformiser hÏ–â‚€]

-- move
@[simp] lemma _root_.WithZero.log_eq_zero_iff {Î± : Type*} [AddMonoid Î±] {x : Î±áµâ°} :
    x.log = 0 â†” x = 0 âˆ¨ x = 1 := by
  cases x using WithZero.expRecOn with
  | zero => simp
  | exp x => simp

lemma v_eq_zero_iff (x : KË£) : v K (.ofMul x) = 0 â†” valuation K x = 1 := by
  simpa using (ValuativeRel.isEquiv _ _).eq_one_iff_eq_one

lemma exact_kerV_v : Function.Exact (kerV K) (v K) := by
  intro x
  obtain âŸ¨x, rflâŸ© := Additive.ofMul.surjective x
  rw [v_eq_zero_iff, valuation_eq_one_iff]
  simp [Units.ext_iff]

end valuation

end IsNonarchimedeanLocalField
