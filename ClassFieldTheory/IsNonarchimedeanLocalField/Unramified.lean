/-
Copyright (c) 2025 Kenny Lau. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kenny Lau
-/
import ClassFieldTheory.LocalCFT.Teichmuller
import ClassFieldTheory.IsNonarchimedeanLocalField.EF
import ClassFieldTheory.Mathlib.RingTheory.HenselPolynomial
import Mathlib.RingTheory.Polynomial.Cyclotomic.Basic
import Mathlib.RingTheory.RootsOfUnity.EnoughRootsOfUnity

/-! # Unramified extension of local field of a given degree

This file shows that if `K` is a non-archimedean local field and `n > 0` is any positive integer,
then there is a unique (up to in general non-unique isomorphism) unramified extension of `K` of
degree `n`.
-/

noncomputable section

namespace IsNonarchimedeanLocalField

open Polynomial ValuativeRel

/-- **The** unramified extension of degree `n > 0` of a given non-archimedean local field `K`. -/
def UnramifiedExtension (K : Type*) [Field K] [ValuativeRel K] [TopologicalSpace K]
    [IsNonarchimedeanLocalField K] (n : ‚Ñï) : Type _ :=
  SplittingField (X ^ (Nat.card ùìÄ[K] ^ n - 1) - 1 : K[X])
deriving Field, Algebra K, FiniteDimensional K

variable (K : Type*) [Field K] [ValuativeRel K] [TopologicalSpace K] [IsNonarchimedeanLocalField K]
variable (n : ‚Ñï)

instance : ValuativeRel (UnramifiedExtension K n) :=
  (isNonarchimedeanLocalField_of_finiteDimensional K _).choose

instance : ValuativeExtension K (UnramifiedExtension K n) :=
  (isNonarchimedeanLocalField_of_finiteDimensional K _).choose_spec.choose

instance : TopologicalSpace (UnramifiedExtension K n) :=
  (isNonarchimedeanLocalField_of_finiteDimensional K _).choose_spec.choose_spec.choose

instance : IsNonarchimedeanLocalField (UnramifiedExtension K n) :=
  (isNonarchimedeanLocalField_of_finiteDimensional K _).choose_spec.choose_spec.choose_spec

instance UnramifiedExtension.isSplittingField :
    IsSplittingField K (UnramifiedExtension K n) (X ^ (Nat.card ùìÄ[K] ^ n - 1) - 1) :=
  .splittingField _

theorem UnramifiedExtension.splits :
    (X ^ (Nat.card ùìÄ[K] ^ n - 1) - 1 : (UnramifiedExtension K n)[X]).Splits (.id _) := by
  have := (UnramifiedExtension.isSplittingField K n).splits
  simpa using (splits_id_iff_splits _).mpr this

theorem UnramifiedExtension.factors :
    (X ^ (Nat.card ùìÄ[K] ^ n - 1) - 1 : (UnramifiedExtension K n)[X]).Factors := by
  simpa [Splits] using splits K n

open UnramifiedExtension

section zero

theorem finrank_unramifiedExtension_zero : Module.finrank K (UnramifiedExtension K 0) = 1 := by
  have := UnramifiedExtension.isSplittingField K 0
  rw [pow_zero, Nat.sub_self, pow_zero, sub_self, isSplittingField_iff_intermediateField,
    rootSet_zero, IntermediateField.adjoin_empty,
    IntermediateField.bot_eq_top_iff_finrank_eq_one] at this
  exact this.2

instance : Subsingleton (Subalgebra K (UnramifiedExtension K 0)) :=
  subsingleton_of_bot_eq_top <| Subalgebra.bot_eq_top_of_finrank_eq_one <|
    finrank_unramifiedExtension_zero K

instance : Subsingleton (IntermediateField K (UnramifiedExtension K 0)) :=
  IntermediateField.toSubalgebra_injective.subsingleton

end zero

-- An auxiliary lemma that we might need more than once
theorem card_residue_pow_sub_one_in_residue_ne_zero {n : ‚Ñï} (hn : n ‚â† 0) :
    ((Nat.card ùìÄ[K] ^ n - 1 :) : ùìÄ[K]) ‚â† 0 := by
  have hp := Fact.mk <| prime_ringChar K
  have := ZMod.algebra
  rw [ne_eq, CharP.cast_eq_zero_iff, Module.natCard_eq_pow_finrank (K := ZMod (ringChar ùìÄ[K])),
    Nat.card_zmod, ‚Üê pow_mul, Nat.dvd_sub_iff_right, Nat.dvd_one]
  ¬∑ exact hp.out.ne_one
  ¬∑ exact one_le_pow_of_one_le' hp.out.one_le _
  ¬∑ exact dvd_pow_self _ <| mul_ne_zero Module.finrank_pos.ne' hn

-- An auxiliary lemma that we might need more than once
theorem card_residue_pow_sub_one_in_integers_ne_zero {n : ‚Ñï} (hn : n ‚â† 0) :
    ((Nat.card ùìÄ[K] ^ n - 1 :) : ùí™[K]) ‚â† 0 := by
  refine ne_zero_of_map (f := algebraMap ùí™[K] ùìÄ[K]) ?_
  rw [map_natCast]
  exact card_residue_pow_sub_one_in_residue_ne_zero K hn

-- An auxiliary lemma that we might need more than once
theorem card_residue_pow_sub_one_in_field_ne_zero {n : ‚Ñï} (hn : n ‚â† 0) :
    ((Nat.card ùìÄ[K] ^ n - 1 :) : K) ‚â† 0 := by
  rw [‚Üê map_natCast (algebraMap ùí™[K] K), ne_eq, FaithfulSMul.algebraMap_eq_zero_iff]
  exact card_residue_pow_sub_one_in_integers_ne_zero K hn

-- An auxiliary lemma that we might need more than once
theorem card_residue_pow_sub_one_in_unramifiedExtension_ne_zero {n : ‚Ñï} (hn : n ‚â† 0) :
    ((Nat.card ùìÄ[K] ^ n - 1 :) : UnramifiedExtension K n) ‚â† 0 := by
  rw [‚Üê map_natCast (algebraMap K _), ne_eq, FaithfulSMul.algebraMap_eq_zero_iff]
  exact card_residue_pow_sub_one_in_field_ne_zero K hn

-- An auxiliary lemma that we might need more than once
theorem card_residue_pow_sub_one_in_unramifiedExtension_residue_ne_zero {n : ‚Ñï} (hn : n ‚â† 0) :
    ((Nat.card ùìÄ[K] ^ n - 1 :) : ùìÄ[UnramifiedExtension K n]) ‚â† 0 := by
  rw [‚Üê map_natCast (algebraMap ùìÄ[K] _), ne_eq, FaithfulSMul.algebraMap_eq_zero_iff]
  exact card_residue_pow_sub_one_in_residue_ne_zero K hn

-- An auxiliary lemma that we might need more than once
theorem card_residue_pow_sub_one_in_ne_zero {n : ‚Ñï} (hn : n ‚â† 0) :
    Nat.card ùìÄ[K] ^ n - 1 ‚â† 0 :=
  ne_zero_of_map (f := algebraMap ‚Ñï K) <| card_residue_pow_sub_one_in_field_ne_zero K hn

instance : IsGalois K (UnramifiedExtension K n) := by
  obtain _ | n := n
  ¬∑ have := finrank_unramifiedExtension_zero K
    rw [‚Üê IntermediateField.bot_eq_top_iff_finrank_eq_one] at this
    rw [‚Üê isGalois_iff_isGalois_top, ‚Üê this]
    exact isGalois_bot
  refine .of_separable_splitting_field (p := X ^ (Nat.card ùìÄ[K] ^ (n + 1) - 1) - 1) ?_
  rw [X_pow_sub_one_separable_iff]
  exact card_residue_pow_sub_one_in_field_ne_zero K n.succ_ne_zero

-- move
theorem _root_.Polynomial.nodup_nthRoots_of_natCast_ne_zero {R : Type*} [CommRing R] [IsDomain R]
    {n : ‚Ñï} {a : R} (hn : (n : R) ‚â† 0) (ha : a ‚â† 0) : (nthRoots n a).Nodup := by
  have : (‚áë(algebraMap R (FractionRing R))).Injective := FaithfulSMul.algebraMap_injective ..
  rw [nthRoots, ‚Üê Multiset.nodup_map_iff_of_injective this]
  refine Multiset.nodup_of_le (map_roots_le_of_injective _ this) ?_
  simp only [Polynomial.map_sub, Polynomial.map_pow, map_X, map_C]
  refine nodup_roots <| separable_X_pow_sub_C _ ?_ (map_ne_zero_iff _ this |>.mpr ha)
  ¬∑ rw [‚Üê map_natCast (algebraMap R _)]
    exact map_ne_zero_iff _ this |>.mpr hn

-- move
theorem _root_.Polynomial.nodup_nthRoots_one_of_natCast_ne_zero {R : Type*} [CommRing R] [IsDomain R]
    {n : ‚Ñï} (hn : (n : R) ‚â† 0) : (nthRoots n (1 : R)).Nodup :=
  nodup_nthRoots_of_natCast_ne_zero hn one_ne_zero

-- move?
theorem _root_.image_rootsOfUnity_eq_nthRoots {R : Type*} [CommRing R] [IsDomain R] {n : ‚Ñï}
    (hn : n ‚â† 0) : Units.val '' (rootsOfUnity n R : Set RÀ£) = (nthRootsFinset n 1 : Finset R) := by
  ext x
  simp only [Set.mem_image, SetLike.mem_coe, mem_rootsOfUnity, Units.ext_iff,
    Units.val_pow_eq_pow_val, Units.val_one, mem_nthRootsFinset (pos_of_ne_zero hn)]
  exact ‚ü®by grind, fun hxn ‚Ü¶ ‚ü®.ofPowEqOne _ _ hxn hn, hxn, rfl‚ü©‚ü©

-- move?
/-- If a domain `R` satisfies that `X ^ n - 1` splits in `R` and `n ‚â† 0` then `R` has enough
`n`-th roots of unity. -/
theorem _root_.HasEnoughRootsOfUnity.of_splits {R : Type*} [CommRing R] [IsDomain R] {n : ‚Ñï}
    (hr : (X ^ n - 1 : R[X]).Factors) (hn : (n : R) ‚â† 0) : HasEnoughRootsOfUnity R n := by
  have := NeZero.mk <| show n ‚â† 0 by aesop
  refine .of_card_le ?_
  classical
  rw [Fintype.card_eq_nat_card, ‚Üê SetLike.coe_sort_coe,
    ‚Üê Nat.card_image_of_injective Units.val_injective, image_rootsOfUnity_eq_nthRoots this.out,
    SetLike.coe_sort_coe, Nat.card_eq_finsetCard, nthRootsFinset,
    Multiset.toFinset_card_of_nodup (nodup_nthRoots_one_of_natCast_ne_zero hn), nthRoots, C_1,
    ‚Üê hr.natDegree_eq_card_roots, ‚Üê C_1, natDegree_X_pow_sub_C]

variable {n} in
instance [NeZero n] :
    HasEnoughRootsOfUnity (UnramifiedExtension K n) (Nat.card ùìÄ[K] ^ n - 1) :=
  .of_splits (factors K n) <| card_residue_pow_sub_one_in_unramifiedExtension_ne_zero K NeZero.out

example [NeZero n] : ‚àÉ Œ∂ : UnramifiedExtension K n, IsPrimitiveRoot Œ∂ (Nat.card ùìÄ[K] ^ n - 1) :=
  HasEnoughRootsOfUnity.exists_primitiveRoot _ _

theorem UnramifiedExtension.top_eq_adjoin_roots :
    (‚ä§ : Subalgebra K (UnramifiedExtension K n)) = Algebra.adjoin K
      (nthRootsFinset (Nat.card ùìÄ[K] ^ n - 1) 1 : Finset (UnramifiedExtension K n)) := by
  rw [‚Üê (isSplittingField K n).adjoin_rootSet, rootSet, aroots, nthRootsFinset, nthRoots]
  simp

-- move
/-- If `M` has enough `n`-th roots of unity and we are given a primitive root `Œ∂`, then any `n`-th
root of unity is a power of `Œ∂`. -/
theorem _root_.HasEnoughRootsOfUnity.exists_pow {M : Type*} [CommMonoid M] {n : ‚Ñï} (hn : n ‚â† 0)
    [HasEnoughRootsOfUnity M n] {Œ∂ : M} (hŒ∂ : IsPrimitiveRoot Œ∂ n) {œâ : M} (hœâ : œâ ^ n = 1) :
    ‚àÉ i < n, Œ∂ ^ i = œâ := by
  have := NeZero.mk hn
  let Œ∂' : rootsOfUnity n M := ‚ü®.ofPowEqOne _ _ hŒ∂.1 hn, Units.ext hŒ∂.1‚ü©
  have hoŒ∂' : orderOf Œ∂' = n := by
    rw [ ‚Üê orderOf_injective (Subgroup.subtype _) Subtype.val_injective Œ∂',
      ‚Üê orderOf_injective (Units.coeHom _) Units.val_injective]
    exact hŒ∂.eq_orderOf.symm
  have hŒ∂' : Subgroup.zpowers Œ∂' = ‚ä§ := by
    refine Subgroup.eq_top_of_le_card _ ?_
    rw [HasEnoughRootsOfUnity.natCard_rootsOfUnity, Nat.card_zpowers, hoŒ∂']
  classical
  simp_rw [Subgroup.eq_top_iff', mem_zpowers_iff_mem_range_orderOf, Finset.mem_image,
    Finset.mem_range, hoŒ∂'] at hŒ∂'
  obtain ‚ü®i, hin, hi‚ü© := hŒ∂' ‚ü®.ofPowEqOne _ _ hœâ hn, Units.ext hœâ‚ü©
  exact ‚ü®i, hin, congr(($hi).val.val)‚ü©

-- ????
theorem _root_.Multiset.toFinset_range {n : ‚Ñï} : (Multiset.range n).toFinset = .range n :=
  Finset.val_injective (Finset.range n).nodup.dedup

-- move
theorem _root_.IsPrimitiveRoot.nthRoots_one_eq {R : Type*}
    [CommRing R] [IsDomain R] {n : ‚Ñï}
    {Œ∂ : R} (hŒ∂ : IsPrimitiveRoot Œ∂ n) :
    nthRoots n (1 : R) = (Multiset.range n).map (Œ∂ ^ ¬∑) := by
  simp_rw [hŒ∂.nthRoots_eq (one_pow n), mul_one]

-- move
theorem _root_.IsPrimitiveRoot.nthRootsFinset_one_eq {R : Type*}
    [CommRing R] [IsDomain R] [DecidableEq R] {n : ‚Ñï}
    {Œ∂ : R} (hŒ∂ : IsPrimitiveRoot Œ∂ n) :
    nthRootsFinset n (1 : R) = (Finset.range n).image (Œ∂ ^ ¬∑) := by
  simp_rw [nthRootsFinset, hŒ∂.nthRoots_one_eq, @Multiset.toFinset_map, Multiset.toFinset_range]
  congr
  subsingleton

-- move
/-- Over domain `R`, `Œ∂ : R` is a primitive `n`-th root iff the multiset `{ Œ∂ ^ i | 0 ‚â§ i < n }`
is equal to the multiset `nthRoots n 1`, and the multiset has no duplicates. -/
theorem _root_.isPrimitiveRoot_iff_nthRoots_and_nodup {R : Type*} [CommRing R] [IsDomain R]
    {n : ‚Ñï} (hn : 1 < n) {Œ∂ : R} :
    IsPrimitiveRoot Œ∂ n ‚Üî
    (Multiset.range n).map (Œ∂ ^ ¬∑) = nthRoots n 1 ‚àß (nthRoots n (1 : R)).Nodup := by
  classical
  refine ‚ü®fun hŒ∂ ‚Ü¶ ‚ü®hŒ∂.nthRoots_one_eq.symm, hŒ∂.nthRoots_one_nodup‚ü©,
    fun ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© ‚Ü¶ IsPrimitiveRoot.iff (by grind) |>.mpr ‚ü®?_, fun i h0i hin ‚Ü¶ ?_‚ü©‚ü©
  ¬∑ rw [‚Üê mem_nthRoots (by grind), ‚Üê h‚ÇÅ, Multiset.mem_map]
    simp_rw [Multiset.mem_range]
    exact ‚ü®1, hn, pow_one Œ∂‚ü©
  ¬∑ rw [‚Üê h‚ÇÅ] at h‚ÇÇ
    replace h‚ÇÇ := Multiset.inj_on_of_nodup_map h‚ÇÇ
    simp_rw [Multiset.mem_range] at h‚ÇÇ
    simpa [h0i.ne'] using h‚ÇÇ i hin 0 (h0i.trans hin)

theorem UnramifiedExtension.top_eq_adjoin_primitive_root
    {Œ∂ : UnramifiedExtension K n} (hŒ∂ : IsPrimitiveRoot Œ∂ (Nat.card ùìÄ[K] ^ n - 1)) :
    (‚ä§ : Subalgebra K (UnramifiedExtension K n)) = Algebra.adjoin K {Œ∂} := by
  obtain _ | n := n
  ¬∑ subsingleton
  have := card_residue_pow_sub_one_in_ne_zero K n.succ_ne_zero
  rw [top_eq_adjoin_roots]
  refine le_antisymm (Algebra.adjoin_le fun œâ hœâ ‚Ü¶ ?_) <|
    Algebra.adjoin_le <| Set.singleton_subset_iff.mpr <| Algebra.subset_adjoin ?_
  ¬∑ rw [SetLike.mem_coe, mem_nthRootsFinset (pos_of_ne_zero this)] at hœâ
    obtain ‚ü®i, _, rfl‚ü© := HasEnoughRootsOfUnity.exists_pow this hŒ∂ hœâ
    exact pow_mem (Algebra.subset_adjoin <| Set.mem_singleton _) _
  ¬∑ rw [SetLike.mem_coe, mem_nthRootsFinset (pos_of_ne_zero this), hŒ∂.1]

theorem UnramifiedExtension.intermediateFieldTop_eq_adjoin_primitive_root
    {Œ∂ : UnramifiedExtension K n} (hŒ∂ : IsPrimitiveRoot Œ∂ (Nat.card ùìÄ[K] ^ n - 1)) :
    (‚ä§ : IntermediateField K (UnramifiedExtension K n)) = .adjoin K {Œ∂} :=
  IntermediateField.eq_adjoin_of_eq_algebra_adjoin _ _ _ <| by
    simp [top_eq_adjoin_primitive_root _ _ hŒ∂]

-- move
section finite_field

/-- For each `n`, `{x : L | x ^ q ^ n = x}` is an intermediate field (where `q = Nat.card K`). -/
def _root_.FiniteField.intermediateField
    (K L : Type*) [Field K] [Field L] [Finite L] [Algebra K L] (n : ‚Ñï) :
    IntermediateField K L where
  carrier := {x | x ^ Nat.card K ^ n = x}
  mul_mem' hx hy := by simp_all [mul_pow]
  add_mem' hx hy := by
    obtain ‚ü®p, _‚ü© := CharP.exists K
    have := charP_of_injective_algebraMap' K (A := L) p
    have := Finite.of_injective _ <| FaithfulSMul.algebraMap_injective K L
    have := Fintype.ofFinite K
    have := ZMod.algebra K p
    have := Fact.mk (CharP.char_is_prime K p)
    simp_all [Module.card_eq_pow_finrank (K := ZMod p) (V := K), ‚Üê pow_mul, add_pow_char_pow]
  algebraMap_mem' := by
    have := Finite.of_injective _ <| FaithfulSMul.algebraMap_injective K L
    have := Fintype.ofFinite K
    simp [‚Üê map_pow, FiniteField.pow_card_pow]
  inv_mem' hx := by simp_all [inv_pow]

open FiniteField IntermediateField

variable {K L : Type*} [Field K] [Field L] [Finite L] [Algebra K L] {n : ‚Ñï}

theorem _root_.FiniteField.mem_intermediateField_iff {x : L} :
    x ‚àà intermediateField K L n ‚Üî x ^ Nat.card K ^ n = x := Iff.rfl

theorem _root_.FiniteField.intermediateField_eq_rootSet (hn : n ‚â† 0) :
    (intermediateField K L n : Set L) = (X ^ Nat.card K ^ n - X : L[X]).rootSet L := by
  have := Finite.of_injective _ <| FaithfulSMul.algebraMap_injective K L
  ext x
  rw [mem_rootSet_of_ne (FiniteField.X_pow_card_pow_sub_X_ne_zero _ hn Finite.one_lt_card)]
  simp [mem_intermediateField_iff, sub_eq_zero]

theorem _root_.FiniteField.mem_intermediateField_iff' {x : L} :
    x ‚àà intermediateField K L n ‚Üî x = 0 ‚à® x ^ (Nat.card K ^ n - 1) = 1 := by
  have := Finite.of_injective _ <| FaithfulSMul.algebraMap_injective K L
  have : 2 ‚â§ Nat.card K := Finite.one_lt_card
  have h : 1 ‚â§ Nat.card K ^ n := Nat.pow_pos (by grind)
  conv_lhs => rw [mem_intermediateField_iff, ‚Üê Nat.sub_add_cancel h]
  rw [pow_succ, mul_left_eq_self‚ÇÄ, or_comm]

theorem _root_.FiniteField.intermediateField_eq_insert_zero_nthRootsFinset_one (hn : n ‚â† 0) :
    (intermediateField K L n : Set L) =
    insert 0 (nthRootsFinset (Nat.card K ^ n - 1) (1 : L) : Set L) := by
  have := Finite.of_injective _ <| FaithfulSMul.algebraMap_injective K L
  have : 2 ‚â§ Nat.card K := Finite.one_lt_card
  have h2n : 2 ‚â§ Nat.card K ^ n := one_lt_pow' (M := ‚Ñï) this hn
  ext x
  rw [SetLike.mem_coe, mem_intermediateField_iff', Set.mem_insert_iff, SetLike.mem_coe,
    mem_nthRootsFinset (by grind)]

theorem _root_.FiniteField.adjoin_eq_intermediateField_of_isPrimitiveRoot
    (hn : n ‚â† 0) {Œ∂ : L} (hŒ∂ : IsPrimitiveRoot Œ∂ (Nat.card K ^ n - 1)) :
    adjoin K {Œ∂} = intermediateField K L n := by
  have := Finite.of_injective _ <| FaithfulSMul.algebraMap_injective K L
  have : HasEnoughRootsOfUnity L (Nat.card K ^ n - 1) := ‚ü®‚ü®_, hŒ∂‚ü©, inferInstance‚ü©
  have h2n : 2 ‚â§ Nat.card K ^ n := (Nat.le_pow (pos_of_ne_zero hn)).trans <|
    (Nat.pow_le_pow_iff_left hn).mpr Finite.one_lt_card
  have h1n : 1 ‚â§ Nat.card K ^ n := by grind
  refine le_antisymm (adjoin_le_iff.mpr <| Set.singleton_subset_iff.mpr ?_) fun x hx ‚Ü¶ ?_
  ¬∑ rw [SetLike.mem_coe, mem_intermediateField_iff', hŒ∂.1]
    exact .inr rfl
  ¬∑ rw [mem_intermediateField_iff'] at hx
    obtain rfl | hx := hx
    ¬∑ simp
    ¬∑ obtain ‚ü®i, _, rfl‚ü© := HasEnoughRootsOfUnity.exists_pow (by grind) hŒ∂ hx
      exact pow_mem (mem_adjoin_simple_self _ _) _

theorem _root_.X_pow_sub_X_factors_of_isPrimitiveRoot {R : Type*} [Field R]
    {n : ‚Ñï} (hn : n ‚â† 0) {Œ∂ : R} (hŒ∂ : IsPrimitiveRoot Œ∂ (n - 1)) :
    (X ^ n - X : R[X]).Factors := by
  rw [‚Üê Nat.sub_add_cancel (pos_of_ne_zero hn), pow_succ, ‚Üê sub_one_mul]
  exact .mul (by simpa [Splits] using X_pow_sub_one_splits hŒ∂) .X

/-- The minimal polynomial of a primitive `(q^n-1)`-st root of unity has degree `n`. -/
theorem _root_.FiniteField.degree_minpoly_of_isPrimitiveRoot
    (hn : n ‚â† 0) {Œ∂ : L} (hŒ∂ : IsPrimitiveRoot Œ∂ (Nat.card K ^ n - 1)) :
    (minpoly K Œ∂).natDegree = n := by
  have := Finite.of_injective _ <| FaithfulSMul.algebraMap_injective K L
  have := Fintype.ofFinite K
  have := Fintype.ofFinite (intermediateField K L n)
  have key : adjoin K {Œ∂} = FiniteField.intermediateField K L n :=
    adjoin_eq_intermediateField_of_isPrimitiveRoot hn hŒ∂
  obtain ‚ü®p, _‚ü© := CharP.exists K
  have := Fact.mk <| CharP.char_is_prime K p
  have := ZMod.algebra K p
  have := charP_of_injective_algebraMap' K (A := L) p
  have : 1 < Nat.card K := Finite.one_lt_card
  have : 1 < Nat.card K ^ n := one_lt_pow' this hn
  have : p ‚à£ Nat.card K ^ n := dvd_pow (Nat.card_zmod p ‚ñ∏ AddSubgroup.card_dvd_of_injective
    (algebraMap (ZMod p) K) (FaithfulSMul.algebraMap_injective _ _)) hn
  classical
  rw [‚Üê IntermediateField.adjoin.finrank (.of_finite _ _), key,
    ‚Üê Nat.pow_right_inj (Finite.one_lt_card (Œ± := K)),
    Nat.card_eq_fintype_card, ‚Üê Module.card_eq_pow_finrank, Fintype.card_eq_nat_card,
    ‚Üê SetLike.coe_sort_coe, intermediateField_eq_rootSet hn, rootSet, aroots,
    Algebra.algebraMap_self, Polynomial.map_id, SetLike.coe_sort_coe, Nat.card_eq_finsetCard,
    Multiset.toFinset_card_of_nodup (nodup_roots <| galois_poly_separable _ _ this),
    ‚Üê (X_pow_sub_X_factors_of_isPrimitiveRoot (by grind) hŒ∂).natDegree_eq_card_roots,
    FiniteField.X_pow_card_sub_X_natDegree_eq _ (by grind), Fintype.card_eq_nat_card]

end finite_field

/-- If `f : R ‚Üí+* S` and `Œ∂ : R` is a primitive `n`-th root and `(n : S) ‚â† 0` then `f Œ∂` is
a primitive `n`-th root in `S`. -/
theorem _root_.IsPrimitiveRoot.map_of_ne_zero {R S : Type*}
    [CommRing R] [IsDomain R] [CommRing S] [IsDomain S]
    {Œ∂ : R} {n : ‚Ñï} (hŒ∂ : IsPrimitiveRoot Œ∂ n) (f : R ‚Üí+* S) (hn : (n : S) ‚â† 0) :
    IsPrimitiveRoot (f Œ∂) n := by
  by_cases hn1 : n = 1
  ¬∑ rw [hn1, IsPrimitiveRoot.one_right_iff] at hŒ∂
    simp [hŒ∂, hn1]
  have : n ‚â† 0 := by aesop
  replace hn1 : 1 < n := by grind
  have hŒ∂' := hŒ∂
  rw [isPrimitiveRoot_iff_nthRoots_and_nodup hn1] at hŒ∂' ‚ä¢
  constructor
  ¬∑ simp_rw [‚Üê map_pow]
    change Multiset.map (f ‚àò (Œ∂ ^ ¬∑)) _ = _
    rw [‚Üê Multiset.map_map, hŒ∂'.1, nthRoots,
      (monic_X_pow_sub_C _ this).roots_map_of_card_eq_natDegree, Polynomial.map_sub,
      Polynomial.map_pow, map_X, map_C, map_one, nthRoots]
    ¬∑ rw [‚Üê nthRoots, hŒ∂.card_nthRoots_one, natDegree_X_pow_sub_C]
  ¬∑ exact nodup_nthRoots_one_of_natCast_ne_zero hn

-- ask andrew
instance : IsAdicComplete ùìÇ[K] ùí™[K] := sorry

variable {n} in
private theorem finrank_unramifiedExtension_and_residue (hn : n ‚â† 0) :
    Module.finrank K (UnramifiedExtension K n) = n ‚àß
    n ‚â§ Module.finrank ùìÄ[K] ùìÄ[UnramifiedExtension K n] := by
  have := NeZero.mk hn
  obtain ‚ü®Œ∂, hŒ∂‚ü© := HasEnoughRootsOfUnity.exists_primitiveRoot
    (UnramifiedExtension K n) (Nat.card ùìÄ[K] ^ n - 1)
  have : 1 < Nat.card ùìÄ[K] ^ n := Nat.one_lt_pow hn Finite.one_lt_card
  have h‚ÇÅ : IsIntegral ùí™[K] Œ∂ := .of_pow (n := Nat.card ùìÄ[K] ^ n - 1) (by grind)
    (by rw [hŒ∂.1]; exact isIntegral_one)
  lift Œ∂ to ùí™[UnramifiedExtension K n] using isIntegral_iff.mp h‚ÇÅ
  have h‚ÇÇ : IsIntegral ùí™[K] Œ∂ := h‚ÇÅ.tower_bot Subtype.val_injective
  have h‚ÇÉ : minpoly ùí™[K] Œ∂.val = minpoly ùí™[K] Œ∂ :=
    minpoly.algHom_eq (Algebra.algHom _ _ _) Subtype.val_injective Œ∂
  have h‚ÇÑ : minpoly ùí™[K] Œ∂ ‚à£ X ^ Nat.card ùìÄ[K] ^ n - X := by
    rw [‚Üê map_dvd_map (f := algebraMap ùí™[K] K) (FaithfulSMul.algebraMap_injective _ _)
      (minpoly.monic h‚ÇÇ), ‚Üê h‚ÇÉ, ‚Üê minpoly.isIntegrallyClosed_eq_field_fractions' K h‚ÇÅ]
    refine minpoly.dvd _ _ ?_
    rw [‚Üê Nat.sub_add_cancel (show 1 ‚â§ Nat.card ùìÄ[K] ^ n by grind)]
    simp [pow_succ, hŒ∂.1]
  let ‚ü®p, hp‚ü© := CharP.exists ùìÄ[K]
  have := Fact.mk <| CharP.char_is_prime ùìÄ[K] p
  have := ZMod.algebra ùìÄ[K] p
  have h‚ÇÖ : p ‚à£ Nat.card ùìÄ[K] ^ n := dvd_pow (Nat.card_zmod p ‚ñ∏ AddSubgroup.card_dvd_of_injective
    (algebraMap (ZMod p) ùìÄ[K]) (FaithfulSMul.algebraMap_injective _ _)) hn
  have h‚ÇÜ : (map (IsLocalRing.residue ùí™[K]) (minpoly ùí™[K] Œ∂)).Separable :=
    Polynomial.Separable.of_dvd (by simpa using galois_poly_separable p _ h‚ÇÖ) <|
      map_dvd (IsLocalRing.residue _) h‚ÇÑ
  have key : map (IsLocalRing.residue ùí™[K]) (minpoly ùí™[K] Œ∂) =
      minpoly ùìÄ[K] (IsLocalRing.residue _ Œ∂) :=
    minpoly.eq_of_irreducible_of_monic
      (irreducible_map_of_irreducible_of_separable_map (minpoly.monic h‚ÇÇ) (minpoly.irreducible h‚ÇÇ)
        h‚ÇÜ)
      (by rw [‚Üê IsLocalRing.ResidueField.algebraMap_eq ùí™[K], aeval_map_algebraMap,
        show ‚áë(IsLocalRing.residue ùí™[UnramifiedExtension K n]) = Algebra.algHom ùí™[K] _ _ from rfl,
        aeval_algHom_apply, minpoly.aeval, map_zero])
      ((minpoly.monic h‚ÇÇ).map _)
  have h‚Çá : IsIntegral ùìÄ[K] (IsLocalRing.residue _ Œ∂) :=
    (h‚ÇÇ.map <| Algebra.algHom ùí™[K] _ _).tower_top
  have hŒ∂‚ÇÅ := hŒ∂.of_map_of_injective (Subring.subtype_injective _)
  have hŒ∂‚ÇÇ := hŒ∂‚ÇÅ.map_of_ne_zero (IsLocalRing.residue _) <|
    card_residue_pow_sub_one_in_unramifiedExtension_residue_ne_zero K hn
  have h‚Çà : (minpoly ùìÄ[K] ((IsLocalRing.residue ùí™[UnramifiedExtension K n]) Œ∂)).natDegree = n :=
    FiniteField.degree_minpoly_of_isPrimitiveRoot hn hŒ∂‚ÇÇ
  constructor
  ¬∑ rw [‚Üê IntermediateField.finrank_top', intermediateFieldTop_eq_adjoin_primitive_root _ _ hŒ∂,
      IntermediateField.adjoin.finrank (h‚ÇÅ.tower_top),
      minpoly.isIntegrallyClosed_eq_field_fractions' K h‚ÇÅ,
      natDegree_map_eq_of_injective (FaithfulSMul.algebraMap_injective _ _),
      ‚Üê Monic.natDegree_map (minpoly.monic h‚ÇÅ) (IsLocalRing.residue ùí™[K]), h‚ÇÉ, key, h‚Çà]
  ¬∑ conv_lhs => rw [‚Üê h‚Çà]
    exact minpoly.natDegree_le _

variable {n} in
@[simp] theorem finrank_unramifiedExtension (hn : n ‚â† 0) :
    Module.finrank K (UnramifiedExtension K n) = n :=
  (finrank_unramifiedExtension_and_residue K hn).1

variable {n} in
@[simp] theorem f_unramifiedExtension (hn : n ‚â† 0) :
    f K (UnramifiedExtension K n) = n := by
  refine le_antisymm ?_ (by simpa [hn, f] using (finrank_unramifiedExtension_and_residue K hn).2)
  conv_rhs => rw [‚Üê finrank_unramifiedExtension K hn]
  exact f_le_n _ _

instance : IsUnramified K (UnramifiedExtension K n) := .mk <| by
  obtain rfl | hn := eq_or_ne n 0
  ¬∑ exact e_eq_one_of_n_eq_one _ _ <| finrank_unramifiedExtension_zero K
  rw [‚Üê Nat.mul_left_inj (ne_of_gt <| f_pos K (UnramifiedExtension K n)),
    one_mul, e_mul_f_eq_n]
  simp [hn]

section more_stuff_on_finite_fields
variable (F : Type*) [Field F] [Finite F]

open FiniteField

theorem _root_.FiniteField.rootsOfUnity_eq_top : rootsOfUnity (Nat.card F - 1) F = ‚ä§ :=
  have := Fintype.ofFinite F
  eq_top_iff.mpr fun x _ ‚Ü¶ Units.ext <| by simp [FiniteField.pow_card_sub_one_eq_one _ x.ne_zero]

instance : HasEnoughRootsOfUnity F (Nat.card F - 1) := by
  have := Finite.one_lt_card (Œ± := F)
  have : NeZero (Nat.card F - 1) := .mk <| by grind
  exact .of_card_le <| by simp [Fintype.card_eq_nat_card, rootsOfUnity_eq_top, Nat.card_units]

end more_stuff_on_finite_fields

instance : HasEnoughRootsOfUnity K (Nat.card ùìÄ[K] - 1) := by
  obtain ‚ü®Œ∂, hŒ∂‚ü© := HasEnoughRootsOfUnity.exists_primitiveRoot ùìÄ[K] (Nat.card ùìÄ[K] - 1)
  have := Finite.one_lt_card (Œ± := ùìÄ[K])
  have : NeZero (Nat.card ùìÄ[K] - 1) := .mk <| by grind
  exact ‚ü®‚ü®_, hŒ∂.map_of_injective teichmuller_injective‚ü©, inferInstance‚ü©

variable (L : Type*) [Field L] [ValuativeRel L] [TopologicalSpace L] [IsNonarchimedeanLocalField L]
  [Algebra K L] [ValuativeExtension K L]

/-- If `Kn` denotes the unramified extension of `K` of degree `n`, then `Kn` embeds into `L` if
`n ‚à£ f K L`. This is half of the universal property. -/
theorem nonempty_unramifiedExtension_alghom_of_dvd_f (n : ‚Ñï) (hn : n ‚à£ f K L) :
    Nonempty (UnramifiedExtension K n ‚Üí‚Çê[K] L) := by
  have hf0 := NeZero.mk (f_pos K L).ne'
  have hn0 := NeZero.mk <| ne_zero_of_dvd_ne_zero hf0.out hn
  have h‚ÇÅ := Nat.pow_sub_one_dvd_pow_sub_one (Nat.card ùìÄ[K]) hn
  obtain ‚ü®Œ∂, hŒ∂‚ü© := HasEnoughRootsOfUnity.exists_primitiveRoot
    (UnramifiedExtension K n) (Nat.card ùìÄ[K] ^ n - 1)
  have h‚ÇÇ := pos_of_ne_zero <| card_residue_pow_sub_one_in_ne_zero K hn0.out
  have h‚ÇÉ := pos_of_ne_zero <| card_residue_pow_sub_one_in_ne_zero K hf0.out
  refine IntermediateField.nonempty_algHom_of_adjoin_splits
    (forall_eq.mpr ‚ü®.of_pow h‚ÇÇ <| hŒ∂.1 ‚ñ∏ isIntegral_one,
      .of_dvd (g := X ^ (Nat.card ùìÄ[K] ^ n - 1) - C 1) ?_
        (X_pow_sub_C_ne_zero h‚ÇÇ _) ?_‚ü©)
    (intermediateFieldTop_eq_adjoin_primitive_root K _ hŒ∂).symm
  ¬∑ rw [f_spec'] at h‚ÇÅ h‚ÇÉ
    have := NeZero.mk h‚ÇÉ.ne'
    have := HasEnoughRootsOfUnity.of_dvd L h‚ÇÅ
    obtain ‚ü®Œ∂', hŒ∂'‚ü© := HasEnoughRootsOfUnity.exists_primitiveRoot L (Nat.card ùìÄ[K] ^ n - 1)
    simpa [Splits] using X_pow_sub_one_splits hŒ∂'
  ¬∑ conv_rhs => exact show _ = map (algebraMap K L) (X ^ (Nat.card ùìÄ[K] ^ n - 1) - C 1) by simp
    rw [map_dvd_map', minpoly.dvd_iff]
    simp [hŒ∂.1]

/-- The universal property of unramified extensions. -/
theorem nonempty_unramifiedExtension_alghom_iff_dvd_f {n : ‚Ñï} (hn : n ‚â† 0) :
    Nonempty (UnramifiedExtension K n ‚Üí‚Çê[K] L) ‚Üî n ‚à£ f K L :=
  ‚ü®fun ‚ü®œÜ‚ü© ‚Ü¶ f_unramifiedExtension K hn ‚ñ∏ f_dvd_f œÜ,
  nonempty_unramifiedExtension_alghom_of_dvd_f _ _ _‚ü©

/-- If `L/K` is unramified, then `L` is isomorphic to `Kn` where `n = [L:K]`. -/
theorem nonempty_unramifiedExtension_algEquiv_of_isUnramified [IsUnramified K L] :
    Nonempty (UnramifiedExtension K (Module.finrank K L) ‚âÉ‚Çê[K] L) := by
  obtain ‚ü®œÜ‚ü© := nonempty_unramifiedExtension_alghom_of_dvd_f K L (Module.finrank K L)
    (IsUnramified.n_dvd_f K L)
  have : œÜ.fieldRange = ‚ä§ := IntermediateField.toSubalgebra_injective <|
    Subalgebra.toSubmodule_injective <| Submodule.eq_top_of_finrank_eq <| by
    change Module.finrank K (LinearMap.range œÜ.toLinearMap) = _
    rw [LinearMap.finrank_range_of_inj œÜ.toRingHom.injective,
      finrank_unramifiedExtension _ Module.finrank_pos.ne']
  exact ‚ü®(AlgEquiv.ofInjective œÜ œÜ.toRingHom.injective).trans <|
    (IntermediateField.equivOfEq this).trans <| IntermediateField.topEquiv‚ü©

/-- Any unramified extension is Galois. -/
instance [IsUnramified K L] : IsGalois K L :=
  let ‚ü®œÜ‚ü© := nonempty_unramifiedExtension_algEquiv_of_isUnramified K L
  .of_algEquiv œÜ

/-- The maximal unramified subextension. -/
def maximalUnramified : IntermediateField K L :=
  (nonempty_unramifiedExtension_alghom_of_dvd_f K L (f K L) dvd_rfl).some.fieldRange

instance : IsUnramified K (maximalUnramified K L) := by
  unfold maximalUnramified
  infer_instance

variable {K L} (E : IntermediateField K L)

/-- The maximal unramified subextension is maximal. -/
theorem le_maximalUnramified_iff : E ‚â§ maximalUnramified K L ‚Üî IsUnramified K E := by
  refine ‚ü®fun h ‚Ü¶ .comap <| show E ‚Üí‚Çê[K] maximalUnramified K L from Subalgebra.inclusion h,
    fun _ ‚Ü¶ ?_‚ü©
  obtain ‚ü®œÜ‚ÇÅ‚ü© := nonempty_unramifiedExtension_algEquiv_of_isUnramified K E
  have h‚ÇÅ : Module.finrank K E ‚à£ f K L := .trans (IsUnramified.n_dvd_f K E) <| f_dvd_f_top ..
  obtain ‚ü®œÜ‚ÇÇ‚ü© := nonempty_unramifiedExtension_alghom_of_dvd_f K (UnramifiedExtension K (f K L))
    (Module.finrank K E) (by simpa [f_unramifiedExtension _ (f_pos _ _).ne'])
  unfold maximalUnramified
  generalize Nonempty.some _ = œÜ‚ÇÉ
  rw [‚Üê IntermediateField.toSubalgebra_le_toSubalgebra, AlgHom.fieldRange_toSubalgebra,
    ‚Üê AlgHom.fieldRange_of_normal (E := E) (œÜ‚ÇÉ.comp (œÜ‚ÇÇ.comp œÜ‚ÇÅ.symm)),
    AlgHom.fieldRange_toSubalgebra]
  exact AlgHom.range_comp_le_range ..

end IsNonarchimedeanLocalField
