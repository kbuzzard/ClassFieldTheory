/-
Copyright (c) 2025 Kenny Lau. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kenny Lau
-/

import ClassFieldTheory.LocalCFT.NonArchDef

/-! # Teichm√ºller character

Given complete discrete valuation ring `(R,m,k)` with finite residue field `k`, we define
Teichm√ºller character `k ‚Üí* R` (a monoid homomorphism, i.e. preserves only multiplication).

... but we don't have CDVR so we will just do it for local fields.
-/

section Lemmas

theorem Filter.HasBasis.comp_dense {Œ≤ Œπ Œπ' : Type*} [Preorder Œπ]
    {F : Filter Œ≤} {p : Œπ ‚Üí Prop} {p' : Œπ' ‚Üí Prop} {g : Œπ ‚Üí Set Œ≤} (b : F.HasBasis p g)
    (hg : Antitone g) (f : Œπ' ‚Üí Œπ) (hp : ‚àÄ ‚¶Éi'‚¶Ñ, p' i' ‚Üí p (f i'))
    (dense : ‚àÄ ‚¶Éi‚¶Ñ, p i ‚Üí ‚àÉ i', p' i' ‚àß i ‚â§ f i') :
    F.HasBasis p' (g ‚àò f) := by
  refine ‚ü®fun t ‚Ü¶ ?_‚ü©
  rw [b.mem_iff]
  refine ‚ü®fun ‚ü®i, hi1, hi2‚ü© ‚Ü¶ ?_, by aesop‚ü©
  obtain ‚ü®i', hi'1, hi'2‚ü© := dense hi1
  exact ‚ü®i', hi'1, (hg hi'2).trans hi2‚ü©

theorem Filter.HasBasis.comp_dense' {Œ≤ Œπ Œπ' : Type*} [Preorder Œπ]
    {F : Filter Œ≤} {p : Œπ ‚Üí Prop} {p' : Œπ' ‚Üí Prop} {g : Œπ ‚Üí Set Œ≤} (b : F.HasBasis p g)
    (hg : Monotone g) (f : Œπ' ‚Üí Œπ) (hp : ‚àÄ ‚¶Éi'‚¶Ñ, p' i' ‚Üí p (f i'))
    (dense : ‚àÄ ‚¶Éi‚¶Ñ, p i ‚Üí ‚àÉ i', p' i' ‚àß f i' ‚â§ i) :
    F.HasBasis p' (g ‚àò f) := by
  refine ‚ü®fun t ‚Ü¶ ?_‚ü©
  rw [b.mem_iff]
  refine ‚ü®fun ‚ü®i, hi1, hi2‚ü© ‚Ü¶ ?_, by aesop‚ü©
  obtain ‚ü®i', hi'1, hi'2‚ü© := dense hi1
  exact ‚ü®i', hi'1, (hg hi'2).trans hi2‚ü©

namespace WithZero

theorem exp_surj' {M : Type*} {y : M·µê‚Å∞} (hy : y ‚â† 0) :
    ‚àÉ x, exp x = y := by
  cases y <;> tauto

theorem exp_zero' {M : Type*} [Zero M] : exp (0 : M) = 1 := rfl

theorem exp_le_exp' {M : Type*} [LE M] {x y : M} : exp x ‚â§ exp y ‚Üî x ‚â§ y := WithZero.coe_le_coe

@[simp] theorem exp_le_one_iff {M : Type*} [LE M] [Zero M] {x : M} : exp x ‚â§ 1 ‚Üî x ‚â§ 0 :=
  exp_zero' (M := M) ‚ñ∏ exp_le_exp'

@[simp] theorem one_le_exp_iff {M : Type*} [LE M] [Zero M] {x : M} : 1 ‚â§ exp x ‚Üî 0 ‚â§ x :=
  exp_zero' (M := M) ‚ñ∏ exp_le_exp'

theorem lt_exp_iff {x : ‚Ñ§·µê‚Å∞} {n : ‚Ñ§} : x < exp n ‚Üî x ‚â§ exp (n - 1) := by
  cases x
  ¬∑ simp
  ¬∑ simp [-exp_sub, Int.le_sub_one_iff]

end WithZero

namespace OrderMonoidIso

variable {Œ± Œ≤ : Type*} [Preorder Œ±] [Preorder Œ≤] [Mul Œ±] [Mul Œ≤] (e : Œ± ‚âÉ*o Œ≤)

nonrec theorem surjective : (‚áëe).Surjective :=
  e.surjective

@[simp] theorem le_symm_apply {x y} : x ‚â§ e.symm y ‚Üî e x ‚â§ y :=
  e.toOrderIso.le_symm_apply

@[simp] theorem symm_apply_le {x y} : e.symm x ‚â§ y ‚Üî x ‚â§ e y :=
  e.toOrderIso.symm_apply_le

@[simp] theorem lt_symm_apply {x y} : x < e.symm y ‚Üî e x < y :=
  e.toOrderIso.lt_symm_apply

@[simp] theorem symm_apply_lt {x y} : e.symm x < y ‚Üî x < e y :=
  e.toOrderIso.symm_apply_lt

end OrderMonoidIso

@[simp] theorem Valuation.algebraMap_integer_coe {R Œì‚ÇÄ : Type*} [CommRing R]
    [LinearOrderedCommGroupWithZero Œì‚ÇÄ] (v : Valuation R Œì‚ÇÄ) :
    ‚áë(algebraMap v.integer R) = (‚Üë) :=
  rfl

instance IsUniformAddGroup.addSubgroupClass {Œ± S : Type*} [UniformSpace Œ±] [AddGroup Œ±]
    [SetLike S Œ±] [AddSubgroupClass S Œ±] [IsUniformAddGroup Œ±] (s : S) :
    IsUniformAddGroup s :=
  AddSubgroup.isUniformAddGroup <| .ofClass s

protected theorem Commute.add_pow_prime_pow_eq'
    {R : Type*} [Semiring R] {p : ‚Ñï} (hp : Nat.Prime p) {x y : R} (h : Commute x y) (n : ‚Ñï) :
    (x + y) ^ p ^ n = x ^ p ^ n + y ^ p ^ n + p * x * y * ‚àë k ‚àà Finset.Ioo 0 (p ^ n),
      x ^ (k - 1) * y ^ (p ^ n - k - 1) * ((p ^ n).choose k / p :) := by
  rw [h.add_pow_prime_pow_eq hp]
  congr 1
  simp_rw [mul_assoc, Finset.mul_sum]
  refine Finset.sum_congr rfl fun i hi ‚Ü¶ ?_
  rw [Finset.mem_Ioo] at hi
  rw [h.left_comm, ‚Üê mul_assoc x, ‚Üê pow_succ', (h.symm.pow_right _).left_comm, ‚Üê mul_assoc y,
    ‚Üê pow_succ', Nat.sub_add_cancel (by omega), Nat.sub_add_cancel (by omega)]

theorem add_pow_prime_pow_eq'
    {R : Type*} [CommSemiring R] {p : ‚Ñï} (hp : Nat.Prime p) (x y : R) (n : ‚Ñï) :
    (x + y) ^ p ^ n = x ^ p ^ n + y ^ p ^ n + p * x * y * ‚àë k ‚àà Finset.Ioo 0 (p ^ n),
      x ^ (k - 1) * y ^ (p ^ n - k - 1) * ((p ^ n).choose k / p :) :=
  (Commute.all x y).add_pow_prime_pow_eq' hp _

theorem exists_add_pow_prime_pow_eq'
    {R : Type*} [CommSemiring R] {p : ‚Ñï} (hp : Nat.Prime p) (x y : R) (n : ‚Ñï) :
    ‚àÉ r, (x + y) ^ p ^ n = x ^ p ^ n + y ^ p ^ n + p * x * y * r :=
  ‚ü®_, add_pow_prime_pow_eq' hp x y n‚ü©

theorem CharP.mem {R : Type*} [CommRing R] (p : ‚Ñï) (I : Ideal R) [CharP (R ‚ß∏ I) p] :
    (p : R) ‚àà I :=
  Ideal.Quotient.eq_zero_iff_mem.mp <| by simp

theorem FiniteField.pow_natCard {K : Type*} [GroupWithZero K] [Finite K] (a : K) :
    a ^ Nat.card K = a := by
  have := Fintype.ofFinite K
  rw [Nat.card_eq_fintype_card, pow_card]

namespace IsValuativeTopology

variable {K : Type*} [Field K] [ValuativeRel K] [TopologicalSpace K] [IsValuativeTopology K]

open ValuativeRel


end IsValuativeTopology

end Lemmas



namespace IsNonarchimedeanLocalField

open ValuativeRel

variable {K : Type*} [Field K] [ValuativeRel K]

section TopologicalSpace
variable [TopologicalSpace K] [IsNonarchimedeanLocalField K]

theorem map_irreducible {œñ : ùí™[K]} (hœñ : Irreducible œñ) :
    valuation K œñ = (valueGroupWithZeroIsoInt K).symm (.exp (-1)) :=
  (OrderMonoidIso.eq_symm_apply _).mpr <| valueGroupWithZeroIsoInt_irreducible hœñ

theorem mem_maximalIdeal_pow_iff {x : ùí™[K]} {n : ‚Ñï} :
    x ‚àà ùìÇ[K] ^ n ‚Üî valuation K x ‚â§ (valueGroupWithZeroIsoInt K).symm (.exp (-n)) := by
  obtain ‚ü®œñ, hœñ‚ü© := IsDiscreteValuationRing.exists_irreducible ùí™[K]
  rw [(IsDiscreteValuationRing.irreducible_iff_uniformizer _).mp hœñ, Ideal.span_singleton_pow,
    Ideal.mem_span_singleton, ‚Üê Valuation.Integers.le_iff_dvd (Valuation.integer.integers _),
    map_pow, map_pow, Valuation.algebraMap_integer_coe, map_irreducible hœñ, ‚Üê map_pow,
    ‚Üê WithZero.exp_nsmul]
  simp

variable (K) in
theorem hasBasis_nhds : (nhds (0 : K)).HasBasis (fun _n : ‚Ñï ‚Ü¶ True)
    fun n ‚Ü¶ {p | valuation K p < (valueGroupWithZeroIsoInt K).symm (.exp (-n))} :=
  (IsValuativeTopology.hasBasis_nhds_zero' K).comp_dense'
    (fun _ _ h‚ÇÅ _ h‚ÇÇ ‚Ü¶ h‚ÇÇ.trans_le h‚ÇÅ)
    _ (by aesop) <| by
  refine (valueGroupWithZeroIsoInt K).symm.surjective.forall.mpr fun x hx ‚Ü¶ ?_
  rw [map_ne_zero] at hx
  obtain ‚ü®x, rfl‚ü© := WithZero.exp_surj' hx
  obtain ‚ü®n, rfl | rfl‚ü© := x.eq_nat_or_neg
  ¬∑ exact ‚ü®0, by simp‚ü©
  ¬∑ aesop

variable (K) in
theorem hasBasis_nhds_integer : (nhds (0 : ùí™[K])).HasBasis (fun _n : ‚Ñï ‚Ü¶ True)
    fun n ‚Ü¶ {p | p ‚àà ùìÇ[K] ^ (n + 1)} := by
  convert (hasBasis_nhds K).comap Subtype.val using 1
  ¬∑ exact nhds_subtype ..
  ext n x
  simp only [SetLike.setOf_mem_eq, SetLike.mem_coe, Set.preimage_setOf_eq, Set.mem_setOf_eq]
  rw [mem_maximalIdeal_pow_iff, OrderMonoidIso.lt_symm_apply, WithZero.lt_exp_iff,
    OrderMonoidIso.le_symm_apply, Nat.cast_succ]
  ring_nf

theorem isClosed_closedBall' (x : ùí™[K]) (n : ‚Ñï) :
    IsClosed {y | y - x ‚àà ùìÇ[K] ^ n} := by
  convert (IsValuativeTopology.isClosed_closedBall
    ((valueGroupWithZeroIsoInt K).symm (.exp (-n)))).preimage_val.preimage
      (continuous_sub_right x) using 1
  ext y
  simp [mem_maximalIdeal_pow_iff]

/-- The sequence `x ^ q ^ n` that defines the Teichmuller character. -/
@[simps] noncomputable def teichmullerSeq : ùí™[K] ‚Üí* (‚Ñï ‚Üí ùí™[K]) where
  toFun x n := x ^ Nat.card ùìÄ[K] ^ n
  map_one' := by ext; simp
  map_mul' := by intros; ext; simp [mul_pow]

-- a lemma to prove that `teichmullerSeq` is Cauchy
theorem pow_sub_pow_mem {a b : ùí™[K]} {i : ‚Ñï} (hi : i ‚â† 0) (h : a - b ‚àà ùìÇ[K] ^ i) :
    a ^ Nat.card ùìÄ[K] - b ^ Nat.card ùìÄ[K] ‚àà ùìÇ[K] ^ (i + 1) := by
  have h‚ÇÅ : 1 ‚â§ i := by grind
  let := Fintype.ofFinite ùìÄ[K]
  obtain ‚ü®p, hCp, ‚ü®n, hn‚ÇÄ‚ü©, hp, hn : _ = _ ^ n‚ü© := FiniteField.card' ùìÄ[K]
  have h‚ÇÇ : 2 ‚â§ p ^ n := Nat.succ_le.mpr <| one_lt_pow' hp.one_lt hn‚ÇÄ.ne'
  obtain ‚ü®r, hr‚ü© := exists_add_pow_prime_pow_eq' hp (a - b) b n
  rw [Nat.card_eq_fintype_card, hn, show a = a - b + b by abel, hr, add_right_comm,
    add_sub_cancel_right, mul_assoc _ b]
  refine add_mem ?_ ?_
  ¬∑ have := Ideal.pow_mem_pow h (p ^ (n : ‚Ñï))
    rw [‚Üê pow_mul] at this
    exact Ideal.pow_le_pow_right (add_one_le_two_mul h‚ÇÅ |>.trans
      (mul_comm i 2 ‚ñ∏ mul_right_mono h‚ÇÇ)) this
  ¬∑ rw [pow_succ']
    have : CharP (ùí™[K] ‚ß∏ IsLocalRing.maximalIdeal ùí™[K]) p := hCp
    refine Ideal.mul_mem_right _ _ (Ideal.mul_mem_mul (CharP.mem _ _) h)

-- another lemma
theorem pow_card_sub_mem (x : ùí™[K]) : x ^ Nat.card ùìÄ[K] - x ‚àà ùìÇ[K] := by
  rw [‚Üê Ideal.Quotient.eq_zero_iff_mem, map_sub, map_pow]
  change algebraMap _ ùìÄ[K] x ^ Nat.card ùìÄ[K] - algebraMap _ ùìÄ[K] x = 0
  rw [FiniteField.pow_natCard, sub_self]

theorem teichmullerSeq_succ_sub_mem (x : ùí™[K]) (i : ‚Ñï) :
    teichmullerSeq x (i + 1) - teichmullerSeq x i ‚àà ùìÇ[K] ^ (i + 1) := by
  induction i with
  | zero =>
    simp_rw [teichmullerSeq_apply, zero_add, pow_zero, pow_one]
    exact pow_card_sub_mem x
  | succ i ih =>
    convert pow_sub_pow_mem _ ih using 2 <;> simp [teichmullerSeq, pow_succ, pow_mul]

theorem teichmullerSeq_add_sub_mem (x : ùí™[K]) (n i : ‚Ñï) :
    teichmullerSeq x (n + i) - teichmullerSeq x n ‚àà ùìÇ[K] ^ (n + 1) := by
  rw [show teichmullerSeq x n = teichmullerSeq x (n + 0) by simp,
    ‚Üê Finset.sum_range_sub (teichmullerSeq x <| n + ¬∑) i]
  refine sum_mem fun j hj ‚Ü¶ ?_
  exact Ideal.pow_le_pow_right (by rw [Nat.add_eq]; omega) <| teichmullerSeq_succ_sub_mem x _

theorem teichmullerSeq_sub_teichmullerSeq_mem {x y : ùí™[K]} (h : x - y ‚àà ùìÇ[K]) (n : ‚Ñï) :
    teichmullerSeq x n - teichmullerSeq y n ‚àà ùìÇ[K] ^ (n + 1) := by
  induction n with
  | zero => simpa [teichmullerSeq] using h
  | succ n ih =>
    simp_rw [teichmullerSeq_apply, pow_succ _ n, pow_mul, ‚Üê teichmullerSeq_apply]
    exact pow_sub_pow_mem (by omega) ih

end TopologicalSpace

section UniformSpace
variable [UniformSpace K] [IsUniformAddGroup K] [IsNonarchimedeanLocalField K]

variable (K) in
theorem hasBasis_uniformity : (uniformity ùí™[K]).HasBasis (fun _n : ‚Ñï ‚Ü¶ True)
    fun n ‚Ü¶ {p | p.1 - p.2 ‚àà ùìÇ[K] ^ (n + 1)} :=
  .uniformity_of_nhds_zero_swapped <| hasBasis_nhds_integer K

theorem cauchySeq_iff' (a : ‚Ñï ‚Üí ùí™[K]) :
    CauchySeq a ‚Üî ‚àÄ i : ‚Ñï, ‚àÉ N, ‚àÄ n ‚â• N, a n - a N ‚àà ùìÇ[K] ^ (i + 1) := by
  convert (hasBasis_uniformity K).cauchySeq_iff' (Œ≤ := ‚Ñï); simp

theorem cauchySeq_iff (a : ‚Ñï ‚Üí ùí™[K]) :
    CauchySeq a ‚Üî ‚àÄ i : ‚Ñï, ‚àÉ N, ‚àÄ n ‚â• N, a n - a N ‚àà ùìÇ[K] ^ i := by
  rw [cauchySeq_iff']
  refine ‚ü®fun h i ‚Ü¶ ?_, by aesop‚ü©
  obtain _ | i := i
  ¬∑ simp
  ¬∑ exact h i

-- special case
theorem cauchySeq_of_succ {a : ‚Ñï ‚Üí ùí™[K]} (ha : ‚àÄ i, a (i + 1) - a i ‚àà ùìÇ[K] ^ (i + 1)) :
    CauchySeq a := by
  refine (cauchySeq_iff' a).mpr fun i ‚Ü¶ ‚ü®i, fun n hn ‚Ü¶ ?_‚ü©
  obtain ‚ü®n, rfl‚ü© := le_iff_exists_add.mp hn
  rw [show a i = a (i + 0) by simp, ‚Üê Finset.sum_range_sub (a <| i + ¬∑) n]
  refine sum_mem fun c _ ‚Ü¶ ?_
  exact Ideal.pow_le_pow_right (by rw [Nat.add_eq]; omega) <| ha _

theorem cauchySeq_teichmuller (x : ùí™[K]) : CauchySeq (teichmullerSeq x) :=
  cauchySeq_of_succ fun i ‚Ü¶ teichmullerSeq_succ_sub_mem x i

end UniformSpace

section TopologicalSpace
variable [TopologicalSpace K] [IsNonarchimedeanLocalField K]

theorem limUnder_teichmullerSeq_mem (x : ùí™[K]) (n : ‚Ñï) :
    limUnder .atTop (teichmullerSeq x) - teichmullerSeq x n ‚àà ùìÇ[K] ^ (n + 1) := by
  refine IsClosed.mem_of_frequently_of_tendsto (f := teichmullerSeq x) (b := .atTop)
    (s := {y | y - teichmullerSeq x n ‚àà IsLocalRing.maximalIdeal ‚Ü•ùí™[K] ^ (n + 1)})
    (isClosed_closedBall' _ _) ?_ ?_
  ¬∑ rw [Filter.frequently_atTop]
    refine fun i ‚Ü¶ ‚ü®n+i, i.le_add_left n, teichmullerSeq_add_sub_mem x n i‚ü©
  ¬∑ letI := IsTopologicalAddGroup.toUniformSpace K
    haveI := isUniformAddGroup_of_addCommGroup (G := K)
    exact (cauchySeq_teichmuller x).tendsto_limUnder

theorem ext_maximalIdeal {x y : ùí™[K]} (h : ‚àÄ n, x - y ‚àà ùìÇ[K] ^ n) : x = y :=
  sub_eq_zero.mp <| Ideal.mem_bot.mp <| Ideal.iInf_pow_eq_bot_of_isDomain ùìÇ[K]
    (Ideal.IsMaximal.ne_top inferInstance) ‚ñ∏ Ideal.mem_iInf.mpr h

theorem ext_maximalIdeal' {x y : ùí™[K]} (h : ‚àÄ n, x - y ‚àà ùìÇ[K] ^ (n + 1)) : x = y :=
  ext_maximalIdeal fun n ‚Ü¶ n.casesOn (by simp) h

/-- The Teichm√ºller character `ùìÄ[K] ‚Üí* ùí™[K]`. -/
noncomputable def teichmuller' : ùìÄ[K] ‚Üí* ùí™[K] where
  toFun x := Quotient.liftOn x (limUnder .atTop <| teichmullerSeq ¬∑) fun x‚ÇÅ x‚ÇÇ hx ‚Ü¶ by
    refine ext_maximalIdeal' fun n ‚Ü¶ ?_
    have h‚ÇÅ := limUnder_teichmullerSeq_mem x‚ÇÅ n
    have h‚ÇÇ := limUnder_teichmullerSeq_mem x‚ÇÇ n
    have h‚ÇÉ := teichmullerSeq_sub_teichmullerSeq_mem ((Submodule.quotientRel_def _).mp hx) n
    have := add_mem (sub_mem h‚ÇÅ h‚ÇÇ) h‚ÇÉ
    ring_nf at this ‚ä¢
    exact this
  map_one' := Filter.Tendsto.limUnder_eq <| by simp [Pi.one_def]
  map_mul' x y := Quotient.inductionOn‚ÇÇ x y fun x y ‚Ü¶ by
    letI := IsTopologicalAddGroup.toUniformSpace K
    haveI := isUniformAddGroup_of_addCommGroup (G := K)
    change limUnder _ _ = limUnder _ _ * limUnder _ _
    rw [map_mul]
    exact Filter.Tendsto.limUnder_eq <| .mul
      (cauchySeq_teichmuller x).tendsto_limUnder (cauchySeq_teichmuller y).tendsto_limUnder

end TopologicalSpace

end IsNonarchimedeanLocalField
