/-
Copyright (c) 2025 Kenny Lau. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kenny Lau
-/

import ClassFieldTheory.LocalCFT.NonArchDef
import ClassFieldTheory.Mathlib.Algebra.CharP.Lemmas
import ClassFieldTheory.Mathlib.Algebra.CharP.Quotient
import ClassFieldTheory.Mathlib.Algebra.Order.Hom.Monoid
import ClassFieldTheory.Mathlib.FieldTheory.Finite.Basic
import ClassFieldTheory.Mathlib.Order.Filter.Bases.Monotone
import ClassFieldTheory.Mathlib.Topology.Algebra.IsUniformGroup.Basic

/-! # TeichmÃ¼ller character

Given complete discrete valuation ring `(R,m,k)` with finite residue field `k`, we define
TeichmÃ¼ller character `k â†’* R` (a monoid homomorphism, i.e. preserves only multiplication).

... but we don't have CDVR so we will just do it for local fields.
-/

namespace IsNonarchimedeanLocalField

open ValuativeRel

variable {K : Type*} [Field K] [ValuativeRel K]

section TopologicalSpace
variable [TopologicalSpace K] [IsNonarchimedeanLocalField K]

theorem map_irreducible {Ï– : ğ’ª[K]} (hÏ– : Irreducible Ï–) :
    valuation K Ï– = (valueGroupWithZeroIsoInt K).symm (.exp (-1)) :=
  (OrderMonoidIso.eq_symm_apply _).mpr <| valueGroupWithZeroIsoInt_irreducible hÏ–

theorem mem_maximalIdeal_pow_iff {x : ğ’ª[K]} {n : â„•} :
    x âˆˆ ğ“‚[K] ^ n â†” valuation K x â‰¤ (valueGroupWithZeroIsoInt K).symm (.exp (-n)) := by
  obtain âŸ¨Ï–, hÏ–âŸ© := IsDiscreteValuationRing.exists_irreducible ğ’ª[K]
  rw [(IsDiscreteValuationRing.irreducible_iff_uniformizer _).mp hÏ–, Ideal.span_singleton_pow,
    Ideal.mem_span_singleton, â† Valuation.Integers.le_iff_dvd (Valuation.integer.integers _),
    map_pow, map_pow, Valuation.algebraMap_integer_coe, map_irreducible hÏ–, â† map_pow,
    â† WithZero.exp_nsmul]
  simp

variable (K) in
theorem hasBasis_nhds : (nhds (0 : K)).HasBasis (fun _n : â„• â†¦ True)
    fun n â†¦ {p | valuation K p < (valueGroupWithZeroIsoInt K).symm (.exp (-n))} :=
  (IsValuativeTopology.hasBasis_nhds_zero' K).comp_dense'
    (fun _ _ hâ‚ _ hâ‚‚ â†¦ hâ‚‚.trans_le hâ‚)
    _ (by aesop) <| by
  refine (valueGroupWithZeroIsoInt K).symm.surjective.forall.mpr fun x hx â†¦ ?_
  rw [map_ne_zero] at hx
  obtain âŸ¨x, rflâŸ© := WithZero.exp_surj' hx
  obtain âŸ¨n, rfl | rflâŸ© := x.eq_nat_or_neg
  Â· exact âŸ¨0, by simpâŸ©
  Â· aesop

variable (K) in
theorem hasBasis_nhds_integer : (nhds (0 : ğ’ª[K])).HasBasis (fun _n : â„• â†¦ True)
    fun n â†¦ {p | p âˆˆ ğ“‚[K] ^ (n + 1)} := by
  convert (hasBasis_nhds K).comap Subtype.val using 1
  Â· exact nhds_subtype ..
  ext n x
  simp only [SetLike.setOf_mem_eq, SetLike.mem_coe, Set.preimage_setOf_eq, Set.mem_setOf_eq]
  rw [mem_maximalIdeal_pow_iff, OrderMonoidIso.lt_symm_apply, WithZero.lt_exp_iff,
    OrderMonoidIso.le_symm_apply, Nat.cast_succ]
  ring_nf

theorem isClosed_closedBall' (x : ğ’ª[K]) (n : â„•) :
    IsClosed {y | y - x âˆˆ ğ“‚[K] ^ n} := by
  convert (IsValuativeTopology.isClosed_closedBall
    ((valueGroupWithZeroIsoInt K).symm (.exp (-n)))).preimage_val.preimage
      (continuous_sub_right x) using 1
  ext y
  simp [mem_maximalIdeal_pow_iff]

/-- The sequence `x ^ q ^ n` that defines the Teichmuller character. -/
@[simps] noncomputable def teichmullerSeq : ğ’ª[K] â†’*â‚€ (â„• â†’ ğ’ª[K]) where
  toFun x n := x ^ Nat.card ğ“€[K] ^ n
  map_zero' := by ext; simp [Nat.card_pos.ne']
  map_one' := by ext; simp
  map_mul' := by intros; ext; simp [mul_pow]

-- a lemma to prove that `teichmullerSeq` is Cauchy
theorem pow_sub_pow_mem {a b : ğ’ª[K]} {i : â„•} (hi : i â‰  0) (h : a - b âˆˆ ğ“‚[K] ^ i) :
    a ^ Nat.card ğ“€[K] - b ^ Nat.card ğ“€[K] âˆˆ ğ“‚[K] ^ (i + 1) := by
  have hâ‚ : 1 â‰¤ i := by grind
  let := Fintype.ofFinite ğ“€[K]
  obtain âŸ¨p, hCp, âŸ¨n, hnâ‚€âŸ©, hp, hn : _ = _ ^ nâŸ© := FiniteField.card' ğ“€[K]
  have hâ‚‚ : 2 â‰¤ p ^ n := Nat.succ_le.mpr <| one_lt_pow' hp.one_lt hnâ‚€.ne'
  obtain âŸ¨r, hrâŸ© := exists_add_pow_prime_pow_eq' hp (a - b) b n
  rw [Nat.card_eq_fintype_card, hn, show a = a - b + b by abel, hr, add_right_comm,
    add_sub_cancel_right, mul_assoc _ b]
  refine add_mem ?_ ?_
  Â· have := Ideal.pow_mem_pow h (p ^ (n : â„•))
    rw [â† pow_mul] at this
    exact Ideal.pow_le_pow_right (add_one_le_two_mul hâ‚ |>.trans
      (mul_comm i 2 â–¸ mul_right_mono hâ‚‚)) this
  Â· rw [pow_succ']
    have : CharP (ğ’ª[K] â§¸ IsLocalRing.maximalIdeal ğ’ª[K]) p := hCp
    refine Ideal.mul_mem_right _ _ (Ideal.mul_mem_mul (CharP.mem _ _) h)

-- another lemma
theorem pow_card_sub_mem (x : ğ’ª[K]) : x ^ Nat.card ğ“€[K] - x âˆˆ ğ“‚[K] := by
  rw [â† Ideal.Quotient.eq_zero_iff_mem, map_sub, map_pow]
  change algebraMap _ ğ“€[K] x ^ Nat.card ğ“€[K] - algebraMap _ ğ“€[K] x = 0
  rw [FiniteField.pow_natCard, sub_self]

theorem teichmullerSeq_succ_sub_mem (x : ğ’ª[K]) (i : â„•) :
    teichmullerSeq x (i + 1) - teichmullerSeq x i âˆˆ ğ“‚[K] ^ (i + 1) := by
  induction i with
  | zero =>
    simp_rw [teichmullerSeq_apply, zero_add, pow_zero, pow_one]
    exact pow_card_sub_mem x
  | succ i ih =>
    convert pow_sub_pow_mem _ ih using 2 <;> simp [teichmullerSeq, pow_succ, pow_mul]

theorem teichmullerSeq_add_sub_mem (x : ğ’ª[K]) (n i : â„•) :
    teichmullerSeq x (n + i) - teichmullerSeq x n âˆˆ ğ“‚[K] ^ (n + 1) := by
  rw [show teichmullerSeq x n = teichmullerSeq x (n + 0) by simp,
    â† Finset.sum_range_sub (teichmullerSeq x <| n + Â·) i]
  refine sum_mem fun j hj â†¦ ?_
  exact Ideal.pow_le_pow_right (by rw [Nat.add_eq]; omega) <| teichmullerSeq_succ_sub_mem x _

theorem teichmullerSeq_sub_teichmullerSeq_mem {x y : ğ’ª[K]} (h : x - y âˆˆ ğ“‚[K]) (n : â„•) :
    teichmullerSeq x n - teichmullerSeq y n âˆˆ ğ“‚[K] ^ (n + 1) := by
  induction n with
  | zero => simpa [teichmullerSeq] using h
  | succ n ih =>
    simp_rw [teichmullerSeq_apply, pow_succ _ n, pow_mul, â† teichmullerSeq_apply]
    exact pow_sub_pow_mem (by omega) ih

end TopologicalSpace

section UniformSpace
variable [UniformSpace K] [IsUniformAddGroup K] [IsNonarchimedeanLocalField K]

variable (K) in
theorem hasBasis_uniformity : (uniformity ğ’ª[K]).HasBasis (fun _n : â„• â†¦ True)
    fun n â†¦ {p | p.1 - p.2 âˆˆ ğ“‚[K] ^ (n + 1)} :=
  .uniformity_of_nhds_zero_swapped <| hasBasis_nhds_integer K

theorem cauchySeq_iff' (a : â„• â†’ ğ’ª[K]) :
    CauchySeq a â†” âˆ€ i : â„•, âˆƒ N, âˆ€ n â‰¥ N, a n - a N âˆˆ ğ“‚[K] ^ (i + 1) := by
  convert (hasBasis_uniformity K).cauchySeq_iff' (Î² := â„•); simp

theorem cauchySeq_iff (a : â„• â†’ ğ’ª[K]) :
    CauchySeq a â†” âˆ€ i : â„•, âˆƒ N, âˆ€ n â‰¥ N, a n - a N âˆˆ ğ“‚[K] ^ i := by
  rw [cauchySeq_iff']
  refine âŸ¨fun h i â†¦ ?_, by aesopâŸ©
  obtain _ | i := i
  Â· simp
  Â· exact h i

-- special case
theorem cauchySeq_of_succ {a : â„• â†’ ğ’ª[K]} (ha : âˆ€ i, a (i + 1) - a i âˆˆ ğ“‚[K] ^ (i + 1)) :
    CauchySeq a := by
  refine (cauchySeq_iff' a).mpr fun i â†¦ âŸ¨i, fun n hn â†¦ ?_âŸ©
  obtain âŸ¨n, rflâŸ© := le_iff_exists_add.mp hn
  rw [show a i = a (i + 0) by simp, â† Finset.sum_range_sub (a <| i + Â·) n]
  refine sum_mem fun c _ â†¦ ?_
  exact Ideal.pow_le_pow_right (by rw [Nat.add_eq]; omega) <| ha _

theorem cauchySeq_teichmuller (x : ğ’ª[K]) : CauchySeq (teichmullerSeq x) :=
  cauchySeq_of_succ fun i â†¦ teichmullerSeq_succ_sub_mem x i

end UniformSpace

section TopologicalSpace
variable [TopologicalSpace K] [IsNonarchimedeanLocalField K]

theorem limUnder_teichmullerSeq_mem (x : ğ’ª[K]) (n : â„•) :
    limUnder .atTop (teichmullerSeq x) - teichmullerSeq x n âˆˆ ğ“‚[K] ^ (n + 1) := by
  refine IsClosed.mem_of_frequently_of_tendsto (f := teichmullerSeq x) (b := .atTop)
    (s := {y | y - teichmullerSeq x n âˆˆ IsLocalRing.maximalIdeal â†¥ğ’ª[K] ^ (n + 1)})
    (isClosed_closedBall' _ _) ?_ ?_
  Â· rw [Filter.frequently_atTop]
    refine fun i â†¦ âŸ¨n+i, i.le_add_left n, teichmullerSeq_add_sub_mem x n iâŸ©
  Â· letI := IsTopologicalAddGroup.toUniformSpace K
    haveI := isUniformAddGroup_of_addCommGroup (G := K)
    exact (cauchySeq_teichmuller x).tendsto_limUnder

theorem ext_maximalIdeal {x y : ğ’ª[K]} (h : âˆ€ n, x - y âˆˆ ğ“‚[K] ^ n) : x = y :=
  sub_eq_zero.mp <| Ideal.mem_bot.mp <| Ideal.iInf_pow_eq_bot_of_isDomain ğ“‚[K]
    (Ideal.IsMaximal.ne_top inferInstance) â–¸ Ideal.mem_iInf.mpr h

theorem ext_maximalIdeal' {x y : ğ’ª[K]} (h : âˆ€ n, x - y âˆˆ ğ“‚[K] ^ (n + 1)) : x = y :=
  ext_maximalIdeal fun n â†¦ n.casesOn (by simp) h

variable (K) in
/-- The TeichmÃ¼ller character `ğ“€[K] â†’*â‚€ ğ’ª[K]`. -/
noncomputable def teichmuller' : ğ“€[K] â†’*â‚€ ğ’ª[K] where
  toFun x := Quotient.liftOn x (limUnder .atTop <| teichmullerSeq Â·) fun xâ‚ xâ‚‚ hx â†¦ by
    refine ext_maximalIdeal' fun n â†¦ ?_
    have hâ‚ := limUnder_teichmullerSeq_mem xâ‚ n
    have hâ‚‚ := limUnder_teichmullerSeq_mem xâ‚‚ n
    have hâ‚ƒ := teichmullerSeq_sub_teichmullerSeq_mem ((Submodule.quotientRel_def _).mp hx) n
    have := add_mem (sub_mem hâ‚ hâ‚‚) hâ‚ƒ
    ring_nf at this âŠ¢
    exact this
  map_zero' := Filter.Tendsto.limUnder_eq <| by simp [Pi.zero_def]
  map_one' := Filter.Tendsto.limUnder_eq <| by simp [Pi.one_def]
  map_mul' x y := Quotient.inductionOnâ‚‚ x y fun x y â†¦ by
    letI := IsTopologicalAddGroup.toUniformSpace K
    haveI := isUniformAddGroup_of_addCommGroup (G := K)
    change limUnder _ _ = limUnder _ _ * limUnder _ _
    rw [map_mul]
    exact Filter.Tendsto.limUnder_eq <| .mul
      (cauchySeq_teichmuller x).tendsto_limUnder (cauchySeq_teichmuller y).tendsto_limUnder

theorem teichmuller'_def (x : ğ’ª[K]) :
    Filter.Tendsto (teichmullerSeq x) .atTop (nhds <| teichmuller' K <| Ideal.Quotient.mk _ x) := by
  letI := IsTopologicalAddGroup.toUniformSpace K
  haveI := isUniformAddGroup_of_addCommGroup (G := K)
  exact (cauchySeq_teichmuller x).tendsto_limUnder

theorem residue_teichmuller' (x : ğ“€[K]) :
    IsLocalRing.residue ğ’ª[K] (teichmuller' K x) = x :=
  Quotient.inductionOn x fun x â†¦ (Ideal.Quotient.mk_eq_mk_iff_sub_mem _ _).mpr <| by
    convert limUnder_teichmullerSeq_mem x 0 <;> simp

theorem residue_comp_teichmuller' :
    (IsLocalRing.residue ğ’ª[K] : ğ’ª[K] â†’*â‚€ ğ“€[K]).comp (teichmuller' K) = .id _ :=
  MonoidWithZeroHom.ext residue_teichmuller'

theorem leftInverse_teichmuller' :
    Function.LeftInverse (IsLocalRing.residue ğ’ª[K]) (teichmuller' K) :=
  residue_teichmuller'

theorem teichmuller'_injective : Function.Injective (teichmuller' K) :=
  leftInverse_teichmuller'.injective

variable (K) in
/-- The TeichmÃ¼ller character `ğ“€[K] â†’*â‚€ K`. -/
noncomputable def teichmuller : ğ“€[K] â†’*â‚€ K :=
  (algebraMap ğ’ª[K] K : ğ’ª[K] â†’*â‚€ K).comp <| teichmuller' K

theorem teichmuller_def (x : ğ’ª[K]) :
    Filter.Tendsto (fun n â†¦ (teichmullerSeq x n : K)) .atTop
      (nhds <| teichmuller K <| Ideal.Quotient.mk _ x) :=
  (continuous_subtype_val.tendsto _).comp <| teichmuller'_def x

theorem teichmuller_eq_teichmuller' (x : ğ“€[K]) :
    teichmuller K x = teichmuller' K x :=
  rfl

theorem teichmuller_injective : Function.Injective (teichmuller K) :=
  fun _ _ h â†¦ teichmuller'_injective <| Subtype.ext h

end TopologicalSpace

end IsNonarchimedeanLocalField
